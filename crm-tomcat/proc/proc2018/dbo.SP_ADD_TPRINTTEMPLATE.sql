USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_ADD_TPRINTTEMPLATE @IN_TEMPLATE_CODE        NVARCHAR(24),   --模板代码
                                       @IN_TEMPLATE_NAME        NVARCHAR(60),   --模板名称
                                       @IN_CATALOG_CODE         NVARCHAR(24),   --分类代码
                                       @IN_TEMPLATE_CONTENT     NVARCHAR(MAX),  --模板内容
                                       @IN_SUMMARY              NVARCHAR(512),  --备注
                                       @IN_INPUT_MAN            INTEGER         --操作员

WITH ENCRYPTION 
AS
    DECLARE @V_CATALOG_NAME NVARCHAR(60)
    DECLARE @V_RET_CODE INT,@IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)
    SET @V_RET_CODE = -99999000
    SET @IBUSI_FLAG = 99999
    SET @SBUSI_NAME = '新增打印模板'
    
    BEGIN TRY
        IF NOT EXISTS(SELECT 1 FROM TPRINTCATALOG WHERE CATALOG_CODE = @IN_CATALOG_CODE)
            RAISERROR('模板分类信息不存在！',16,1)
            
        SELECT @V_CATALOG_NAME = CATALOG_NAME FROM TPRINTCATALOG WHERE CATALOG_CODE = @IN_CATALOG_CODE
        
        BEGIN TRANSACTION

        INSERT INTO TPRINTTEMPLATE(TEMPLATE_CODE,TEMPLATE_NAME,CATALOG_CODE,TEMPLATE_CONTENT,INPUT_MAN,SUMMARY)
            VALUES(@IN_TEMPLATE_CODE,@IN_TEMPLATE_NAME,@IN_CATALOG_CODE,@IN_TEMPLATE_CONTENT,@IN_INPUT_MAN,@IN_SUMMARY)
            
        SELECT @SSUMMARY = @SBUSI_NAME + '，分类代码：' + @IN_CATALOG_CODE + '，分类名称：' + @V_CATALOG_NAME
                                       + '，模板代码：' + @IN_TEMPLATE_CODE + '，模板名称：' + @IN_TEMPLATE_NAME
        INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
            VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
        
        COMMIT TRANSACTION
        RETURN 100
    END TRY
    
    BEGIN CATCH
        IF @@TRANCOUNT > 0 BEGIN
            ROLLBACK TRANSACTION
        END
        
        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)

        SELECT @V_ERROR_STR = N'Message:%s,Error:%d,Level:%d,State:%d,Procedure:%s,Line:%d',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()

        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_NUMBER,@V_ERROR_SEVERITY,@V_ERROR_STATE,
                  @V_ERROR_PROCEDURE,@V_ERROR_LINE)
        
        RETURN -100
    END CATCH
GO
