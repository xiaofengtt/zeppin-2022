USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_DEL_TQuesTopic     @IN_TOPIC_ID            INT,
                                       @IN_INPUT_MAN           INT = 0         --操作员
WITH ENCRYPTION
AS
    DECLARE @V_TOPIC_SERIAL_NO INTEGER, @V_QUES_ID INTEGER
    DECLARE @V_RET_CODE INT, @IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40), @SSUMMARY NVARCHAR(200)
    SELECT @V_RET_CODE = -30502000, @IBUSI_FLAG = 30502
    SELECT @SBUSI_NAME = N'删除问卷题目信息', @SSUMMARY = N'删除问卷题目信息'

    IF NOT EXISTS(SELECT 1 FROM TQuesTopic WHERE TOPIC_ID = @IN_TOPIC_ID)
        RETURN @V_RET_CODE - 1 --问卷信息不存在

    SELECT @V_TOPIC_SERIAL_NO = ISNULL(TOPIC_SERIAL_NO,0),@V_QUES_ID = ISNULL(QUES_ID,0)
    FROM TQuesTopic WHERE TOPIC_ID = @IN_TOPIC_ID

BEGIN TRANSACTION
    -- 删除问卷分值表中问卷相关信息
    DELETE FROM TTopicScore
    WHERE TOPIC_ID = @IN_TOPIC_ID
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END

    UPDATE TQUESTOPIC
    SET TOPIC_SERIAL_NO = TOPIC_SERIAL_NO - 1
    WHERE QUES_ID = @V_QUES_ID
        AND TOPIC_SERIAL_NO IN(
            SELECT TOPIC_SERIAL_NO
            FROM TQUESTOPIC
            WHERE QUES_ID = @V_QUES_ID
                AND TOPIC_SERIAL_NO > @V_TOPIC_SERIAL_NO
        )
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END

    -- 删除题库中的相关问卷题目
    DELETE FROM TQuesTopic
    WHERE TOPIC_ID = @IN_TOPIC_ID
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END

    SET @SSUMMARY = N'删除问卷题目信息，操作员：' + CAST(@IN_INPUT_MAN AS NVARCHAR(10))
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END

COMMIT TRANSACTION
    RETURN 100
GO
