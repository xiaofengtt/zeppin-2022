USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_ADD_TMONEYDATA_CRM  @IN_BOOK_CODE       INTEGER,            --账套
										@IN_PRODUCT_ID      INTEGER,            --产品ID
										@IN_SUB_PRODUCT_ID  INTEGER,            --子产品ID
										@IN_CUST_ID         INTEGER,            --客户ID
										@IN_DZ_DATE         INTEGER,            --到账日期
										@IN_JK_TYPE         NVARCHAR(10),       --缴款方式 1114
										@IN_TO_MONEY        DECIMAL(16,2),      --认购金额
										@IN_TO_AMOUNT       DECIMAL(16,2),      --份额
										@IN_SUMMARY         NVARCHAR(200),      --说明
										@IN_CITY_SERIAL_NO  INTEGER,            --资金来源地编号
										@IN_INPUT_MAN       INTEGER,             --操作员
										@IN_CUST_NAME       NVARCHAR (100) = '',
										@IN_CUST_TYPE       INT = 0,
										@IN_SBF_SERIAL_NO   INT = 0,             --入账银行ID
										@IN_PRE_SERIAL_NO   INT =0, --预约表主键(EFCRM..TPRECONTRACT.SERIAL_NO)
                                        @IN_DZ_TIME         DATETIME=NULL,     --到帐时间
                                        @IN_ONWAY_FLAG      INT = 0       --在途资金标志:1是
WITH ENCRYPTION
AS
    DECLARE @V_ERROR NVARCHAR(200),@IN_CARD_TYPE NVARCHAR(10),@V_CARD_TYPE_NAME NVARCHAR(30),@V_JK_TYPE_NAME NVARCHAR(30)
    DECLARE @V_PRODUCT_CODE NVARCHAR(6),@V_PRODUCT_NAME NVARCHAR(60),@V_SUB_FLAG INT,@V_SUB_FUND_FLAG INT
    DECLARE @V_CUST_NAME NVARCHAR(200),@V_CITY_NAME NVARCHAR(20),@V_CUST_TYPE_NAME NVARCHAR(10),@V_CUST_NO NVARCHAR(8),@V_MAX_ID INT
	DECLARE @V_CUST_ID2 INT, @V_MAX_ID2 INT, @V_CHECK_FLAG INT
    DECLARE @V_MENU_ID NVARCHAR(10),@V_TASK_CODE INT,@V_SYS_DATE INT,@V_SERIAL_NO INT,@V_OP_NAME NVARCHAR(10),
            @V_LOGIN_NAME NVARCHAR(100),@V_LOGIN_INFO NVARCHAR(1000)
    DECLARE @V_PRE_PRODUCT_ID INT
    DECLARE @V_RET_CODE INT,@IBUSI_FLAG INT,@SBUSI_NAME VARCHAR(40),@SSUMMARY VARCHAR(200)
    SELECT @V_RET_CODE = -31206000 ,@IBUSI_FLAG = 31206
    
    SET @IN_PRODUCT_ID=ISNULL(@IN_PRODUCT_ID,0)
    SET @IN_SUB_PRODUCT_ID=ISNULL(@IN_SUB_PRODUCT_ID,0)
    SET @IN_PRE_SERIAL_NO=ISNULL(@IN_PRE_SERIAL_NO,0)
    SET @IN_DZ_TIME=ISNULL(@IN_DZ_TIME,GETDATE())
    BEGIN TRY
    BEGIN TRANSACTION
    
    SELECT @SBUSI_NAME = N'认购资金录入' ,@SSUMMARY = N'认购资金录入'

    --1.信息验证
    IF ISNULL(@IN_TO_MONEY,0) = 0 BEGIN
        SET @V_ERROR = '资金无效！'
        RAISERROR(@V_ERROR,16,1)
    END
    
    --1.1.客户验证
    IF NOT EXISTS(SELECT 1 FROM TCustomers WHERE CUST_ID = @IN_CUST_ID) BEGIN
        --SET @V_ERROR = '无效的客户信息！'
        --RAISERROR(@V_ERROR,16,1)
        IF @IN_CUST_TYPE = 1 BEGIN
            SELECT @V_CUST_TYPE_NAME = N'个人'
            SELECT @IN_CARD_TYPE = '110801'
        END
        ELSE BEGIN
            SELECT @V_CUST_TYPE_NAME = N'机构'
            SELECT @IN_CARD_TYPE = '210801'
        END
        
        SELECT @V_CARD_TYPE_NAME = TYPE_CONTENT FROM TDICTPARAM  WHERE TYPE_VALUE = @IN_CARD_TYPE
        SELECT @IN_CUST_ID = ISNULL(MAX(CUST_ID),0)+ 1 FROM INTRUST..TCUSTOMERINFO
        SELECT @V_MAX_ID = ISNULL(MAX(LIST_ID),0)+ 1 FROM INTRUST..TCUSTOMERINFO WHERE BOOK_CODE = @IN_BOOK_CODE
		SELECT @V_CUST_ID2 = ISNULL(MAX(CUST_ID),0)+ 1 FROM TCustomers
		SELECT @V_MAX_ID2 = ISNULL(MAX(LIST_ID),0)+ 1 FROM TCustomers
		
		IF @V_CUST_ID2 > @IN_CUST_ID
			SET @IN_CUST_ID = @V_CUST_ID2
		IF @V_MAX_ID2 > @V_MAX_ID
			SET @V_MAX_ID = @V_MAX_ID2
			
        SELECT @V_CUST_NO = CONVERT(CHAR(8),@IN_CUST_TYPE * 10000000+@V_MAX_ID)
        
        --个人客户编号以0开头，机构以J开头
        IF @IN_CUST_TYPE = 1
            SELECT @V_CUST_NO = '0' + RIGHT(@V_CUST_NO,7)
        ELSE
            SELECT @V_CUST_NO = 'J' + RIGHT(@V_CUST_NO,7)
			
		INSERT INTO TCustomers(CUST_ID,CUST_NO,CUST_NAME,CARD_TYPE,CARD_TYPE_NAME,CARD_ID,CUST_TYPE,CUST_TYPE_NAME,
                                  WT_FLAG,WT_FLAG_NAME,CUST_SOURCE,CUST_SOURCE_NAME,INPUT_MAN,CHECK_FLAG,LIST_ID,IS_LINK,TOUCH_TYPE,TOUCH_TYPE_NAME)
            VALUES(@IN_CUST_ID,@V_CUST_NO,@IN_CUST_NAME,@IN_CARD_TYPE,@V_CARD_TYPE_NAME,@V_CUST_NO,@IN_CUST_TYPE,@V_CUST_TYPE_NAME,
                   1,N'是','111099',N'其他',@IN_INPUT_MAN,2,@V_MAX_ID,2,N'110901',N'电话')

        INSERT INTO INTRUST..TCUSTOMERINFO(BOOK_CODE,CUST_ID,CUST_NO,CUST_NAME,CARD_TYPE,CARD_TYPE_NAME,CARD_ID,CUST_TYPE,CUST_TYPE_NAME,
                                  WT_FLAG,WT_FLAG_NAME,CUST_SOURCE,CUST_SOURCE_NAME,INPUT_MAN,CHECK_FLAG,LIST_ID,IS_LINK)
            VALUES(@IN_BOOK_CODE,@IN_CUST_ID,@V_CUST_NO,@IN_CUST_NAME,@IN_CARD_TYPE,@V_CARD_TYPE_NAME,@V_CUST_NO,@IN_CUST_TYPE,@V_CUST_TYPE_NAME,
                   1,N'是','111099',N'其他',@IN_INPUT_MAN,2,@V_MAX_ID,2)
                   
        SELECT @V_CUST_NAME = @IN_CUST_NAME
    END
    ELSE BEGIN
        SELECT @V_CUST_NAME = CUST_NAME FROM INTRUST..TCUSTOMERINFO WHERE CUST_ID = @IN_CUST_ID
    END
    IF @IN_PRODUCT_ID=0 --预发行产品还未绑定时
    BEGIN
		--调用CRM的预发行产品预约过程
		EXEC SP_ADD_TPREMONEYDETAIL @IN_PRE_SERIAL_NO , --预约表主键(EFCRM..TPRECONTRACT.SERIAL_NO)
                                        @IN_DZ_TIME   , --到帐时间
                                        @IN_TO_MONEY  , --到帐金额
                                        @IN_JK_TYPE   , --缴款方式(1114)
                                        @IN_INPUT_MAN ,
                                        @IN_ONWAY_FLAG  --在途资金标志:1是
        RETURN
	END
    --1.2.客户产品
    SELECT @V_PRODUCT_CODE = PRODUCT_CODE,@V_PRODUCT_NAME = PRODUCT_NAME,
           @V_SUB_FLAG = ISNULL(SUB_FLAG,0),@V_SUB_FUND_FLAG = SUB_FUND_FLAG 
        FROM INTRUST..TPRODUCT WHERE PRODUCT_ID = @IN_PRODUCT_ID
    IF @@ROWCOUNT=0
    BEGIN
		SET @V_ERROR  = '产品不存在，请核对!提示：PRODUCT_ID='+CAST(@IN_PRODUCT_ID AS VARCHAR)
		RAISERROR(@V_ERROR,16,2)
    END
    IF @V_SUB_FLAG = 1 AND NOT EXISTS(SELECT 1 FROM INTRUST..TSUBPRODUCT WHERE PRODUCT_ID = @IN_PRODUCT_ID AND SUB_PRODUCT_ID = @IN_SUB_PRODUCT_ID) BEGIN
        SET @V_ERROR  = '无效的子产品!'
        RAISERROR(@V_ERROR,16,2)
    END

    IF EXISTS (SELECT * FROM TSYSCONTROL WHERE FLAG_TYPE='TMONEYDATA_INPUT_CHECK' AND VALUE=1) --录入要审核
		SET @V_CHECK_FLAG=11
	ELSE
		SET @V_CHECK_FLAG=1
    SELECT @V_JK_TYPE_NAME = TYPE_CONTENT FROM INTRUST..TDICTPARAM WHERE TYPE_VALUE = @IN_JK_TYPE
    SELECT @V_CITY_NAME = CITY_NAME FROM INTRUST..TPRODUCTCITY WHERE SERIAL_NO = @IN_CITY_SERIAL_NO

    INSERT INTO INTRUST..TMONEYDATA(BOOK_CODE,PRODUCT_ID,SUB_PRODUCT_ID,CUST_ID,TO_MONEY,CITY_SERIAL_NO,CITY_NAME,
                           JK_TYPE,JK_TYPE_NAME,TO_AMOUNT,DZ_DATE,SUMMARY,INPUT_MAN,SBF_SERIAL_NO,CUST_TYPE,CUST_NAME,CHECK_FLAG)
        VALUES(@IN_BOOK_CODE,@IN_PRODUCT_ID,@IN_SUB_PRODUCT_ID,@IN_CUST_ID,@IN_TO_MONEY,@IN_CITY_SERIAL_NO,@V_CITY_NAME,
               @IN_JK_TYPE,@V_JK_TYPE_NAME,@IN_TO_AMOUNT,@IN_DZ_DATE,@IN_SUMMARY,@IN_INPUT_MAN,@IN_SBF_SERIAL_NO,@IN_CUST_TYPE,@IN_CUST_NAME,@V_CHECK_FLAG)
    SET @V_SERIAL_NO = @@IDENTITY
    --到账录入时，更新客户预约的到账金额，因为在审核时，在业务系统执行
    SELECT @V_PRE_PRODUCT_ID=PREPRODUCT_ID FROM TPREPRODUCT WHERE BIND_PRODUCT_ID=@IN_PRODUCT_ID
    UPDATE TPRECONTRACT SET RG_MONEY=@IN_TO_MONEY+ISNULL(RG_MONEY,0),RG_DATE=@IN_DZ_DATE,FLOW_STATUS=3 WHERE CUST_ID=@IN_CUST_ID AND PREPRODUCT_ID=@V_PRE_PRODUCT_ID
    --2.工作提示信息
    SELECT @V_MENU_ID = '2130110'
    SELECT @V_TASK_CODE = TASK_CODE,@V_LOGIN_NAME = TASK_INFO FROM INTRUST..TTASKDICT WHERE TASK_CODE = 304
    SELECT @V_SYS_DATE = INTRUST.dbo.GETDATEINT(GETDATE())
    
    --2.1.关闭产品开立审核已通过，可以做认购合同录入的提示
    EXEC @V_RET_CODE = INTRUST..SP_INNER_CLOSETASKINFO @V_TASK_CODE,@IN_PRODUCT_ID,@IN_PRODUCT_ID,0
    
    --2.2.生成认购资金已录入，请查看并审核提示
    SET @V_LOGIN_INFO = ISNULL(@V_LOGIN_NAME,'') + N' 产品编号：' + @V_PRODUCT_CODE + N',产品名称: ' + @V_PRODUCT_NAME
                                                 + N'，客户名称：' + @V_CUST_NAME + N'，到账日期：' + INTRUST.dbo.GETDATESTR(@IN_DZ_DATE)
                                                 + N'，到账金额：' + RTRIM(CONVERT(CHAR,@IN_TO_MONEY))
    
    EXEC @V_RET_CODE = INTRUST..SP_INNER_MAKETASKINFO @V_TASK_CODE,@V_LOGIN_NAME,@V_LOGIN_INFO,@V_SYS_DATE,@IN_PRODUCT_ID,
                                             @IN_INPUT_MAN,@V_MENU_ID,0,@V_SERIAL_NO,0
    
    --3.日志记录
    SELECT @SSUMMARY = N'认购资金录入' + '，产品：' + @V_PRODUCT_CODE + '--' + @V_PRODUCT_NAME + 
                                         '，客户ID：' + RTRIM(CONVERT(CHAR,@IN_CUST_ID)) +
                                         '，客户名称：' + @V_CUST_NAME +
                                         '，到账日期：' + INTRUST.dbo.GETDATESTR(@IN_DZ_DATE) + 
                                         '，到账金额：' + RTRIM(CONVERT(CHAR,@IN_TO_MONEY)) +
                                         '，资金来源地：' + @V_CITY_NAME

    INSERT INTO INTRUST..TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)

    COMMIT TRANSACTION
    END TRY

    BEGIN CATCH

        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION
        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)

        SELECT @V_ERROR_STR = N'%s<BR><font color = "white">Error:%d,Level:%d,State:%d,Procedure:%s,Line:%d</font>',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()

        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_NUMBER,@V_ERROR_SEVERITY,@V_ERROR_STATE,
                  @V_ERROR_PROCEDURE,@V_ERROR_LINE)
        
        RETURN -100
    END CATCH
    RETURN 100
GO
