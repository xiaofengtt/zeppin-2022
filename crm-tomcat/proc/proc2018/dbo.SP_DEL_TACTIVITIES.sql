USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_DEL_TACTIVITIES     @IN_SERIAL_NO           INT,            --活动序列号
                                        @IN_INPUT_MAN           INT = 0         --操作员

WITH ENCRYPTION
AS
    DECLARE @V_RET_CODE INT, @IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40), @SSUMMARY NVARCHAR(200)
    SELECT @V_RET_CODE = -30401000, @IBUSI_FLAG = 30401
    SELECT @SBUSI_NAME = N'删除营销活动记录信息', @SSUMMARY = N'删除营销活动记录信息'
    IF NOT EXISTS(SELECT 1 FROM TACTIVITIES WHERE SERIAL_NO = @IN_SERIAL_NO)
        RETURN @V_RET_CODE - 1 --营销活动记录信息不存在

    BEGIN TRANSACTION
    --删除活动任务表记录
    DELETE FROM  TActivitiesTask
    WHERE Active_Serial_no = @IN_SERIAL_NO
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END
    --删除活动费用表信息
    DELETE FROM  TActivitiesFee
    WHERE Active_Serial_no = @IN_SERIAL_NO
        IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END
    --删除活动记录表信息
    DELETE FROM  TACTIVITIES
    WHERE SERIAL_NO = @IN_SERIAL_NO
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END

    SET @SSUMMARY = N'删除营销活动记录信息，操作员：' + CAST(@IN_INPUT_MAN AS NVARCHAR(10))
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END

    COMMIT TRANSACTION
    RETURN 100
GO
