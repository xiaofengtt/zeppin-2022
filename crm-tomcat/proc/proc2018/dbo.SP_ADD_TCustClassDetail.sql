USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_ADD_TCustClassDetail @IN_CLASSDEFINE_ID     INTEGER,        --分级定义ID
                                         @IN_CLASSDETAIL_ID     INTEGER,        --分级明细ID，唯一索引
                                         @IN_CLASSDETAIL_NAME   NVARCHAR(60),   --分级明细名称
                                         @IN_CLASS_MINVALUE     INTEGER,        --当前分类判断依据的最小值
                                         @IN_CLASS_MAXVALUE     INTEGER,        --当前分类判断依据的最大值
                                         @IN_INPUT_MAN          INTEGER         --操作员
WITH ENCRYPTION
AS
    DECLARE @V_RET_CODE INT,@IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)
    SELECT @SBUSI_NAME = N'增加客户分级定义之明细'
    SELECT @SSUMMARY = N'增加客户分级定义之明细'
    SELECT @IBUSI_FLAG = 20205
    SELECT @V_RET_CODE = -20205000

    DECLARE @V_CANMODI INTEGER, --是否可修改标志
            @V_CLASSDEFINE_NAME NVARCHAR(60)

    IF NOT EXISTS(SELECT 1 FROM TCustClassDefine WHERE CLASSDEFINE_ID = @IN_CLASSDEFINE_ID)
        RETURN @V_RET_CODE - 4 --记录不存在
    SELECT @V_CANMODI = CANMODI, @V_CLASSDEFINE_NAME = CLASSDEFINE_NAME FROM TCustClassDefine WHERE CLASSDEFINE_ID = @IN_CLASSDEFINE_ID
    IF @V_CANMODI <> 1
        RETURN @V_RET_CODE - 6 --当前分级不能添加明细
    IF EXISTS(SELECT * FROM TCustClassDetail WHERE CLASSDETAIL_ID = @IN_CLASSDETAIL_ID)
        RETURN @V_RET_CODE - 7 --定义明细ID已经存在

    BEGIN TRANSACTION

    INSERT INTO TCustClassDetail(CLASSDEFINE_ID,CLASSDEFINE_NAME,CLASSDETAIL_ID,CLASSDETAIL_NAME,CLASS_MINVALUE,CLASS_MAXVALUE,CANMODI,CANDEL,CANADD)
        VALUES(@IN_CLASSDEFINE_ID,@V_CLASSDEFINE_NAME,@IN_CLASSDETAIL_ID,@IN_CLASSDETAIL_NAME,@IN_CLASS_MINVALUE,@IN_CLASS_MAXVALUE,1,1,1)
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END

    SELECT @SSUMMARY = N'增加客户分级定义之明细：分级名称：'+@V_CLASSDEFINE_NAME+N'|明细ID:'+RTRIM(CONVERT(NVARCHAR(16),@IN_CLASSDETAIL_ID))+N'|名称:'+@IN_CLASSDETAIL_NAME
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END

    COMMIT TRANSACTION
    RETURN 100
GO
