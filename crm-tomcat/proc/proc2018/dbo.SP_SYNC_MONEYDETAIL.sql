USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_SYNC_MONEYDETAIL @IN_SERIAL_NO       INTEGER,         --序号
                                     @IN_INPUT_MAN       INTEGER          --操作员
WITH ENCRYPTION
AS
    SET NOCOUNT ON
    DECLARE @V_RET INT,@V_RET_CODE INT
    DECLARE @IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)
    SELECT @V_RET_CODE = -20701000
    SELECT @IBUSI_FLAG = 20701
    SELECT @SBUSI_NAME = N'复制到账信息'
    SELECT @SSUMMARY = N'复制到账信息'

    DECLARE @V_ERROR NVARCHAR(200), @V_PRODUCT_ID INT, @V_PREPRODUCT_NAME NVARCHAR(100), @V_BIND_SERIAL_NO INT
    ------------------------------------------------------------------------
    BEGIN TRY
        --校验
        IF NOT EXISTS(SELECT 1 FROM TPRECONTRACT WHERE SERIAL_NO = @IN_SERIAL_NO)
        BEGIN
            SET @V_ERROR = N'记录不存在！'
            RAISERROR(@V_ERROR,16,3)
        END        
        --读取数据
        SELECT @V_PRODUCT_ID = B.BIND_PRODUCT_ID, @V_PREPRODUCT_NAME = PREPRODUCT_NAME, @V_BIND_SERIAL_NO = BIND_SERIAL_NO
            FROM TPRECONTRACT A LEFT JOIN TPREPRODUCT B ON A.PREPRODUCT_ID = B.PREPRODUCT_ID
            WHERE A.SERIAL_NO = @IN_SERIAL_NO
        --校验
        IF ISNULL(@V_PRODUCT_ID,0) = 0
        BEGIN
            SET @V_ERROR = N'未绑定项目！'
            RAISERROR(@V_ERROR,16,3)
        END
        IF ISNULL(@V_BIND_SERIAL_NO,0) = 0
           OR NOT EXISTS(SELECT 1 FROM INTRUST..TPRECONTRACT WHERE SERIAL_NO = @V_BIND_SERIAL_NO)
           OR NOT EXISTS(SELECT 1 FROM INTRUST..TCONTRACT A, INTRUST..TPRECONTRACT B WHERE A.PRODUCT_ID = B.PRODUCT_ID AND A.PRE_CODE = B.PRE_CODE AND B.SERIAL_NO = @V_BIND_SERIAL_NO)
        BEGIN
            SET @V_ERROR = N'请通过签约功能实现预约转签约！'
            RAISERROR(@V_ERROR,16,3)
        END
        --
        BEGIN TRANSACTION
            --1.复制到账信息
            UPDATE INTRUST..TMONEYDETAIL SET DZ_DATE = A.RG_DATE, JK_TYPE = M.JK_TYPE, JK_TYPE_NAME = M.JK_TYPE_NAME
            FROM EFCRM..TPRECONTRACT A, INTRUST..TPRECONTRACT B, INTRUST..TCONTRACT C, INTRUST..TMONEYDETAIL D, EFCRM..TPREMONEYDETAIL M
            WHERE A.BIND_SERIAL_NO = B.SERIAL_NO AND B.PRODUCT_ID = C.PRODUCT_ID AND B.PRE_CODE = C.PRE_CODE AND A.SERIAL_NO = M.PRE_SERIAL_NO
                AND C.PRODUCT_ID = D.PRODUCT_ID AND C.CONTRACT_BH = D.CONTRACT_BH AND D.IS_JK_DATA = 1 AND D.CHECK_FLAG = 1
                AND A.SERIAL_NO = @IN_SERIAL_NO

             --2.日志
            SELECT @SSUMMARY = N'复制到账信息同步，产品名称：' + @V_PREPRODUCT_NAME + N'，序号：' + CONVERT(NVARCHAR(20),@IN_SERIAL_NO)
            INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY) VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
        COMMIT TRANSACTION
    END TRY
    --3.异常处理
    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION
        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)
        SELECT @V_ERROR_STR = N'Message:%s,Procedure:%s,Line:%d',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()
        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_PROCEDURE,@V_ERROR_LINE)
        RETURN -100
    END CATCH
    RETURN 100
GO
