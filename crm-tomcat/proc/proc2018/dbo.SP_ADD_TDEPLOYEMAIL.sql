USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_ADD_TDEPLOYEMAIL
        @IN_CUST_ID          INT          ,    --客户ID
        @IN_SEND_DATE        INT          ,    --发送日期
        @IN_EMAIL            VARCHAR(40)  ,    --接收的EMAIL
        @IN_TITLE            NVARCHAR(40) ,    --邮件主题
        @IN_CONTENT          NTEXT        ,    --邮件内容
        @IN_INPUT_MAN        INTEGER           --操作员
WITH ENCRYPTION
AS
    SET NOCOUNT ON

    DECLARE @V_ERROR NVARCHAR(200), @IBUSI_FLAG INT, @SBUSI_NAME NVARCHAR(40), @SSUMMARY NVARCHAR(200)
    DECLARE @V_CUST_NAME NVARCHAR(200)
    
    BEGIN TRY
    
    SET @IBUSI_FLAG = 30601
    SET @SBUSI_NAME = '对账单EMAIL邮件发送'
    IF ISNULL(@IN_SEND_DATE,0) = 0
		SET @IN_SEND_DATE= CAST(CONVERT(VARCHAR,GETDATE(),112) AS INT)

    --IF  BEGIN
    --    RAISERROR('ERROR',16,1)
    --END
    
    --校验输入数据
    /*IF ISNULL(@IN_EMAIL,'') = ''
	BEGIN
		SET @V_ERROR = N'请核对EMAIL'
		RAISERROR(@V_ERROR,16,1)
	END*/
	SET @IN_EMAIL=ISNULL(@IN_EMAIL,'')
    IF ISNULL(@IN_TITLE,'') = ''
	BEGIN
		SET @V_ERROR = N'邮件主题不能为空'
		RAISERROR(@V_ERROR,16,2)
	END
    /*IF ISNULL(@IN_CONTENT,'') = ''
	BEGIN
		SET @V_ERROR = N'邮件内容不能为空'
		RAISERROR(@V_ERROR,16,3)
	END*/
    BEGIN TRANSACTION
    --在当天同一客户的发送内容更新（未发送的），如果不存在，则新增
    UPDATE TDEPLOYEMAIL 
		SET SEND_DATE = @IN_SEND_DATE,
		EMAIL         = @IN_EMAIL,
		TITLE         = @IN_TITLE,
		CONTENT       = @IN_CONTENT
		WHERE CUST_ID=@IN_CUST_ID AND SEND_FLAG=1 --发送标识：1未发送，2已发送
    IF @@ROWCOUNT=0
    INSERT INTO TDEPLOYEMAIL(CUST_ID, SEND_DATE, EMAIL, TITLE, CONTENT, SEND_FLAG)
        VALUES(@IN_CUST_ID,@IN_SEND_DATE,@IN_EMAIL,@IN_TITLE,@IN_CONTENT,1)
    
    --日志记录
    SET @SSUMMARY = @SBUSI_NAME + ',新增,对账单EMAIL邮件:CUST_ID='+CAST(@IN_CUST_ID AS VARCHAR)
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    COMMIT TRANSACTION
    END TRY

    --3.异常处理
    BEGIN CATCH
        IF @@TRANCOUNT > 0
            ROLLBACK TRANSACTION
        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)
        SELECT @V_ERROR_STR = N'Message:%s<BR><font color = "white">Error:%d,Level:%d,State:%d,Procedure:%s,Line:%d</font>',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()
        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_NUMBER,@V_ERROR_SEVERITY,@V_ERROR_STATE,
                  @V_ERROR_PROCEDURE,@V_ERROR_LINE)
        RETURN -100
    END CATCH
    RETURN 100
GO
