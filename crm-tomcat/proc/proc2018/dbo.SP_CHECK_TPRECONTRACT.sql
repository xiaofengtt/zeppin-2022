USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_CHECK_TPRECONTRACT @IN_SERIAL_NO    INT,    --记录号
                                     @IN_CHECK_FLAG     INT,    --预约审核1未审核2审核通过3审核不通过
                                     @IN_INPUT_MAN      INT     --录入操作员
WITH ENCRYPTION
AS
    --SET NOCOUNT ON
    DECLARE @V_RET_CODE INT,@IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)
    DECLARE @V_PRODUCT_ID INT,@V_SUB_PRODUCT_ID INT,@V_PRE_NUM INT,@V_PRE_MONEY DEC(18,3),@V_USER_ID INT
    SELECT @SBUSI_NAME = N'预约审核'
    SELECT @SSUMMARY = N'预约审核'
    SELECT @IBUSI_FLAG = 39007
    SELECT @V_USER_ID=USER_ID FROM TSYSTEMINFO
    
    BEGIN TRY
    SELECT @V_PRODUCT_ID=PRODUCT_ID, @V_SUB_PRODUCT_ID=SUB_PRODUCT_ID, @V_PRE_NUM=PRE_NUM, @V_PRE_MONEY=PRE_MONEY 
      FROM TPRECONTRACT 
      WHERE SERIAL_NO=@IN_SERIAL_NO
    IF @@ROWCOUNT=0
        RAISERROR('预约信息不存在',16,1)
    
    BEGIN TRANSACTION
        
		--UPDATE INTRUST..TPRECONTRACT SET CHECK_FLAG = 2, CHECK_MAN = @IN_INPUT_MAN
        --    WHERE SERIAL_NO = @IN_SERIAL_NO
        --CRM库中的预约表
        UPDATE TPRECONTRACT SET CHECK_FLAG = @IN_CHECK_FLAG, CHECK_MAN = @IN_INPUT_MAN WHERE SERIAL_NO = @IN_SERIAL_NO
        IF @IN_CHECK_FLAG=1 AND @V_PRODUCT_ID>0 AND @V_USER_ID<>15/*建信要求不同步给业务系统*/ --同步给信托库
        BEGIN
			INSERT INTO INTRUST..TPRECONTRACT(BOOK_CODE,PRODUCT_ID, SUB_PRODUCT_ID, CUST_ID, PRE_CODE,
				PRE_MONEY, LINK_MAN, PRE_DATE,VALID_DAYS, END_DATE, PRE_TYPE,PRE_TYPE_NAME,
				PRE_STATUS,PRE_STATUS_NAME,INPUT_MAN,SUMMARY,PRE_NUM,EXP_REG_DATE,CUST_SOURCE,CUST_SOURCE_NAME,CHANNEL_TYPE)
			SELECT 1,PRODUCT_ID, SUB_PRODUCT_ID, CUST_ID,PRE_CODE,
				PRE_MONEY,LINK_MAN,PRE_DATE,VALID_DAYS,END_DATE,PRE_TYPE,PRE_TYPE_NAME,
				PRE_STATUS,PRE_STATUS_NAME,INPUT_MAN,SUMMARY,PRE_NUM,EXP_REG_DATE,CUST_SOURCE,CUST_SOURCE_NAME,CHANNEL_TYPE
				FROM TPRECONTRACT WHERE SERIAL_NO=@IN_SERIAL_NO
				
            UPDATE TPRECONTRACT
              SET BIND_SERIAL_NO=@@IDENTITY
              WHERE SERIAL_NO=@IN_SERIAL_NO          
			
			IF @V_SUB_PRODUCT_ID>0 --子产品
			    UPDATE INTRUST..TSUBPRODUCT 
			      SET PRE_NUM = ISNULL(PRE_NUM,0)+@V_PRE_NUM, PRE_MONEY = ISNULL(PRE_MONEY,0) + @V_PRE_MONEY
				  WHERE SUB_PRODUCT_ID=@V_SUB_PRODUCT_ID				
			ELSE 
				UPDATE INTRUST..TPRODUCT 
				  SET FACT_PRE_NUM = ISNULL(FACT_PRE_NUM,0)+@V_PRE_NUM, FACT_PRE_MONEY = ISNULL(FACT_PRE_MONEY,0) + @V_PRE_MONEY
			      WHERE PRODUCT_ID = @V_PRODUCT_ID
		END
	SET @SSUMMARY = N'预约审核，操作员：' + CAST(@IN_INPUT_MAN AS NVARCHAR(10))+'，记录号：'+CAST(@IN_SERIAL_NO AS VARCHAR)
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    
    COMMIT TRANSACTION
    END TRY

    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION

        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)

        SELECT @V_ERROR_STR = N'Message:%s,Procedure:%s,Line:%d',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()

        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_PROCEDURE,@V_ERROR_LINE)

        RETURN -100
    END CATCH
    RETURN 100
GO
