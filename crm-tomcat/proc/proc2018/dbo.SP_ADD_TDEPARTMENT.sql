USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_ADD_TDEPARTMENT
                                   @IN_DEPART_ID  INT,
                                   @IN_DEPART_NAME NVARCHAR(24),
                                   @IN_DEPART_JC NVARCHAR(12),
                                   @IN_PARENT_ID INT,
                                   @IN_MANAGER_MAN NVARCHAR(20),
                                   @IN_LEADER_MAN NVARCHAR(20),
                                   @IN_INPUT_MAN INT
WITH ENCRYPTION
AS
  DECLARE @V_RET_CODE INT,@IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)
  DECLARE @V_DEPART_NAME NVARCHAR(24), @OUT_RET_CODE INT

  SELECT @V_RET_CODE = -10101000
  SELECT @IBUSI_FLAG = 10101
  SELECT @SBUSI_NAME = N'增加部门信息'
  SELECT @SSUMMARY = N'增加部门信息'

  SELECT @OUT_RET_CODE = 100

  IF EXISTS(SELECT * FROM TDEPARTMENT WHERE DEPART_ID = @IN_DEPART_ID)
  BEGIN
        SELECT @V_DEPART_NAME = DEPART_NAME FROM TDEPARTMENT WHERE DEPART_ID = @IN_DEPART_ID
        SELECT @OUT_RET_CODE = @V_RET_CODE-1   -- 部门编号已存在
        UPDATE TERRORINFO SET MSG1 = CONVERT(NVARCHAR(100),@IN_DEPART_ID), MSG2 = @V_DEPART_NAME WHERE ERROR_ID = @OUT_RET_CODE
        RETURN @OUT_RET_CODE
  END

  IF NOT EXISTS(SELECT * FROM TDEPARTMENT WHERE DEPART_ID = @IN_PARENT_ID or @IN_PARENT_ID is null or @IN_PARENT_ID = 0 )
  BEGIN
        SELECT @OUT_RET_CODE = @V_RET_CODE-2   -- 上级部门编号不存在
        UPDATE TERRORINFO SET MSG1 = CONVERT(NVARCHAR(100),@IN_PARENT_ID) WHERE ERROR_ID = @OUT_RET_CODE
        RETURN @OUT_RET_CODE
  END

  BEGIN TRANSACTION
  SAVE TRANSACTION SP1

  INSERT INTO TDEPARTMENT(DEPART_ID,DEPART_NAME,DEPART_JC,PARENT_ID,BOTTOM_FLAG,MANAGER_MAN,LEADER_MAN)
  VALUES(@IN_DEPART_ID,@IN_DEPART_NAME,@IN_DEPART_JC,@IN_PARENT_ID,1,@IN_MANAGER_MAN,@IN_LEADER_MAN)

  IF ( @@error <> 0 )
  BEGIN
       ROLLBACK TRANSACTION SP1
       COMMIT TRANSACTION
       RETURN -100
  END

  IF @IN_PARENT_ID > 0
  BEGIN
       UPDATE TDEPARTMENT SET BOTTOM_FLAG=2 WHERE DEPART_ID=@IN_PARENT_ID
      IF ( @@error <> 0 )
      BEGIN
           ROLLBACK TRANSACTION SP1
           COMMIT TRANSACTION
           RETURN -100
      END
  END

  SELECT @SSUMMARY = N'增加部门信息，部门编号：'+CONVERT(CHAR(8),@IN_DEPART_ID)+N'，部门名称：'+@IN_DEPART_NAME
  SELECT @SSUMMARY = @SSUMMARY+N'，部门简称：'+@IN_DEPART_JC+N'，上级部门编号：'+CONVERT(CHAR(8),@IN_PARENT_ID)

  INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
  VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)

   COMMIT TRANSACTION
   return 100
GO
