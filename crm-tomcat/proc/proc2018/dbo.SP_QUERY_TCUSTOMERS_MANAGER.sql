USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_QUERY_TCUSTOMERS_MANAGER @IN_TEAM_ID      INT,                 --团队ID
                                             @IN_MANAGERNAME  NVARCHAR(128),       --团队成员名称(客户经理)
                                             @IN_LEADER_NAME  NVARCHAR(64) = N'',  --团队负责人姓名
                                             @IN_INPUT_MAN    INT = NULL,          --操作员
                                             @IN_YEAR         INT = NULL           --年份过滤
WITH ENCRYPTION
AS
    SET NOCOUNT ON
    DECLARE @V_DT_INTRUST INT
    SELECT @V_DT_INTRUST = VALUE FROM TSYSCONTROL WHERE FLAG_TYPE = N'DT_INTRUST'

    CREATE TABLE #TEMP_TCUSTOMERS_MANAGER
    (
        SERIAL_NO       INT NOT NULL IDENTITY,
        TEAM_ID         INT,             --团队ID
        TEAM_NO         NVARCHAR(60),    --团队编号
        TEAM_NAME       NVARCHAR(160),   --团队名称
        LEADER_NAME     NVARCHAR(60),    --团队负责人
        MANAGERID       INT,             --经理ID
        MANAGERNAME     NVARCHAR(128),   --经理名称
        CUST_TYPE_P     INT,             --个人客户数
        CUST_TYPE_E     INT,             --机构客户数
        TOTAL_MONEY     DEC(20,3),       --认购金额
        BEN_AMOUNT      DEC(20,3)        --受益份额
    )
    CREATE TABLE #TEMP_CUSTOMERS2
    (
        CUST_ID         INT,
        CUST_TYPE       INT,
        MANAGERID       INT,
        RG_MONEY        DEC(16,3),
        BEN_AMOUNT      DEC(16,3)
    )
    BEGIN
        INSERT INTO #TEMP_CUSTOMERS2(CUST_ID,CUST_TYPE,MANAGERID,RG_MONEY,BEN_AMOUNT)
            SELECT A.CUST_ID,B.CUST_TYPE,ISNULL(B.SERVICE_MAN,0),SUM(A.TO_AMOUNT),SUM(A.BEN_AMOUNT)
            FROM INTRUST..TBENIFITOR A, INTRUST..TCUSTOMERINFO B
            WHERE A.CUST_ID = B.CUST_ID
                AND(A.BEN_DATE/10000 = @IN_YEAR OR ISNULL(@IN_YEAR,0) = 0)
            GROUP BY A.CUST_ID,B.CUST_TYPE,B.SERVICE_MAN
            ORDER BY A.CUST_ID
    END
    INSERT INTO #TEMP_TCUSTOMERS_MANAGER(MANAGERID,MANAGERNAME,TOTAL_MONEY,BEN_AMOUNT)
        SELECT 0,'-',0.0,0.0
        UNION ALL
        SELECT ManagerID, ManagerName, 0.0, 0.0 FROM TCustManagers
    UPDATE #TEMP_TCUSTOMERS_MANAGER
        SET TEAM_ID = B.TEAM_ID, TEAM_NO = B.TEAM_NO, TEAM_NAME = B.TEAM_NAME, LEADER_NAME = B.LEADER_NAME
        FROM #TEMP_TCUSTOMERS_MANAGER A, TMANAGERTEAMS B
        WHERE A.MANAGERID = B.LEADER
    UPDATE #TEMP_TCUSTOMERS_MANAGER
        SET TEAM_ID = B.TEAM_ID, TEAM_NO = B.TEAM_NO, TEAM_NAME = B.TEAM_NAME
        FROM #TEMP_TCUSTOMERS_MANAGER A, TMANAGERTEAMMEMBERS B
        WHERE A.MANAGERID = B.MANAGERID
    UPDATE #TEMP_TCUSTOMERS_MANAGER
        SET CUST_TYPE_P = B.CUST_TYPE_P, TOTAL_MONEY = TOTAL_MONEY+B.RG_MONEY, BEN_AMOUNT = A.BEN_AMOUNT+B.BEN_AMOUNT
        FROM #TEMP_TCUSTOMERS_MANAGER A, (SELECT ISNULL(MANAGERID,0) AS MANAGERID, COUNT(*) AS CUST_TYPE_P, SUM(RG_MONEY) AS RG_MONEY, SUM(BEN_AMOUNT) AS BEN_AMOUNT FROM #TEMP_CUSTOMERS2 WHERE CUST_TYPE = 1 GROUP BY MANAGERID) B
        WHERE A.MANAGERID = B.MANAGERID
    UPDATE #TEMP_TCUSTOMERS_MANAGER
        SET CUST_TYPE_E = B.CUST_TYPE_E, TOTAL_MONEY = TOTAL_MONEY+B.RG_MONEY, BEN_AMOUNT = A.BEN_AMOUNT+B.BEN_AMOUNT
        FROM #TEMP_TCUSTOMERS_MANAGER A, (SELECT ISNULL(MANAGERID,0) AS MANAGERID, COUNT(*) AS CUST_TYPE_E, SUM(RG_MONEY) AS RG_MONEY, SUM(BEN_AMOUNT) AS BEN_AMOUNT FROM #TEMP_CUSTOMERS2 WHERE CUST_TYPE = 2 GROUP BY MANAGERID) B
        WHERE A.MANAGERID = B.MANAGERID

    SELECT * FROM #TEMP_TCUSTOMERS_MANAGER
        WHERE (TEAM_ID = @IN_TEAM_ID OR @IN_TEAM_ID IS NULL OR @IN_TEAM_ID = 0)
            AND(MANAGERNAME LIKE '%'+@IN_MANAGERNAME+'%' OR @IN_MANAGERNAME IS NULL)
            AND(LEADER_NAME LIKE '%'+@IN_LEADER_NAME+'%' OR @IN_LEADER_NAME IS NULL)
        ORDER BY TEAM_ID,TEAM_NO,MANAGERID
GO
