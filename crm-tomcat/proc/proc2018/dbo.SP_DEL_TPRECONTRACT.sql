USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE dbo.SP_DEL_TPRECONTRACT @IN_SERIAL_NO     INT,
                                     @IN_DEL_FLAG      INT,  --删除标志:1.彻底删除,2作废
                                     @IN_INPUT_MAN     INT
WITH ENCRYPTION
AS
    SET NOCOUNT ON
    DECLARE @V_ERROR NVARCHAR(200)
    DECLARE @V_RET_CODE INT,@IBUSI_FLAG INT,@SBUSI_NAME VARCHAR(40),@SSUMMARY VARCHAR(200)
    SELECT @V_RET_CODE = -20702000
    SELECT @IBUSI_FLAG = 20702
    SELECT @SBUSI_NAME = '删除客户预约销售'
    SELECT @SSUMMARY = '删除客户预约销售'

    DECLARE @V_PRE_CODE VARCHAR(16),@V_PRODUCT_ID INT,@V_SUB_PRODUCT_ID INT,@V_PREPRODUCT_ID INT
    DECLARE @V_PREPRODUCT_NAME NVARCHAR(60),@V_PRE_MONEY DECIMAL(16,3), @V_PRE_NUM INT
    DECLARE @V_PRE_TYPE_NAME VARCHAR(30),@V_END_DATE INT,@V_CUST_SOURCE_NAME NVARCHAR(30), @V_LINK_MAN INT, @V_BIND_SERIAL_NO INT
    DECLARE @V_NEED_CHECK INT,@V_SERVICE_MAN INT,@V_CUST_ID INT,@V_CUST_NAME NVARCHAR(100)
    
    BEGIN TRY
    --1.业务逻辑与操作
    IF NOT EXISTS(SELECT * FROM TPRECONTRACT WHERE SERIAL_NO = @IN_SERIAL_NO)
        RAISERROR(N'记录不存在！',16,3)
    IF EXISTS(SELECT * FROM TPRECONTRACT WHERE SERIAL_NO = @IN_SERIAL_NO AND PRE_STATUS IN ('111303','111304') )
        RAISERROR(N'预约已过期或已作废！',16,3)

    SELECT @V_PRE_MONEY = PRE_MONEY, @V_PRE_NUM=PRE_NUM,@V_CUST_ID=CUST_ID
        ,@V_PRODUCT_ID=PRODUCT_ID,@V_PREPRODUCT_ID = PREPRODUCT_ID,@V_SUB_PRODUCT_ID=SUB_PRODUCT_ID
        ,@V_PRE_CODE = PRE_CODE, @V_LINK_MAN = LINK_MAN, @V_BIND_SERIAL_NO = BIND_SERIAL_NO
      FROM TPRECONTRACT WHERE SERIAL_NO = @IN_SERIAL_NO
    IF @V_PRODUCT_ID>0
      BEGIN
        SELECT @V_PREPRODUCT_NAME=PRODUCT_NAME FROM INTRUST..TPRODUCT WHERE PRODUCT_ID = @V_PRODUCT_ID
        IF @V_SUB_PRODUCT_ID>0
			SELECT @V_PREPRODUCT_NAME = @V_PREPRODUCT_NAME+'['+LIST_NAME+']'
			  FROM INTRUST..TSUBPRODUCT 
			  WHERE SUB_PRODUCT_ID=@V_SUB_PRODUCT_ID
      END
	ELSE
        SELECT @V_PREPRODUCT_NAME = PREPRODUCT_NAME FROM TPREPRODUCT WHERE PREPRODUCT_ID = @V_PREPRODUCT_ID

    BEGIN TRANSACTION
    IF @IN_DEL_FLAG = 1
    BEGIN
        DELETE FROM TPRECONTRACT WHERE SERIAL_NO = @IN_SERIAL_NO
        DELETE FROM INTRUST..TPRECONTRACT WHERE SERIAL_NO = @V_BIND_SERIAL_NO
        SELECT @V_SERVICE_MAN=SERVICE_MAN,@V_CUST_NAME=ISNULL(CUST_NAME,'') FROM TCustomers WHERE CUST_ID=@V_CUST_ID
        IF ISNULL(@V_SERVICE_MAN,0)>0 AND @V_SERVICE_MAN <> @IN_INPUT_MAN
        BEGIN
			INSERT INTO TSYSMESSAGE(TITLE,MSG,FROM_OPCODE,TO_OPCODE,INPUT_TIME,IS_READ)
				SELECT '预约作废','客户['+@V_CUST_NAME+']的预约已作废:所预约的产品：'+@V_PREPRODUCT_NAME+'；预约金额：'+CAST(@V_PRE_MONEY AS VARCHAR),@IN_INPUT_MAN,@V_SERVICE_MAN,GETDATE(),1
        END
        IF ISNULL(@V_LINK_MAN,0)>0 AND @V_LINK_MAN <> @IN_INPUT_MAN
        BEGIN
			INSERT INTO TSYSMESSAGE(TITLE,MSG,FROM_OPCODE,TO_OPCODE,INPUT_TIME,IS_READ)
				SELECT '预约作废','客户['+@V_CUST_NAME+']的预约已作废:所预约的产品：'+@V_PREPRODUCT_NAME+'；预约金额：'+CAST(@V_PRE_MONEY AS VARCHAR),@IN_INPUT_MAN,@V_LINK_MAN,GETDATE(),1
        END
    END
    ELSE
    BEGIN
        UPDATE TPRECONTRACT
            SET PRE_STATUS = '111304',PRE_STATUS_NAME = '已作废'
            WHERE SERIAL_NO = @IN_SERIAL_NO
        UPDATE INTRUST..TPRECONTRACT
            SET PRE_STATUS = '111304',PRE_STATUS_NAME = '已作废'
            WHERE SERIAL_NO = @V_BIND_SERIAL_NO
        SELECT @V_SERVICE_MAN=SERVICE_MAN,@V_CUST_NAME=ISNULL(CUST_NAME,'') FROM TCustomers WHERE CUST_ID=@V_CUST_ID
        IF ISNULL(@V_SERVICE_MAN,0)>0 AND @V_SERVICE_MAN <> @IN_INPUT_MAN
        BEGIN
			INSERT INTO TSYSMESSAGE(TITLE,MSG,FROM_OPCODE,TO_OPCODE,INPUT_TIME,IS_READ)
				SELECT '预约作废','客户['+@V_CUST_NAME+']的预约已作废:所预约的产品：'+@V_PREPRODUCT_NAME+'；预约金额：'+CAST(@V_PRE_MONEY AS VARCHAR),@IN_INPUT_MAN,@V_SERVICE_MAN,GETDATE(),1
        END
        IF ISNULL(@V_LINK_MAN,0)>0 AND @V_LINK_MAN <> @IN_INPUT_MAN
        BEGIN
			INSERT INTO TSYSMESSAGE(TITLE,MSG,FROM_OPCODE,TO_OPCODE,INPUT_TIME,IS_READ)
				SELECT '预约作废','客户['+@V_CUST_NAME+']的预约已作废:所预约的产品：'+@V_PREPRODUCT_NAME+'；预约金额：'+CAST(@V_PRE_MONEY AS VARCHAR),@IN_INPUT_MAN,@V_LINK_MAN,GETDATE(),1
        END
    END
    
    --预约是否要求审核 1是
    SELECT @V_NEED_CHECK=ISNULL(MAX(VALUE),0) FROM TSYSCONTROL WHERE FLAG_TYPE='PRECONTRACT_CHECK'
      
    IF @V_NEED_CHECK<>1 AND @V_PRODUCT_ID>0
        IF @V_SUB_PRODUCT_ID>0
            UPDATE INTRUST..TSUBPRODUCT
              SET PRE_MONEY = ISNULL(PRE_MONEY,0.0) - @V_PRE_MONEY, PRE_NUM=PRE_NUM-@V_PRE_NUM
              WHERE SUB_PRODUCT_ID = @V_SUB_PRODUCT_ID
        ELSE
            UPDATE INTRUST..TPRODUCT
              SET FACT_PRE_MONEY = ISNULL(FACT_PRE_MONEY,0.0) - @V_PRE_MONEY, FACT_PRE_NUM=FACT_PRE_NUM-@V_PRE_NUM
              WHERE PRODUCT_ID = @V_PRODUCT_ID
    
    IF @V_PREPRODUCT_ID>0
        UPDATE TPREPRODUCT
            SET PRE_FACT_MONEY = ISNULL(PRE_FACT_MONEY,0) - @V_PRE_MONEY
            WHERE PREPRODUCT_ID = @V_PREPRODUCT_ID
        
    --修改销售人员预约配额完成情况
    DECLARE @V_TEAM_ID INT, @V_OLD_QUALIFIEDNUM INT,@V_CUST_TYPE INT
    SELECT @V_CUST_TYPE=CUST_TYPE FROM TCustomers WHERE CUST_ID=@V_CUST_ID
    IF @V_PRE_MONEY < 3000000 AND @V_CUST_TYPE=1/*个人*/
        SET @V_OLD_QUALIFIEDNUM = 1
    ELSE
        SET @V_OLD_QUALIFIEDNUM = 0
    SELECT @V_TEAM_ID = A.TEAM_ID
        FROM TManagerTeams A
        WHERE A.LEADER = @V_LINK_MAN OR EXISTS(SELECT 1 FROM TManagerTeamMembers Z WHERE A.TEAM_ID = Z.TEAM_ID AND Z.MANAGERID = @V_LINK_MAN)
    IF @V_TEAM_ID IS NULL SET @V_TEAM_ID = -1
    UPDATE TTEAMQUOTA
        SET PRE_SALEMONEY = ISNULL(PRE_SALEMONEY,0) - @V_PRE_MONEY,
            PRE_QUALIFIEDNUM = ISNULL(PRE_QUALIFIEDNUM,0) - @V_OLD_QUALIFIEDNUM
        WHERE TEAM_ID = @V_TEAM_ID AND PREPRODUCT_ID = @V_PREPRODUCT_ID

    --2.日志
    SELECT @SSUMMARY = '删除客户预约销售，产品名称：' + @V_PREPRODUCT_NAME + ',预约号：' + @V_PRE_CODE
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY) VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    COMMIT TRANSACTION
    END TRY
    --3.异常处理
    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION
        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)
        SELECT @V_ERROR_STR = N'Message:%s,Procedure:%s,Line:%d',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()
        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_PROCEDURE,@V_ERROR_LINE)
        RETURN -100
    END CATCH
    RETURN 100
GO
