USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_DEL_THOLIDAYS @IN_HOLIDAY_ID      INT,  --节假日ID
                                  @IN_INPUT_MAN       INT
WITH ENCRYPTION
AS
    SET NOCOUNT ON
    DECLARE @V_RET_CODE INT, @IBUSI_FLAG INT, @SBUSI_NAME NVARCHAR(40), @SSUMMARY NVARCHAR(200)
    SELECT @V_RET_CODE = -10130000, @IBUSI_FLAG = 10130
    SELECT @SBUSI_NAME = N'删除节假日', @SSUMMARY = N'删除节假日'

    IF NOT EXISTS(SELECT 1 FROM THOLIDAYS WHERE HOLIDAY_ID = @IN_HOLIDAY_ID)
        RETURN -20402001 --记录不存在
    DECLARE @V_CAL_FLAG INT, @V_HOLIDAY_DAY INT, @V_HOLIDAY_NAME NVARCHAR(60)
    SELECT @V_HOLIDAY_NAME = HOLIDAY_NAME, @V_CAL_FLAG = CAL_FLAG, @V_HOLIDAY_DAY = HOLIDAY_DAY FROM THOLIDAYS WHERE HOLIDAY_ID = @IN_HOLIDAY_ID

    BEGIN TRANSACTION

    DELETE FROM THOLIDAYS WHERE HOLIDAY_ID = @IN_HOLIDAY_ID
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END
    DELETE FROM THOLIDAYDATE WHERE HOLIDAY_ID = @IN_HOLIDAY_ID
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END

    SELECT @SSUMMARY = N'删除节假日：' + @V_HOLIDAY_NAME + CASE @V_CAL_FLAG WHEN 1 THEN N'；公历：' ELSE N'；农历：' END + dbo.fn_getmonthday(@V_HOLIDAY_DAY)
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END

    COMMIT TRANSACTION
    RETURN 100
GO
