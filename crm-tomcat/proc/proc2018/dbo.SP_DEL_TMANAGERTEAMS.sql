USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_DEL_TMANAGERTEAMS   @IN_TEAM_ID             INT,            --团队ID
                                        @IN_INPUT_MAN           INT = 0         --操作员


WITH ENCRYPTION
AS
    DECLARE @V_RET_CODE INT, @IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40), @SSUMMARY NVARCHAR(200)
    DECLARE @V_TEAM_NAME NVARCHAR(40), @V_ERROR NVARCHAR(200)
    SELECT @V_RET_CODE = -30402000, @IBUSI_FLAG = 30402
    SELECT @SBUSI_NAME = N'删除营销团队记录', @SSUMMARY = N'删除营销团队记录'
    BEGIN TRY
    IF NOT EXISTS(SELECT 1 FROM  TManagerTeams WHERE TEAM_ID = @IN_TEAM_ID)
        RETURN @V_RET_CODE - 1 --对应团队信息不存在
    SELECT TOP 1 @V_TEAM_NAME =TEAM_NAME FROM TManagerTeams WHERE PARENT_ID=@IN_TEAM_ID
    IF @@ROWCOUNT>0
    BEGIN
        SET @V_ERROR = '请先删除所有子团队，目前至少有子团队：'+@V_TEAM_NAME
        RAISERROR(@V_ERROR,16,1)
    END

    BEGIN TRANSACTION
    --先删除团队成员列表
    DELETE FROM TManagerTeamMembers WHERE TEAM_ID = @IN_TEAM_ID
    
    --再删除团队信息
    DELETE FROM  TManagerTeams WHERE TEAM_ID = @IN_TEAM_ID
    
    SET @SSUMMARY = N'删除营销团队记录，操作员：' + CAST(@IN_INPUT_MAN AS NVARCHAR(10))
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    
    COMMIT TRANSACTION
    END TRY
    
    BEGIN CATCH
        IF @@TRANCOUNT > 0 BEGIN
            ROLLBACK TRANSACTION
        END
        
        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)

        SELECT @V_ERROR_STR = N'Message:%s,Error:%d,Level:%d,State:%d,Procedure:%s,Line:%d',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()

        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_NUMBER,@V_ERROR_SEVERITY,@V_ERROR_STATE,
                  @V_ERROR_PROCEDURE,@V_ERROR_LINE)
        
        RETURN -100
    END CATCH
GO
