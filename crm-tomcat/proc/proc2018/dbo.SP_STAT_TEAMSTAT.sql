USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_STAT_TEAMSTAT @IN_TEAM_ID      INT,             --团队ID
                                  @IN_MANAGERNAME  NVARCHAR(128),   --团队成员名称(客户经理)
                                  @IN_INPUT_MAN    INT,             --操作员
                                  @IN_QUERY_DATE   INT              --查询日期
WITH ENCRYPTION
AS
    SET NOCOUNT ON
    IF @IN_QUERY_DATE = 0 OR @IN_QUERY_DATE IS NULL
        SET @IN_QUERY_DATE = dbo.GETDATEINT(GETDATE())

    CREATE TABLE #TEMP_QUERY_TEAMPRE
    (
        SERIAL_NO       INT NOT NULL IDENTITY,
        TEAM_ID         INT,             --团队ID
        TEAM_NO         NVARCHAR(60),    --团队编号
        TEAM_NAME       NVARCHAR(160),   --团队名称
        LEADER          INT,             --负责人ID
        LEADER_NAME     NVARCHAR(60),    --团队负责人
        LINK_MAN        INT,             --销售人员
        LINK_MAN_NAME   NVARCHAR(30),    --销售人员名称
        RG_DATE         INT,             --预约日期
        RG_MONEY        DEC(20,3)       --到帐金额
    )
    INSERT INTO #TEMP_QUERY_TEAMPRE(LINK_MAN,LINK_MAN_NAME,RG_MONEY,RG_DATE)
        SELECT A.LINK_MAN,C.OP_NAME,A.RG_MONEY,A.START_DATE
        FROM INTRUST..TCONTRACT A,TOPERATOR C
        WHERE A.LINK_MAN = C.OP_CODE AND A.START_DATE/10000 = @IN_QUERY_DATE/10000
    UPDATE #TEMP_QUERY_TEAMPRE
        SET TEAM_ID = B.TEAM_ID, TEAM_NO = B.TEAM_NO, TEAM_NAME = B.TEAM_NAME
        FROM #TEMP_QUERY_TEAMPRE A, TManagerTeamMembers B
        WHERE A.LINK_MAN = B.MANAGERID
    UPDATE #TEMP_QUERY_TEAMPRE
        SET TEAM_ID = B.TEAM_ID, TEAM_NO = B.TEAM_NO, TEAM_NAME = B.TEAM_NAME
        FROM #TEMP_QUERY_TEAMPRE A, TManagerTeams B
        WHERE A.LINK_MAN = B.LEADER
    UPDATE #TEMP_QUERY_TEAMPRE
        SET LEADER = B.LEADER, LEADER_NAME = B.LEADER_NAME
        FROM #TEMP_QUERY_TEAMPRE A, TManagerTeams B
        WHERE A.TEAM_ID = B.TEAM_ID

    SELECT  A.TEAM_ID,A.TEAM_NO,A.TEAM_NAME,A.LEADER,A.LEADER_NAME,
            ISNULL(B.RG_MONEY,0) AS RG_MONEY_CURRYEAR, ISNULL(B.RG_MONEY_CURRMONTH,0) AS RG_MONEY_CURRMONTH
    FROM TManagerTeams A LEFT JOIN
        (SELECT TEAM_ID,TEAM_NO,SUM(CASE WHEN RG_DATE/100=@IN_QUERY_DATE/100 THEN RG_MONEY ELSE 0 END) AS RG_MONEY_CURRMONTH, SUM(RG_MONEY) AS RG_MONEY
            FROM #TEMP_QUERY_TEAMPRE C
            WHERE (C.TEAM_ID = @IN_TEAM_ID OR @IN_TEAM_ID IS NULL OR @IN_TEAM_ID = 0)
                AND(LINK_MAN_NAME LIKE '%'+@IN_MANAGERNAME+'%' OR @IN_MANAGERNAME IS NULL OR LINK_MAN_NAME IS NULL)
            GROUP BY C.TEAM_ID,C.TEAM_NO) B ON A.TEAM_ID = B.TEAM_ID
    ORDER BY RG_MONEY DESC
    RETURN 100
GO
