USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_MODI_TSQREDEEM_CRM @IN_SERIAL_NO        INTEGER,                    --SERIAL_NO
                                   @IN_REDEEM_AMOUNT        DECIMAL(16,3),              --申请赎回份额
                                   @IN_SQ_DATE              INTEGER,                    --申请办理日期
                                   @IN_INPUT_MAN            INTEGER,                    --录入操作员
                                   @IN_NAV_PRICE            DECIMAL(16,10)  = 0,        --赎回净值
                                   @IN_OPEN_DATE            INTEGER         = NULL,     --赎回开放日
                                   @IN_DEC_BEN_MONEY        DECIMAL(16,3)   = 0,        --赎回信托本金
                                   @IN_REDEEM_MONEY         DECIMAL(16,3)   = 0,        --赎回金额
                                   @IN_FEE_MONEY            DECIMAL(16,3)   = 0,        --其中：赎回费用
                                   @IN_TRANSFER_PRODUCT_ID  INTEGER         = NULL,     --转入产品
                                   @IN_TRANSFER_SUB_PRODUCT_ID  INTEGER     = NULL,     --转入子产品
                                   @IN_TRANSFER_MONEY       DECIMAL(16,3)   = NULL      --转入金额
WITH ENCRYPTION
AS
    DECLARE @V_PRODUCT_CODE VARCHAR(6),@V_PRODUCT_NAME NVARCHAR(60),@V_TA_FLAG INT
    DECLARE @V_CONTRACT_BH VARCHAR(16),@V_BL_SERIAL_NO INT,@V_CUST_NAME NVARCHAR(100)
    DECLARE @V_BEN_DATE INT,@V_BEN_AMOUNT DECIMAL(16,3),@V_ORDER_FLAG INTEGER,@V_CHECK_FLAG INT
    DECLARE @V_REDEEM_AMOUNT DECIMAL(16,3),@V_REDEEM_MONEY DECIMAL(16,3),@V_FEE_MONEY DECIMAL(16,3),
            @V_SQ_DATE INTEGER,@V_NAV_PRICE DECIMAL(16,10),@V_COERCE_FLAG INT
    DECLARE @V_COERCE_REDEEM_FLAG INT,@V_MIN_REDEEM_VOL DECIMAL(16,2),
            @V_REDEEM_AMOUNT_COERCE DECIMAL(16,2),@V_REDEEM_FREEZE INT,@V_REDEEM_AMOUNT_COERCE0 DECIMAL(16,2)
    DECLARE @V_REDEEM_AMOUNT0 DECIMAL(16,3)
    DECLARE @V_FULLAMOUNT_FLAG INT
    DECLARE @V_RET_CODE INT,@IBUSI_FLAG INT,@SBUSI_NAME VARCHAR(40),@SSUMMARY VARCHAR(200)
    
    BEGIN TRY
    
    SET @V_RET_CODE = -20404000
    SET @IBUSI_FLAG = 20404
    SET @SBUSI_NAME = '修改赎回申请记录'

    
    SELECT @V_CHECK_FLAG = A.CHECK_FLAG,@V_BL_SERIAL_NO = A.BL_SERIAL_NO,@V_TA_FLAG = ISNULL(A.TA_FLAG,2),
           @V_ORDER_FLAG = A.ORDER_FLAG,@V_REDEEM_AMOUNT = A.REDEEM_AMOUNT,@V_SQ_DATE = A.SQ_DATE,
           @V_NAV_PRICE = A.NAV_PRICE,@V_REDEEM_MONEY = A.REDEEM_MONEY,@V_FEE_MONEY = A.FEE,
           @V_CONTRACT_BH = A.CONTRACT_BH,@V_BEN_DATE = B.BEN_DATE,@V_BEN_AMOUNT = B.BEN_AMOUNT,@V_CUST_NAME = C.CUST_NAME,
           @V_PRODUCT_CODE = D.PRODUCT_CODE,@V_PRODUCT_NAME = D.PRODUCT_NAME,
           @V_COERCE_REDEEM_FLAG = E.COERCE_REDEEM_FLAG,@V_REDEEM_FREEZE = ISNULL(E.REDEEM_FREEZE,0),
           @V_MIN_REDEEM_VOL = ISNULL(CASE C.CUST_TYPE WHEN 1 THEN E.MIN_REDEEM_VOL ELSE E.MIN_REDEEM_VOL2 END,0)
        FROM INTRUST..TSQREDEEM A,INTRUST..TBENIFITOR B,INTRUST..TCUSTOMERINFO C,TPRODUCT D LEFT JOIN INTRUST..TPRODUCTLIMIT E ON D.PRODUCT_ID = E.PRODUCT_ID
        WHERE A.SERIAL_NO = @IN_SERIAL_NO AND A.BEN_SERIAL_NO = B.SERIAL_NO AND 
              B.CUST_ID = C.CUST_ID AND A.PRODUCT_ID = D.PRODUCT_ID
    IF @@ROWCOUNT=0
		RAISERROR('赎回记录不存在',16,1)

    IF @V_CHECK_FLAG <> 1 AND ISNULL(@IN_SQ_DATE,0) <> 30000101
        RAISERROR('该记录已经审核不能修改',16,2)

    IF @V_ORDER_FLAG = 2
        SELECT @V_BEN_AMOUNT = BEN_AMOUNT FROM INTRUST..TBENLIST WHERE SERIAL_NO = @V_BL_SERIAL_NO
        
    IF @IN_REDEEM_AMOUNT > @V_BEN_AMOUNT
        RAISERROR('赎回份额不能大于受益份额',16,3)

    IF dbo.GETDATEDIFF(@V_BEN_DATE,@IN_SQ_DATE) < @V_REDEEM_FREEZE AND @V_REDEEM_FREEZE > 0
        RAISERROR('赎回冻结期内不能进行赎回申请!',16,4)

    SELECT @V_COERCE_FLAG = 2,@V_REDEEM_AMOUNT_COERCE = 0,@V_REDEEM_AMOUNT_COERCE0 = 0,@V_FULLAMOUNT_FLAG = 2
    
    SET @V_FULLAMOUNT_FLAG = CASE WHEN @IN_REDEEM_AMOUNT = @V_BEN_AMOUNT THEN 1 ELSE 2 END
    
    IF ISNULL(@V_COERCE_REDEEM_FLAG,2) = 1 /*AND @V_ORDER_FLAG = 1*/ 
    BEGIN
        SET @V_REDEEM_AMOUNT_COERCE0 = @V_BEN_AMOUNT - @IN_REDEEM_AMOUNT
        IF 0 < @V_REDEEM_AMOUNT_COERCE0 AND @V_REDEEM_AMOUNT_COERCE0 < ISNULL(@V_MIN_REDEEM_VOL,0) 
        BEGIN
            SELECT @V_COERCE_FLAG = 1
            SELECT @V_REDEEM_AMOUNT_COERCE = @V_REDEEM_AMOUNT_COERCE0
            SELECT @V_FULLAMOUNT_FLAG = 1
        END
    END

    BEGIN TRANSACTION

    IF ISNULL(@IN_SQ_DATE,0) <> 30000101 
    BEGIN
        UPDATE INTRUST..TSQREDEEM
            SET REDEEM_AMOUNT        = @IN_REDEEM_AMOUNT,
                REDEEM_AMOUNT0       = @IN_REDEEM_AMOUNT + ISNULL(@V_REDEEM_AMOUNT_COERCE,0),
                COERCE_FLAG          = @V_COERCE_FLAG,
                REDEEM_AMOUNT_COERCE = @V_REDEEM_AMOUNT_COERCE,
                REDEEM_MONEY         = @IN_REDEEM_MONEY,
                FEE                  = @IN_FEE_MONEY,
                SQ_DATE              = @IN_SQ_DATE,
                NAV_PRICE            = @IN_NAV_PRICE,
                TRANS_DATE           = @IN_OPEN_DATE,
                DEC_BEN_MONEY        = @IN_DEC_BEN_MONEY,
                FULLAMOUNT_FLAG      = @V_FULLAMOUNT_FLAG,
                TRANSFER_PRODUCT_ID  = @IN_TRANSFER_PRODUCT_ID,
                TRANSFER_SUB_PRODUCT_ID = @IN_TRANSFER_SUB_PRODUCT_ID,
                TRANSFER_MONEY       = @IN_TRANSFER_MONEY
            WHERE SERIAL_NO = @IN_SERIAL_NO
        /*
        --强制赎回
        IF EXISTS(SELECT 1 FROM TSQREDEEM WHERE REDEEM_ID = @IN_SERIAL_NO AND COERCE_FLAG = 1)
            DELETE FROM TSQREDEEM WHERE REDEEM_ID = @IN_SERIAL_NO AND COERCE_FLAG = 1
        
        IF @V_TA_FLAG <> 1 AND @V_COERCE_FLAG = 1
            INSERT INTO TSQREDEEM(BEN_SERIAL_NO,PRODUCT_ID,CONTRACT_BH,LIST_ID,REDEEM_AMOUNT,REDEEM_AMOUNT0,
                                  SQ_DATE,INPUT_MAN,ORDER_FLAG,BL_SERIAL_NO,NAV_PRICE,TRANS_DATE,
                                  REDEEM_RATE,REDEEM_MONEY,FEE,COERCE_FLAG,REDEEM_ID)
                SELECT BEN_SERIAL_NO,PRODUCT_ID,CONTRACT_BH,LIST_ID,@V_REDEEM_AMOUNT_COERCE,@V_REDEEM_AMOUNT_COERCE,
                       @IN_SQ_DATE,@IN_INPUT_MAN,@V_ORDER_FLAG,@V_BL_SERIAL_NO,@IN_NAV_PRICE,@IN_OPEN_DATE,
                       1,@IN_REDEEM_MONEY,@IN_FEE_MONEY,@V_COERCE_FLAG,SERIAL_NO
                    FROM TSQREDEEM
                    WHERE SERIAL_NO = @IN_SERIAL_NO 
        */
    END
    ELSE BEGIN
        UPDATE INTRUST..TSQREDEEM
            SET DEC_BEN_MONEY = @IN_DEC_BEN_MONEY
            WHERE SERIAL_NO = @IN_SERIAL_NO
    END

    SET @SSUMMARY = @SBUSI_NAME + N',赎回记录号：' + RTRIM(CONVERT(NVARCHAR,@IN_SERIAL_NO)) +
                                  N'，产品编号：' + @V_PRODUCT_CODE +
                                  N'，产品名称：' + @V_PRODUCT_NAME +
                                  N'，合同序号：' + @V_CONTRACT_BH  +
                                  N'，受益人：' + @V_CUST_NAME
    
    IF @V_REDEEM_AMOUNT <> @IN_REDEEM_AMOUNT
        SET @SSUMMARY = @SSUMMARY + N'，赎回份额：' + RTRIM(CONVERT(CHAR,@V_REDEEM_AMOUNT)) + N'->' + RTRIM(CONVERT(CHAR,@IN_REDEEM_AMOUNT))
    IF @V_SQ_DATE <> @IN_SQ_DATE
        SET @SSUMMARY = @SSUMMARY + N'，申购日期：' + RTRIM(CONVERT(CHAR,@V_SQ_DATE)) + N'->' + RTRIM(CONVERT(CHAR,@IN_SQ_DATE))
    IF @V_NAV_PRICE <> @IN_NAV_PRICE
        SET @SSUMMARY = @SSUMMARY + N'，净值：' + RTRIM(CONVERT(CHAR,@V_NAV_PRICE)) + N'->' + RTRIM(CONVERT(CHAR,@IN_NAV_PRICE))
    IF @V_REDEEM_MONEY <> @IN_REDEEM_MONEY
        SET @SSUMMARY = @SSUMMARY + N'，赎回金额：' + RTRIM(CONVERT(CHAR,@V_REDEEM_MONEY)) + N'->' + RTRIM(CONVERT(CHAR,@IN_REDEEM_MONEY))
    IF @V_FEE_MONEY <> @IN_FEE_MONEY
        SET @SSUMMARY = @SSUMMARY + N'，其中费用：' + RTRIM(CONVERT(CHAR,@V_FEE_MONEY)) + N'->' + RTRIM(CONVERT(CHAR,@IN_FEE_MONEY))
        
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)

    COMMIT TRANSACTION
    END TRY

    BEGIN CATCH
        IF @@TRANCOUNT > 0 BEGIN
            ROLLBACK TRANSACTION
        END
        
        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)

        SELECT @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()

        --如果是本过程抛出异常，则只抛出本过程相关的异常信息
        IF @V_ERROR_PROCEDURE = 'SP_MODI_TSQREDEEM_CRM' BEGIN
            SELECT @V_ERROR_STR = N'过程：%s，第 %d 行，错误：%s'
            RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,@V_ERROR_STATE,@V_ERROR_PROCEDURE,@V_ERROR_LINE,@V_ERROR_MESSAGE)
        END
        --如果是内部过程抛出异常，则只抛出内部过程相关的异常信息
        ELSE BEGIN
            SELECT @V_ERROR_STR = N'%s'
            RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,@V_ERROR_STATE,@V_ERROR_MESSAGE)
        END
        
        RETURN -100
    END CATCH
    
    RETURN 100
GO
