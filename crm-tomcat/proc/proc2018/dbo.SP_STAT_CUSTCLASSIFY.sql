USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_STAT_CUSTCLASSIFY  @IN_CLASSDEFINE_ID    INT,    --分类ID
                                       @IN_BOOK_CODE         INT,    --帐套
                                       @IN_INPUT_MAN         INT     --操作员
WITH ENCRYPTION
AS
    DECLARE @V_DT_INTRUST INT
    SELECT @V_DT_INTRUST = VALUE FROM TSYSCONTROL WHERE FLAG_TYPE = N'DT_INTRUST'
    DECLARE @V_SUM_MONEY DECIMAL(16,3)
    CREATE TABLE #TMP_CUSTMONEY (
        CUST_ID     INT,
        RG_MONEY    DEC(16,3),
        CLASSDETAIL_ID      INT
    )
    DECLARE @V_MONEY TABLE
    (
        CLASSDETAIL_ID      INT,
        CLASSDETAIL_NAME    NVARCHAR(60),
        RG_MONEY        DECIMAL(16,3),
        PERCENTAGE          DECIMAL(16,3)
    )
    INSERT INTO #TMP_CUSTMONEY(CUST_ID) SELECT CUST_ID FROM TCustomers
    UPDATE #TMP_CUSTMONEY SET CLASSDETAIL_ID = B.CLASSDETAIL_ID
        FROM #TMP_CUSTMONEY A, TCUSTOMERCLASS B
        WHERE A.CUST_ID = B.CUST_ID AND B.CLASSDEFINE_ID = @IN_CLASSDEFINE_ID
    IF @V_DT_INTRUST = 1 --启用分布式
        UPDATE #TMP_CUSTMONEY SET RG_MONEY = B.RG_MONEY
            FROM #TMP_CUSTMONEY A, ( SELECT CUST_ID, SUM(ISNULL(RG_MONEY,0)) RG_MONEY FROM SRV_Intrust.INTRUST.dbo.TCONTRACT
                                     WHERE BOOK_CODE = @IN_BOOK_CODE AND ISNULL(RG_MONEY,0) <> 0 GROUP BY CUST_ID ) B
            WHERE A.CUST_ID = B.CUST_ID
    ELSE IF (@V_DT_INTRUST = 2) AND EXISTS(SELECT * FROM master..sysdatabases WHERE NAME = N'INTRUST') --本地信托数据库
        UPDATE #TMP_CUSTMONEY SET RG_MONEY = B.RG_MONEY
            FROM #TMP_CUSTMONEY A, ( SELECT CUST_ID, SUM(ISNULL(RG_MONEY,0)) RG_MONEY FROM INTRUST..TCONTRACT
                                     WHERE BOOK_CODE = @IN_BOOK_CODE AND ISNULL(RG_MONEY,0) <> 0 GROUP BY CUST_ID ) B
            WHERE A.CUST_ID = B.CUST_ID

    --金额
    INSERT INTO @V_MONEY(CLASSDETAIL_ID,CLASSDETAIL_NAME, RG_MONEY)
        SELECT A.CLASSDETAIL_ID, B.CLASSDETAIL_NAME, SUM(A.RG_MONEY)
        FROM #TMP_CUSTMONEY A LEFT JOIN TCustClassDetail B ON A.CLASSDETAIL_ID = B.CLASSDETAIL_ID
        GROUP BY A.CLASSDETAIL_ID, B.CLASSDETAIL_NAME
    SELECT @V_SUM_MONEY = SUM(RG_MONEY) FROM @V_MONEY
    UPDATE @V_MONEY SET PERCENTAGE = ROUND(1.0*RG_MONEY/@V_SUM_MONEY*100,2)
    UPDATE @V_MONEY SET CLASSDETAIL_ID = 0, CLASSDETAIL_NAME = '未分类' WHERE CLASSDETAIL_ID IS NULL
    --人数
    DECLARE @V_SUM INT
    DECLARE @V_PERSON TABLE
    (
        CLASSDETAIL_ID      INT,
        CLASSDETAIL_NAME    NVARCHAR(60),
        NUM                 INT,
        PERCENTAGE          DECIMAL(16,3)
    )
    INSERT INTO @V_PERSON(CLASSDETAIL_ID,CLASSDETAIL_NAME, NUM)
        SELECT A.CLASSDETAIL_ID, B.CLASSDETAIL_NAME, COUNT(*)
        FROM #TMP_CUSTMONEY A LEFT JOIN TCustClassDetail B ON A.CLASSDETAIL_ID = B.CLASSDETAIL_ID
        GROUP BY A.CLASSDETAIL_ID, B.CLASSDETAIL_NAME
    SELECT @V_SUM = SUM(NUM) FROM @V_PERSON
    UPDATE @V_PERSON SET PERCENTAGE = ROUND(1.0*NUM/@V_SUM*100,2)
    UPDATE @V_PERSON SET CLASSDETAIL_ID = 0, CLASSDETAIL_NAME = '未分类' WHERE CLASSDETAIL_ID IS NULL

    --总和
    SELECT A.CLASSDETAIL_ID,A.CLASSDETAIL_NAME,A.RG_MONEY,@V_SUM_MONEY SUM_MONEY,A.PERCENTAGE MONEY_RATE,B.NUM,@V_SUM SUM_NUM,B.PERCENTAGE NUM_RATE
        FROM @V_MONEY A,@V_PERSON B
        WHERE A.CLASSDETAIL_ID=B.CLASSDETAIL_ID
GO
