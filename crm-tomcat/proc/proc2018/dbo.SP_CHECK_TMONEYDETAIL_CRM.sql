USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_CHECK_TMONEYDETAIL_CRM @IN_SERIAL_NO    INTEGER,            --缴款流水号
                                       @IN_TO_BANK_DATE     INTEGER,            --实际到账日期
                                       @IN_INPUT_MAN        INTEGER,            --审核人
                                       @IN_SBF_SERIAL_NO    INTEGER = 0         --资金到账银行账号员
WITH ENCRYPTION
AS
    --SET NOCOUNT ON
    DECLARE @V_RET_CODE INT,@IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)
    DECLARE @V_PRODUCT_ID INT,@V_SUB_PRODUCT_ID INT,@V_PRE_NUM INT,@V_PRE_MONEY DEC(18,3)
    DECLARE @V_INPUT_MAN INT,@V_CHECK_FLAG INT,@V_ISMONYFH INT
    SELECT @SBUSI_NAME = N'到账资金审核'
    SELECT @SSUMMARY = N'到账资金审核'
    SELECT @IBUSI_FLAG = 30219
    
    BEGIN TRY
		SELECT @V_CHECK_FLAG=CHECK_FLAG FROM INTRUST..TMONEYDETAIL WHERE SERIAL_NO = @IN_SERIAL_NO
        IF @@ROWCOUNT=0
            RAISERROR('记录不存在',16,1)

        IF ISNULL(@V_CHECK_FLAG,1)<>1
            RAISERROR('已复核，不能再次复核',16,1)
     
    BEGIN TRANSACTION
        --资金到账需要业务经理审核时，必须先做业务经理审核，才能做此审核；资金到账不需要业务经理审核时，表TMONEYDETAIL中的CHECK_FLAG必须为1时，才能做复核
        SELECT @V_ISMONYFH = VALUE FROM INTRUST..TSYSCONTROL WHERE FLAG_TYPE = 'ISMONYFH'
        IF @V_ISMONYFH <> 1--直接一步到位审核（此时CRM客户经理代替了财务审核）
        BEGIN        
            EXEC INTRUST..SP_CHECK_TMONEYDETAIL @IN_SERIAL_NO,@IN_TO_BANK_DATE,@IN_INPUT_MAN,@IN_SBF_SERIAL_NO
            --RAISERROR('不需要业务经理审核！',16,1)
        END
        ELSE
		BEGIN
			IF ISNULL(@IN_TO_BANK_DATE,0)=0
				UPDATE INTRUST..TMONEYDETAIL SET CHECK_FLAG=9 WHERE SERIAL_NO = @IN_SERIAL_NO
			ELSE
				UPDATE INTRUST..TMONEYDETAIL SET CHECK_FLAG=9,TO_BANK_DATE=@IN_TO_BANK_DATE WHERE SERIAL_NO = @IN_SERIAL_NO
		END
		SET @SSUMMARY = N'到账资金审核，操作员：' + CAST(@IN_INPUT_MAN AS NVARCHAR(10))+'，记录号：'+CAST(@IN_SERIAL_NO AS VARCHAR)
		INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
			VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    
    COMMIT TRANSACTION
    END TRY

    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION

        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)

        SELECT @V_ERROR_STR = N'Message:%s,Procedure:%s,Line:%d',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()

        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_PROCEDURE,@V_ERROR_LINE)

        RETURN -100
    END CATCH
    RETURN 100
GO
