USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_MODI_TGAINLEVELRATE @IN_SERIAL_NO     INTEGER,        --记录号
                                        @IN_GAIN_RATE     DECIMAL(5,4),   --收益率
                                        @IN_START_DATE    INTEGER,        --起始日期
                                        @IN_END_DATE      INTEGER,        --结束日期
                                        @IN_INPUT_MAN     INTEGER         --操作员
WITH ENCRYPTION 
AS  
    DECLARE @V_PRODUCT_ID INT,@V_PRODUCT_CODE NVARCHAR(6),@V_PRODUCT_NAME NVARCHAR(60)
    DECLARE @V_SUB_PRODUCT_ID INT,@V_SUB_PRODUCT_NAME NVARCHAR(60)
    DECLARE @V_DF_SERIAL_NO INT,@V_PROV_LEVEL_NAME NVARCHAR(20)
    DECLARE @V_GAIN_RATE DECIMAL(5,4),@V_START_DATE INT,@V_END_DATE INT
    DECLARE @V_GAIN_FLAG INT,@V_PERIOD_UNIT INT
    DECLARE @IBUSI_FLAG INT
    DECLARE @SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)

    SET @IBUSI_FLAG = 3300109
    SET @SBUSI_NAME = N'修改收益率'
    
    BEGIN TRY
        BEGIN TRANSACTION

        IF @IN_START_DATE > @IN_END_DATE
            RAISERROR('起始日期不能大于结束日期',16,1)
        
        SELECT @V_PRODUCT_ID = B.PREPRODUCT_ID,@V_PRODUCT_NAME = B.PREPRODUCT_NAME,@V_PROV_LEVEL_NAME = A.PROV_LEVEL_NAME,
               @V_DF_SERIAL_NO = D.DF_SERIAL_NO,@V_GAIN_RATE = D.GAIN_RATE,@V_START_DATE = D.START_DATE,@V_END_DATE = D.END_DATE
            FROM TGAINLEVELRATE D LEFT JOIN TGAINLEVEL A ON D.DF_SERIAL_NO=A.SERIAL_NO
                LEFT JOIN TPREPRODUCT B ON A.PREPRODUCT_ID=B.PREPRODUCT_ID
            WHERE D.SERIAL_NO = @IN_SERIAL_NO
        --修改上一区间的结束日期
        IF EXISTS(SELECT 1 FROM TGAINLEVELRATE WHERE DF_SERIAL_NO = @V_DF_SERIAL_NO AND END_DATE = dbo.GETDATE(@V_START_DATE,-1))
        BEGIN
            UPDATE TGAINLEVELRATE SET END_DATE = dbo.GETDATE(@IN_START_DATE,-1) WHERE DF_SERIAL_NO = @V_DF_SERIAL_NO AND END_DATE = dbo.GETDATE(@V_START_DATE,-1)
        END
        --修改下一区间的开始日期
        IF EXISTS(SELECT 1 FROM TGAINLEVELRATE WHERE DF_SERIAL_NO = @V_DF_SERIAL_NO AND START_DATE = dbo.GETDATE(@V_END_DATE,1))
        BEGIN
            UPDATE TGAINLEVELRATE SET START_DATE = dbo.GETDATE(@IN_END_DATE,1) WHERE DF_SERIAL_NO = @V_DF_SERIAL_NO AND START_DATE = dbo.GETDATE(@V_END_DATE,1)
        END
        --业务处理
        UPDATE TGAINLEVELRATE
            SET GAIN_RATE = @IN_GAIN_RATE,
                START_DATE = @IN_START_DATE,
                END_DATE = @IN_END_DATE
            WHERE SERIAL_NO = @IN_SERIAL_NO

        --日志记录
        SET @SSUMMARY = @SBUSI_NAME + N'，预发行产品，产品名称：' + @V_PRODUCT_NAME
                                    + N'，收益级别：' + ISNULL(@V_PROV_LEVEL_NAME,'')
        IF @V_GAIN_RATE <> @IN_GAIN_RATE
            SET @SSUMMARY = @SSUMMARY + N'，收益率：' + CONVERT(NVARCHAR,ISNULL(@V_GAIN_RATE,0)) + '-->' + CONVERT(NVARCHAR,ISNULL(@IN_GAIN_RATE,0))
        IF @V_START_DATE <> @IN_START_DATE
            SET @SSUMMARY = @SSUMMARY + N'，起始日期：' + CONVERT(NVARCHAR,ISNULL(@V_START_DATE,0)) + '-->' + CONVERT(NVARCHAR,ISNULL(@IN_START_DATE,0))
        IF @V_END_DATE <> @IN_END_DATE
            SET @SSUMMARY = @SSUMMARY + N'，结束日期：' + CONVERT(NVARCHAR,ISNULL(@V_END_DATE,0)) + '-->' + CONVERT(NVARCHAR,ISNULL(@IN_END_DATE,0))
        INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
            VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
            
        COMMIT TRANSACTION
        RETURN 100
    END TRY
    
    BEGIN CATCH
        IF @@TRANCOUNT > 0 BEGIN
            ROLLBACK TRANSACTION
        END
        
        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)

        SELECT @V_ERROR_STR = N'Error %d,Level %d,State %d,Procedure %s,Line %d,Message %s',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()

        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_NUMBER,@V_ERROR_SEVERITY,@V_ERROR_STATE,
                  @V_ERROR_PROCEDURE,@V_ERROR_LINE,@V_ERROR_MESSAGE)

        RETURN -100
    END CATCH
GO
