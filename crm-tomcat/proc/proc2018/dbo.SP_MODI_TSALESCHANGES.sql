USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE SP_MODI_TSALESCHANGES @IN_SERIAL_NO  INT, --序号 
											 @IN_INPUT_MAN     INT,
											 @IN_TO_SERVICE_MAN    INT, --转客户经理
											 @IN_ZR_MONEY          DEC(16,3),   --转让金额
											 @IN_CHANGE_REASON     NVARCHAR(40) -- 转让事由
WITH ENCRYPTION
AS
    SET NOCOUNT ON
    DECLARE @V_ERROR NVARCHAR(200)
    DECLARE @V_STATUS INT -- 状态 1-待确认 2-确认未通过 3-待审核 4-审核未通过 5-已通过
    DECLARE @V_STATUS_NAME NVARCHAR(50)
    
    SELECT @V_STATUS=1
    SELECT @V_STATUS_NAME=N'待确认'
    DECLARE @IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)

    SELECT @IBUSI_FLAG = 655352
    SELECT @SBUSI_NAME = N'编辑转销量申请'
    SELECT @SSUMMARY = N'编辑转销量申请'
    
    DECLARE @V_CUST_ID INT
    DECLARE @V_CUST_NAME NVARCHAR(100)
    
    DECLARE @V_PRE_SERIAL_NO INT--预约表ID
	DECLARE @V_PRE_MONEY_SERIAL_NO INT--预约到帐表ID
	
	DECLARE @V_FK_TSALESRESULT INT--预约表ID
    
    DECLARE @V_TO_SERVICE_MAN_NAME NVARCHAR(100)
    DECLARE @V_PREPRODUCT_ID INT,@V_PREPRODUCT_NAME NVARCHAR(200)
    
    DECLARE @V_ZR_MONEY DEC(16,3)
    
    DECLARE @V_FROM_SERVICE_MAN INT

    --DECLARE @V_PRE_CODE NVARCHAR(16), @V_PREPRODUCT_NAME NVARCHAR(100), @V_PREPRODUCT_ID INT, @V_PRE_SERIAL_NO INT
    
    /*DECLARE @V_STATUS INT -- -1未审核 0审核未通过 1审核已通过
    DECLARE @V_STATUS_NAME NVARCHAR(50)*/
    ------------------------------------------------------------------------
    BEGIN TRY
    --1.业务逻辑与操作
    --校验
    IF NOT EXISTS(SELECT 1 FROM TSALES_CHANGES WHERE SERIAL_NO = @IN_SERIAL_NO)
    BEGIN
        SET @V_ERROR = N'记录不存在！'
        RAISERROR(@V_ERROR,16,1)
    END
    
    IF NOT EXISTS(SELECT 1 FROM TOPERATOR WHERE OP_CODE = @IN_TO_SERVICE_MAN)
    BEGIN
        SET @V_ERROR = N'转销量后客户经理不存在！'
        RAISERROR(@V_ERROR,16,5)
    END
    
	SELECT @V_PRE_SERIAL_NO = PRE_SERIAL_NO,@V_ZR_MONEY = CHANGE_MONEY,@V_FK_TSALESRESULT = FK_TSALESRESULT
	,@V_FROM_SERVICE_MAN = FROM_SERVICE_MAN
	FROM TSALES_CHANGES WHERE SERIAL_NO = @IN_SERIAL_NO
	
	IF @IN_TO_SERVICE_MAN = @V_FROM_SERVICE_MAN
    BEGIN
        SET @V_ERROR = N'转销量后客户经理不能为原销量客户经理！'
        RAISERROR(@V_ERROR,16,5)
    END	
	
	SELECT @V_TO_SERVICE_MAN_NAME=OP_NAME
    FROM TOPERATOR WHERE OP_CODE = @IN_TO_SERVICE_MAN
    
    IF NOT EXISTS(SELECT 1 FROM TPRECONTRACT WHERE SERIAL_NO = @V_PRE_SERIAL_NO)
    BEGIN
        SET @V_ERROR = N'预约记录不存在！'
        RAISERROR(@V_ERROR,16,3)
    END
    SELECT @V_PREPRODUCT_ID = PREPRODUCT_ID, @V_CUST_ID = CUST_ID FROM TPRECONTRACT WHERE SERIAL_NO = @V_PRE_SERIAL_NO  
    SELECT @V_PREPRODUCT_NAME = PREPRODUCT_NAME FROM TPREPRODUCT WHERE PREPRODUCT_ID = @V_PREPRODUCT_ID 
 
	
	IF NOT EXISTS(SELECT 1 FROM TCustomers WHERE CUST_ID = @V_CUST_ID)
    BEGIN
        SET @V_ERROR = N'客户信息不存在！'
        RAISERROR(@V_ERROR,16,3)
    END
    SELECT @V_CUST_NAME=CUST_NAME FROM TCustomers WHERE CUST_ID = @V_CUST_ID
	
	IF @V_STATUS>1
	BEGIN
        SET @V_ERROR = N'记录不能修改编辑！'
        RAISERROR(@V_ERROR,16,2)
    END
    
	/*SELECT @V_PREPRODUCT_ID = PREPRODUCT_ID, @V_PRE_CODE = PRE_CODE
        FROM TPRECONTRACT WHERE SERIAL_NO = @V_PRE_SERIAL_NO
    SELECT @V_PREPRODUCT_NAME = PREPRODUCT_NAME FROM TPREPRODUCT WHERE PREPRODUCT_ID = @V_PREPRODUCT_ID*/
    ------------------------------------------------------------------------
    BEGIN TRANSACTION

    UPDATE TSALES_CHANGES 
    SET TO_SERVICE_MAN = @IN_TO_SERVICE_MAN, TO_SERVICE_MAN_NAME = @V_TO_SERVICE_MAN_NAME,
     CHANGE_MONEY = @IN_ZR_MONEY, CHANGE_REASON = @IN_CHANGE_REASON
        WHERE SERIAL_NO = @IN_SERIAL_NO
    
	--变更到帐金额
    UPDATE TSALESRESULTFORSTATISTIC SET DZ_MONEY=@IN_ZR_MONEY WHERE FK_TSALES_CHANGES=@IN_SERIAL_NO   
	--变更原到帐金额
    --UPDATE TSALESRESULTFORSTATISTIC SET DZ_MONEY=(DZ_MONEY+@V_ZR_MONEY-@IN_ZR_MONEY) WHERE SERIAL_NO=@V_FK_TSALESRESULT
    
    --2.日志
        SELECT @SSUMMARY = N'编辑转销量申请，产品名称：'+@V_PREPRODUCT_NAME
						+N'客户名称：' + @V_CUST_NAME
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY) VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    COMMIT TRANSACTION
    END TRY
    --3.异常处理
    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION
        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)
        SELECT @V_ERROR_STR = N'Message:%s,Procedure:%s,Line:%d',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()
        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_PROCEDURE,@V_ERROR_LINE)
        RETURN -100
    END CATCH
    RETURN 100
GO
