USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_ADD_TCOCONTRACTMONEYMX @IN_PAYPLAN_ID    INT,              --付款计划ID，对应表TCOCONTRACTPAYPLAN. PAYPLAN_ID
                                           @IN_ARRIVE_TIME   INT,              --到款时间
                                           @IN_ARRIVE_MONEY  DECIMAL(16,3),    --到款金额
                                           @IN_ARRIVE_SUMMARY  NVARCHAR(2000), --备注
                                           @IN_INPUT_MAN     INT,               --录入人员，对应表TOPERATOR.OP_CODE
                                           @IN_FLAG          INT               --操作员可自己修改的到账标志，用于折扣等，即使到账金额不等于合同金额也可以更改
WITH ENCRYPTION
AS
    DECLARE @V_RET_CODE INT,@IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)
    DECLARE @V_COCONTRACT_ID INT,@V_COCONTRACT_SUB_BH NVARCHAR(200)
    DECLARE @V_PAY_NUM_NAME NVARCHAR(60)        --期数说明
    DECLARE @V_PAY_MONEY   DECIMAL(16,3)        --计划付款金额
    DECLARE @V_ARRIVE_MONEY DECIMAL(16,3)       --实际已到帐金额

    SELECT @SBUSI_NAME = N'合同管理之新增合同到帐明细'
    SELECT @SSUMMARY = N'合同管理之新增合同到帐明细'
    SELECT @IBUSI_FLAG = 50001
    SELECT @V_RET_CODE = -50001000

    SELECT @V_PAY_NUM_NAME = PAY_NUM_NAME,@V_COCONTRACT_ID =COCONTRACT_ID,@V_PAY_MONEY = PAY_MONEY,@V_ARRIVE_MONEY = ARRIVE_MONEY
        FROM TCOCONTRACTPAYPLAN WHERE PAYPLAN_ID =@IN_PAYPLAN_ID
    SELECT @V_COCONTRACT_SUB_BH = COCONTRACT_SUB_BH FROM TCOCONTRACT WHERE COCONTRACT_ID =@V_COCONTRACT_ID

    BEGIN TRANSACTION

    INSERT INTO TCOCONTRACTMONEYMX(PAYPLAN_ID,ARRIVE_TIME,ARRIVE_MONEY,INPUT_MAN,ARRIVE_SUMMARY)
        VALUES(@IN_PAYPLAN_ID,@IN_ARRIVE_TIME,@IN_ARRIVE_MONEY,@IN_INPUT_MAN,@IN_ARRIVE_SUMMARY)
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END
    --更新付款计划表中到帐金额
    IF @IN_FLAG=1
        BEGIN
            UPDATE TCOCONTRACTPAYPLAN
                SET ARRIVE_MONEY = ISNULL(ARRIVE_MONEY,0) + ISNULL(@IN_ARRIVE_MONEY,0),
                    ARRIVEMONEY_FLAG =3
                WHERE PAYPLAN_ID =@IN_PAYPLAN_ID
        END
    ELSE
        BEGIN
        UPDATE TCOCONTRACTPAYPLAN
            SET ARRIVE_MONEY = ISNULL(ARRIVE_MONEY,0) + ISNULL(@IN_ARRIVE_MONEY,0),
                ARRIVEMONEY_FLAG =CASE WHEN (@V_PAY_MONEY <=  (ISNULL(@V_ARRIVE_MONEY,0) + ISNULL(@IN_ARRIVE_MONEY,0))) THEN 3 ELSE 2 END
            WHERE PAYPLAN_ID =@IN_PAYPLAN_ID
        END
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END

    --更新收款计划提醒表(TMONEYTASKINFO,TMONEYTASKLIST)中已读标志，如果全部到账了就将该标志改为不再提示
    IF EXISTS(SELECT 1 FROM TMONEYTASKINFO WHERE RECORD_ID=@IN_PAYPLAN_ID) AND EXISTS(SELECT 1 FROM TCOCONTRACTPAYPLAN WHERE PAYPLAN_ID=@IN_PAYPLAN_ID AND ARRIVEMONEY_FLAG=3)
    BEGIN
        DECLARE @V_REC_NO INT,@V_SERIAL_NO INT
        SELECT @V_REC_NO=SERIAL_NO FROM TMONEYTASKINFO WHERE RECORD_ID=@IN_PAYPLAN_ID
        SELECT @V_SERIAL_NO=SERIAL_NO FROM TMONEYTASKLIST WHERE REC_NO=@V_REC_NO
        UPDATE TMONEYTASKLIST SET READ_FLAG=3 WHERE SERIAL_NO=@V_SERIAL_NO
    END
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END
    --
    SELECT @SSUMMARY = N'合同管理之新增合同到帐明细，合同编号：'+@V_COCONTRACT_SUB_BH+'，合同期数说明：'+@V_PAY_NUM_NAME +'，到帐日期：'+CONVERT(NVARCHAR(8),@IN_ARRIVE_TIME)+'，到帐金额：'+CONVERT(VARCHAR(30),@IN_ARRIVE_MONEY)
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END

    COMMIT TRANSACTION
    RETURN 100
GO
