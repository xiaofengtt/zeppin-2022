USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_STAT_COMPREHENSIVE_ANALYSIS_2 @IN_PRODUCT_ID    	INT, 		 	 --产品单元
												  @IN_BEGIN_DATE 		INT, 		 	 --开始日期
												  @IN_END_DATE   		INT, 		 	 --结日期
												  @IN_INPUT_MAN  		INT, 		 	 --操作员,
												  @IN_PRODUCT_STATUS	NVARCHAR(10)='', --产品状态
												  @IN_ANALYSIS_FLAG 	INT=1, 		 	 --1、按产品  2、按渠道  3、按客户经理
												  @IN_INTRUST_FLAG	INT=2,			 	 --1、单一  2、集合
												  @IN_PRODUCT_NAME	NVARCHAR(60)='', 	 --产品名称
												  @IN_CHANNEL_TYPE	NVARCHAR(10)
WITH ENCRYPTION
AS
    SET NOCOUNT ON
    CREATE TABLE #TMP_STAT_TABLE(
        PRODUCT_ID      	INTEGER,		--产品ID
		PRODUCT_CODE		NVARCHAR(10),   --产品编号
        PRODUCT_NAME    	NVARCHAR(60),   --产品名称
		START_DATE			INTEGER,		--产品开始时间
		END_DATE			INTEGER,        --产品结束时间
		PRODUCT_STATUS_NAME	NVARCHAR(10),	--产品状态
		PRODUCT_PRE_MONEY	DEC(16,3),		--产品规模
		PRE_MONEY			DEC(16,3),		--预约总规模
		PRE_NUM				INTEGER,		--预约总分数
		PRE_MONEY_2			DEC(16,3),		--已预约总规模
		PRE_NUM_2			INTEGER,		--已预约总份数
        RG_AMOUNT			DEC(16,3),		--认购份额
		SG_AMOUNT			DEC(16,3),		--申购份额
		SH_AMOUNT			DEC(16,3),		--赎回份额
		NEW_CUST_NUM		INTEGER,		--新增客户数
		TOTAL_CUST_NUM		INTEGER,        --客户总数
        CHANNEL_TYPE		NVARCHAR(10),   --渠道类别
		CHANNEL_TYPE_NAME	NVARCHAR(30),   --渠道名称
		SERVICE_MAN			INTEGER,   		--客户经理ID
		SERVICE_MAN_NAME	NVARCHAR(30)    --客户经理
    )

	--每个产品的新增客户
	CREATE TABLE #TEMP_NEW_CUST_TABLE(
			PRODUCT_ID		INTEGER,
			CHANNEL_TYPE	NVARCHAR(10),
			SERVICE_MAN		INTEGER,
			NEW_CUST_NUM	INTEGER
	)

	--按产品、渠道、客户经理来统计新增客户数
	BEGIN
	    SELECT CUST_ID,MIN(QS_DATE) AS FAST_BUY_DATE INTO #V_TEMP_FAST_TABLE
	        FROM INTRUST..TCONTRACT GROUP BY CUST_ID

		INSERT INTO #TEMP_NEW_CUST_TABLE(PRODUCT_ID,CHANNEL_TYPE,SERVICE_MAN,NEW_CUST_NUM)
			SELECT B.PRODUCT_ID,ISNULL(B.CHANNEL_TYPE,'') AS CHANNEL_TYPE,
				   B.SERVICE_MAN,COUNT(B.CUST_ID) AS NEW_CUST_NUM
				FROM INTRUST..TCONTRACT B,INTRUST..TPRODUCT C,#V_TEMP_FAST_TABLE F
					WHERE B.PRODUCT_ID = C.PRODUCT_ID
					AND (C.PRODUCT_NAME LIKE '%'+@IN_PRODUCT_NAME+'%' OR ISNULL(@IN_PRODUCT_NAME,'')='')
					AND (C.INTRUST_FLAG1 = @IN_INTRUST_FLAG OR ISNULL(@IN_INTRUST_FLAG,0)=0)
					AND (C.PRODUCT_STATUS = '110202')
					AND (B.CUST_ID = F.CUST_ID)
					AND (B.QS_DATE = F.FAST_BUY_DATE)
					AND (LEFT(B.CHANNEL_TYPE,6) = @IN_CHANNEL_TYPE OR ISNULL(@IN_CHANNEL_TYPE,'')='')
			GROUP BY B.PRODUCT_ID,ISNULL(B.CHANNEL_TYPE,''),B.SERVICE_MAN
	END

    --统计每个产品的认购份额，申购份额，赎回份额,总客户数,
	IF @IN_ANALYSIS_FLAG = 1
	BEGIN
		INSERT INTO #TMP_STAT_TABLE (PRODUCT_ID,PRODUCT_CODE,PRODUCT_NAME,PRODUCT_STATUS_NAME,
					START_DATE,END_DATE,TOTAL_CUST_NUM,PRE_MONEY,PRE_NUM,PRE_MONEY_2)
			 SELECT A.PRODUCT_ID,A.PRODUCT_CODE,A.PRODUCT_NAME,A.PRODUCT_STATUS_NAME,A.START_DATE,A.END_DATE,
				   H.TOTAL_CUST_NUM,A.PRE_MONEY,A.PRE_NUM,H.PRE_MONEY_1
				FROM INTRUST..TPRODUCT A
					--产品的认购总客户数
					--募集规模（交银确认只要认购合同录入就算一笔）
					LEFT JOIN (SELECT F.PRODUCT_ID,SUM(RG_MONEY) AS PRE_MONEY_1,COUNT(F.CUST_ID) AS TOTAL_CUST_NUM
								FROM INTRUST..TCONTRACT F GROUP BY F.PRODUCT_ID) H	 ON A.PRODUCT_ID = H.PRODUCT_ID
				WHERE (A.PRODUCT_STATUS = @IN_PRODUCT_STATUS OR ISNULL(@IN_PRODUCT_STATUS,'')='')
				AND   (A.PRODUCT_NAME LIKE '%'+@IN_PRODUCT_NAME+'%' OR ISNULL(@IN_PRODUCT_NAME,'')='')
				AND	  (A.INTRUST_FLAG1 = @IN_INTRUST_FLAG OR ISNULL(@IN_INTRUST_FLAG,0)=0)
				AND	  (A.START_DATE BETWEEN ISNULL(@IN_BEGIN_DATE,0) AND ISNULL(@IN_END_DATE,20991231))

		UPDATE #TMP_STAT_TABLE SET NEW_CUST_NUM = A.NEW_CUST_NUM
			FROM #TMP_STAT_TABLE B,(SELECT PRODUCT_ID,SUM(NEW_CUST_NUM) AS NEW_CUST_NUM
										FROM #TEMP_NEW_CUST_TABLE GROUP BY PRODUCT_ID)A
			WHERE B.PRODUCT_ID = A.PRODUCT_ID

	END

	--按渠道统计每个产品下认购份额，申购份额，赎回份额,总客户数
	ELSE IF @IN_ANALYSIS_FLAG = 2
	BEGIN
		INSERT INTO #TMP_STAT_TABLE (CHANNEL_TYPE,CHANNEL_TYPE_NAME,PRE_MONEY_2,TOTAL_CUST_NUM)
			SELECT ISNULL(A.CHANNEL_TYPE,'') AS CHANNEL_TYPE,ISNULL(B.TYPE_CONTENT,'') AS CHANNEL_TYPE_NAME,
					SUM(A.RG_MONEY) AS PRE_MONEY_2,COUNT(A.CUST_ID) AS TOTAL_CUST_NUM
				FROM INTRUST..TCONTRACT A
					LEFT JOIN INTRUST..TDICTPARAM B ON A.CHANNEL_TYPE = B.TYPE_VALUE AND B.TYPE_ID = 5500
				WHERE (A.PRODUCT_ID = @IN_PRODUCT_ID OR ISNULL(@IN_PRODUCT_ID,0)=0)
				AND (LEFT(A.CHANNEL_TYPE,6) = @IN_CHANNEL_TYPE OR ISNULL(@IN_CHANNEL_TYPE,'')='')
				GROUP BY ISNULL(A.CHANNEL_TYPE,''),ISNULL(B.TYPE_CONTENT,'')
				ORDER BY ISNULL(A.CHANNEL_TYPE,''),ISNULL(B.TYPE_CONTENT,'')

		UPDATE #TMP_STAT_TABLE SET NEW_CUST_NUM = A.NEW_CUST_NUM
			FROM #TMP_STAT_TABLE B,(SELECT CHANNEL_TYPE,SUM(NEW_CUST_NUM) AS NEW_CUST_NUM
										FROM #TEMP_NEW_CUST_TABLE WHERE PRODUCT_ID = @IN_PRODUCT_ID
										GROUP BY CHANNEL_TYPE)A
			WHERE B.CHANNEL_TYPE = A.CHANNEL_TYPE
	END
	
	--按渠道统计每个产品下认购份额，申购份额，赎回份额,总客户数
	ELSE IF @IN_ANALYSIS_FLAG = 4
	BEGIN
		INSERT INTO #TMP_STAT_TABLE (CHANNEL_TYPE,PRE_MONEY_2,TOTAL_CUST_NUM)
			SELECT ISNULL(LEFT(A.CHANNEL_TYPE,6),'') AS CHANNEL_TYPE,
					SUM(A.RG_MONEY) AS PRE_MONEY_2,COUNT(A.CUST_ID) AS TOTAL_CUST_NUM
				FROM INTRUST..TCONTRACT A
				WHERE (A.PRODUCT_ID = @IN_PRODUCT_ID OR ISNULL(@IN_PRODUCT_ID,0)=0)
				AND (LEFT(A.CHANNEL_TYPE,6) = @IN_CHANNEL_TYPE OR ISNULL(@IN_CHANNEL_TYPE,'')='')
				GROUP BY ISNULL(LEFT(A.CHANNEL_TYPE,6),'')

		UPDATE #TMP_STAT_TABLE SET NEW_CUST_NUM = A.NEW_CUST_NUM
			FROM #TMP_STAT_TABLE B,(SELECT LEFT(CHANNEL_TYPE,6) AS CHANNEL_TYPE,SUM(NEW_CUST_NUM) AS NEW_CUST_NUM
										FROM #TEMP_NEW_CUST_TABLE WHERE PRODUCT_ID = @IN_PRODUCT_ID
										GROUP BY LEFT(CHANNEL_TYPE,6))A
			WHERE B.CHANNEL_TYPE = A.CHANNEL_TYPE
		
		UPDATE #TMP_STAT_TABLE SET CHANNEL_TYPE_NAME = CASE WHEN CHANNEL_TYPE = '550001' THEN '直销'
															WHEN CHANNEL_TYPE = '550002' THEN '交银'
															WHEN CHANNEL_TYPE = '550003' THEN '外部渠道'
														ELSE '其它' END		
	END
	
	ELSE
	BEGIN
		INSERT INTO #TMP_STAT_TABLE (SERVICE_MAN,SERVICE_MAN_NAME,PRE_MONEY_2,TOTAL_CUST_NUM)
			SELECT ISNULL(A.SERVICE_MAN,'') AS SERVICE_MAN,ISNULL(B.OP_NAME,'') AS SERVICE_MAN_NAME,
					SUM(A.RG_MONEY) AS PRE_MOENY_2,COUNT(A.CUST_ID) AS TOTAL_CUST_NUM
				FROM INTRUST..TCONTRACT A LEFT JOIN INTRUST..TOPERATOR B ON A.SERVICE_MAN = B.OP_CODE
				WHERE (A.PRODUCT_ID = @IN_PRODUCT_ID OR ISNULL(@IN_PRODUCT_ID,0)=0)
				GROUP BY ISNULL(A.SERVICE_MAN,''),ISNULL(B.OP_NAME,'')
				ORDER BY ISNULL(A.SERVICE_MAN,''),ISNULL(B.OP_NAME,'')

		UPDATE #TMP_STAT_TABLE SET NEW_CUST_NUM = A.NEW_CUST_NUM
			FROM #TMP_STAT_TABLE B,(SELECT SERVICE_MAN,SUM(NEW_CUST_NUM) AS NEW_CUST_NUM
										FROM #TEMP_NEW_CUST_TABLE WHERE PRODUCT_ID = @IN_PRODUCT_ID
										GROUP BY SERVICE_MAN)A
			WHERE B.SERVICE_MAN = A.SERVICE_MAN
	END
	SELECT * FROM #TMP_STAT_TABLE ORDER BY PRODUCT_CODE DESC

    RETURN @@ROWCOUNT
GO
