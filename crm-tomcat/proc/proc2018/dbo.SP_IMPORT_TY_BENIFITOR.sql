USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE SP_IMPORT_TY_BENIFITOR  @OUT_RETURN_MESG NVARCHAR(4000) OUTPUT, --返回的消息
                                         @IN_INPUT_MAN INT = NULL                --操作员
                                         
WITH ENCRYPTION
AS
    SET NOCOUNT ON
    SET XACT_ABORT ON
    DECLARE @V_DT_INTRUST INT,@V_MSG_COUNT INT
    /*[产品名称]          NVARCHAR(200) COLLATE Chinese_PRC_CI_AS NULL ,
		[子产品名称]        NVARCHAR(200) COLLATE Chinese_PRC_CI_AS NULL ,
		[合同编号]          NVARCHAR(200) COLLATE Chinese_PRC_CI_AS NULL ,
		[信托份额]			NVARCHAR(200) COLLATE Chinese_PRC_CI_AS NULL 
		[受益金额]          NVARCHAR(200) COLLATE Chinese_PRC_CI_AS NULL ,
		[受益起始日期]      NVARCHAR(200) COLLATE Chinese_PRC_CI_AS NULL ,
    	[受益终止日期]      NVARCHAR(200) COLLATE Chinese_PRC_CI_AS NULL ,
		[受益人]            NVARCHAR(200) COLLATE Chinese_PRC_CI_AS NULL ,
		[受益人类型]        NVARCHAR(200) COLLATE Chinese_PRC_CI_AS NULL ,
		[受益人联系方式]    NVARCHAR(200) COLLATE Chinese_PRC_CI_AS NULL ,
		[受益人地址]        NVARCHAR(200) COLLATE Chinese_PRC_CI_AS NULL ,
		[受益人邮编]        NVARCHAR(200) COLLATE Chinese_PRC_CI_AS NULL ,
		[受益人证件名称]    NVARCHAR(200) COLLATE Chinese_PRC_CI_AS NULL ,
		[受益人证件编号]    NVARCHAR(200) COLLATE Chinese_PRC_CI_AS NULL ,
		[受益人法人代表]    NVARCHAR(200) COLLATE Chinese_PRC_CI_AS NULL ,
		[受益人固定电话]    NVARCHAR(200) COLLATE Chinese_PRC_CI_AS NULL ,
		[受益人手机]        NVARCHAR(200) COLLATE Chinese_PRC_CI_AS NULL ,
		[受益人电子邮件]    NVARCHAR(200) COLLATE Chinese_PRC_CI_AS NULL ,
		[开户银行名称]      NVARCHAR(200) COLLATE Chinese_PRC_CI_AS NULL ,
		[支行名称]          NVARCHAR(200) COLLATE Chinese_PRC_CI_AS NULL ,
		[银行账号]          NVARCHAR(200) COLLATE Chinese_PRC_CI_AS NULL ,
		[开户银行所在省]    NVARCHAR(200) COLLATE Chinese_PRC_CI_AS NULL ,
		[开户银行所在市]    NVARCHAR(200) COLLATE Chinese_PRC_CI_AS NULL */
    DECLARE @IN_PRODUCT_NAME NVARCHAR(100),   --产品名称
            @IN_SUB_PRODUCT_NAME NVARCHAR(100),   --子产品名称
            @IN_SUB_CONTRACT_BH NVARCHAR(100),    --合同编号CONTRACT_SUB_BH001001
            @IN_BEN_AMOUNT  NVARCHAR(100),    --信托份额
            @IN_BEN_MONEY NVARCHAR(100),    --受益金额
            @IN_BEN_DATE NVARCHAR(10),    --受益起始日期
            @IN_BEN_END_DATE NVARCHAR(10),   --受益终止日期
            @IN_BEN_CUST_NAME NVARCHAR(100),     --受益人
            @IN_BEN_CUST_TYPE INT,              --受益人类型 1-个人 2-机构
            @IN_BEN_TOUCH_TYPE_NAME NVARCHAR(20),     --受益人联系方式
            @IN_BEN_ADDRESS NVARCHAR(60),       --受益人地址
            @IN_BEN_POST_CODE NVARCHAR(6),      --受益人邮编
            @IN_BEN_CARD_TYPE_NAME NVARCHAR(30), --受益人证件名称，需要判断证件类别是否存在
            @IN_BEN_CARD_ID NVARCHAR(40),        --受益人证件编号
            @IN_BEN_LEGAL_MAN NVARCHAR(20),      --受益人法人代表
            @IN_BEN_TEL NVARCHAR(40),            --受益人座机
            @IN_BEN_MOBILE NVARCHAR (20),        --受益人手机
            @IN_BEN_EMAIL NVARCHAR (20),         --受益人电子邮件
            @IN_BANK_NAME NVARCHAR(30),          --受益人开户银行名称，需要判断银行是否存在
            @IN_BANK_SUB_NAME NVARCHAR(30),      --受益人开户银行支行名称
            @IN_BANK_ACCT NVARCHAR(50),          --受益人银行帐号，输入可能为空需要处理
            @IN_BANK_PROVINCE NVARCHAR(50),      --开户银行所在省
            @IN_BANK_CITY NVARCHAR(50)           --开户银行所在市
            
    DECLARE @V_BEN_MONEY DECIMAL(16,3),  --受益金额
            @V_BEN_DATE INT,       --受益起始日期
            @V_BEN_END_DATE INT,   --受益终止日期
            @V_CUST_ID INT,@V_CUST_NO NVARCHAR(8),@V_SERVICE_MAN INT,@V_BEN_CUST_TYPE_NAME NVARCHAR(10)
            
    DECLARE @SBUSI_NAME NVARCHAR(40), @SSUMMARY NVARCHAR(200)
    SELECT @SBUSI_NAME = N'受益人数据文件导入', @SSUMMARY = N'受益人数据文件导入'
    DECLARE @V_THIS_YEAR INT,@V_AGE INT,@V_SEX INT,@V_SEX_NAME NVARCHAR(10)
    DECLARE @V_PRODUCT_ID INT, @V_SUB_FLAG INT, @V_SUB_PRODUCT_ID INT
    DECLARE @V_CUST_LIST_ID INT,@V_CUST_LIST_ID2 INT,@V_CUST_ID2 INT
    DECLARE @V_BEN_TOUCH_TYPE INT,@V_BEN_CARD_TYPE INT,@V_BANK_ID INT,@V_HT_START_DATE INT,@V_HT_END_DATE INT
    DECLARE @V_BOOK_CODE INT,@V_CONTRACT_BH NVARCHAR(20),@V_JK_TYPE NVARCHAR(10),@V_JK_TYPE_NAME NVARCHAR(40)
    DECLARE @V_VALID_PERIOD INT,@V_PROV_FLAG INT,@V_PROV_LEVEL NVARCHAR(10),@V_PROV_LEVEL_NAME NVARCHAR(30)
    DECLARE @V_LIST_ID INT,@V_LIST_ID2 INT,@V_HT_MONEY DECIMAL(16,3),@V_CHECK_FLAG INT
    
    BEGIN TRY
    BEGIN TRANSACTION
    --SAVE TRANSACTION TRANS_IMPORT
	--建立新客户临时表
	SELECT 1 BOOK_CODE,CUST_ID,CUST_NO,CUST_NAME,CUST_TEL,POST_ADDRESS,CARD_TYPE,CARD_TYPE_NAME,CARD_ID,AGE,SEX,SEX_NAME,
			O_TEL,CUST_TYPE,CUST_TYPE_NAME,WT_FLAG,WT_FLAG_NAME,TOUCH_TYPE,TOUCH_TYPE_NAME,CUST_SOURCE,
			CUST_SOURCE_NAME,SUMMARY,INPUT_MAN,CHECK_MAN,POST_CODE,CHECK_FLAG,INPUT_TIME,LEGAL_MAN,
			MOBILE,E_MAIL,LIST_ID,SERVICE_MAN
		INTO #TMP_NEW_CUST
		FROM TCustomers WHERE 1=0
	--建立待加入的受益信息临时表
	SELECT BOOK_CODE,PRODUCT_ID,CONTRACT_BH,CUST_ID,TO_AMOUNT, BEN_AMOUNT,BEN_MONEY,
			BEN_DATE,VALID_PERIOD,BEN_END_DATE,PROV_FLAG,PROV_LEVEL,PROV_LEVEL_NAME,
			JK_TYPE,JK_TYPE_NAME,INPUT_MAN,BANK_ID,BANK_NAME,BANK_ACCT,LIST_ID,
			BANK_SUB_NAME,BEN_STATUS,BEN_STATUS_NAME,CUST_ACCT_NAME,BANK_ACCT_TYPE,BONUS_FLAG,BONUS_RATE,BZJ_FLAG,SUB_PRODUCT_ID,
			MONEY_ORIGIN,MONEY_ORIGIN_NAME,SUB_MONEY_ORIGIN,SUB_MONEY_ORIGIN_NAME
		INTO #TMP_BENIFITOR
		FROM INTRUST..TBENIFITOR WHERE 1=0
	--建立待加入的客户银行账户临时表
	SELECT CUST_ID, BANK_ID, BANK_NAME, BANK_ACCT, CARD_TYPE, CARD_ID, ACCT_NAME,SUB_BANK_NAME,BANK_PROVINCE,BANK_CITY
		INTO #TMP_TCUSTBANKACCT
		FROM INTRUST..TCUSTBANKACCT WHERE 1=0
		
	SET @V_MSG_COUNT=1
	SET @OUT_RETURN_MESG=''
	SELECT @V_THIS_YEAR = YEAR(CURRENT_TIMESTAMP)
    DECLARE CUR_IMPORTDATA CURSOR LOCAL FOR
        SELECT  RTRIM(LTRIM(ISNULL([产品名称],''))), RTRIM(LTRIM(ISNULL([子产品名称],''))),RTRIM(LTRIM(ISNULL([合同编号],''))),RTRIM(LTRIM(ISNULL([信托份额],''))), RTRIM(LTRIM(ISNULL([受益金额],''))), RTRIM(LTRIM(ISNULL([受益起始日期],''))),RTRIM(LTRIM(ISNULL([受益终止日期],''))),
                RTRIM(LTRIM(ISNULL([受益人],''))), RTRIM(LTRIM(ISNULL([受益人类型],''))), RTRIM(LTRIM(ISNULL([受益人联系方式],''))), RTRIM(LTRIM(ISNULL([受益人地址],''))), RTRIM(LTRIM(ISNULL([受益人邮编],''))),
                RTRIM(LTRIM(ISNULL([受益人证件名称],''))), RTRIM(LTRIM(ISNULL([受益人证件编号],''))), RTRIM(LTRIM(ISNULL([受益人法人代表],''))),RTRIM(LTRIM(ISNULL([受益人固定电话],''))),RTRIM(LTRIM(ISNULL([受益人手机],''))),
                RTRIM(LTRIM(ISNULL([受益人电子邮件],''))), RTRIM(LTRIM(ISNULL([开户银行名称],''))), RTRIM(LTRIM(ISNULL([支行名称],''))), RTRIM(LTRIM(ISNULL([银行账号],''))), RTRIM(LTRIM(ISNULL([开户银行所在省],''))),
                RTRIM(LTRIM(ISNULL([开户银行所在市],''))) 
            FROM OLD_BENIFITOR
    OPEN CUR_IMPORTDATA
    FETCH NEXT FROM CUR_IMPORTDATA INTO @IN_PRODUCT_NAME, @IN_SUB_PRODUCT_NAME, @IN_SUB_CONTRACT_BH,@IN_BEN_AMOUNT, @IN_BEN_MONEY,@IN_BEN_DATE,@IN_BEN_END_DATE,
            @IN_BEN_CUST_NAME, @IN_BEN_CUST_TYPE, @IN_BEN_TOUCH_TYPE_NAME, @IN_BEN_ADDRESS, @IN_BEN_POST_CODE,
            @IN_BEN_CARD_TYPE_NAME, @IN_BEN_CARD_ID, @IN_BEN_LEGAL_MAN,    @IN_BEN_TEL,     @IN_BEN_MOBILE,
            @IN_BEN_EMAIL, @IN_BANK_NAME,      @IN_BANK_SUB_NAME,          @IN_BANK_ACCT,   @IN_BANK_PROVINCE,
            @IN_BANK_CITY
    WHILE @@FETCH_STATUS=0
    BEGIN
        SELECT @V_HT_START_DATE=0,@V_HT_END_DATE=0
        --校验导入数据
        IF @IN_PRODUCT_NAME=''
			SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:产品名称必需填写'
		ELSE
		BEGIN
			SELECT @V_PRODUCT_ID=PRODUCT_ID,@V_SUB_FLAG=ISNULL(SUB_FLAG,0) FROM INTRUST..TPRODUCT WHERE PRODUCT_NAME=@IN_PRODUCT_NAME
			IF @@ROWCOUNT=0
				SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:产品名称['+@IN_PRODUCT_NAME+']不存在，请核对'
			ELSE IF @V_SUB_FLAG=1 --有子产品
			BEGIN
				IF @IN_SUB_PRODUCT_NAME=''
					SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:产品['+@IN_PRODUCT_NAME+']有子产品，请填写子产品名称'
				ELSE
				BEGIN
					SELECT @V_SUB_PRODUCT_ID=SUB_PRODUCT_ID FROM INTRUST..TSUBPRODUCT WHERE PRODUCT_ID=@V_PRODUCT_ID AND LIST_NAME=@IN_SUB_PRODUCT_NAME
					IF @@ROWCOUNT=0
						SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:产品名称['+@IN_PRODUCT_NAME+']的子产品['+@IN_SUB_PRODUCT_NAME+']不存在，请核对'
				END
			END
			ELSE --没有子产品
				SET @V_SUB_PRODUCT_ID=0
		END
		IF @IN_SUB_CONTRACT_BH=''
			SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:合同编号必需填写'
		ELSE IF @V_PRODUCT_ID>0
		BEGIN
			SELECT @V_HT_START_DATE=START_DATE,@V_HT_END_DATE=END_DATE,@V_CUST_ID=CUST_ID,@V_BOOK_CODE=BOOK_CODE,@V_CONTRACT_BH=CONTRACT_BH
					,@V_VALID_PERIOD = VALID_PERIOD,@V_PROV_FLAG = PROV_FLAG,@V_PROV_LEVEL=PROV_LEVEL,@V_JK_TYPE=JK_TYPE,@V_JK_TYPE_NAME=JK_TYPE_NAME
					,@V_HT_MONEY=RG_MONEY,@V_CHECK_FLAG=CHECK_FLAG
				FROM INTRUST..TCONTRACT WHERE PRODUCT_ID=@V_PRODUCT_ID AND ISNULL(SUB_PRODUCT_ID,0)=@V_SUB_PRODUCT_ID AND CONTRACT_SUB_BH=@IN_SUB_CONTRACT_BH
			IF @@ROWCOUNT=0
				SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:合同编号['+@IN_SUB_CONTRACT_BH+']不存在，请核对'
			ELSE
			BEGIN
				IF @V_CHECK_FLAG=2
					SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'合同['+@IN_SUB_CONTRACT_BH+']已审核，不能导入'
				SELECT @V_SERVICE_MAN=SERVICE_MAN FROM TCustomers WHERE CUST_ID=@V_CUST_ID
				IF ISNULL(@V_PROV_LEVEL,'')='' SET @V_PROV_LEVEL='120401'
				SELECT @V_PROV_LEVEL_NAME = TYPE_CONTENT FROM INTRUST..TDICTPARAM WHERE TYPE_ID='1204' AND TYPE_VALUE=@V_PROV_LEVEL
			END
		END
		IF @IN_BEN_MONEY=''
			SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:受益金额必需填写'
		ELSE IF ISNUMERIC(@IN_BEN_MONEY)=0 --金额非数字
			SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:受益金额['+@IN_BEN_MONEY+']不合法，请填写数字金额'
        IF @IN_BEN_CUST_NAME=''
			SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:受益人必需填写'
        IF @IN_BEN_CUST_TYPE=''
			SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:受益人类型必需填写'
		ELSE IF @IN_BEN_CUST_TYPE NOT IN ('1','2')
			SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:受益人类型['+@IN_BEN_CUST_TYPE+']不合法，个人请填写数字1，机构请填写数字2'
        IF @IN_BEN_TOUCH_TYPE_NAME=''
			SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:受益人联系方式必需填写'
		ELSE
		BEGIN
			SELECT @V_BEN_TOUCH_TYPE=TYPE_VALUE FROM TDICTPARAM WHERE TYPE_ID='1109' AND TYPE_CONTENT=@IN_BEN_TOUCH_TYPE_NAME
			IF @@ROWCOUNT=0
				SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:受益人联系方式['+@IN_BEN_TOUCH_TYPE_NAME+']不存在，请核对'
		END
		IF @IN_BEN_ADDRESS=''
			SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:受益人地址必需填写'
        IF @IN_BEN_POST_CODE=''
			SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:受益人邮编必需填写'
        IF @IN_BEN_CARD_TYPE_NAME=''
			SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:受益人证件名称必需填写'
		ELSE
		BEGIN
			IF @IN_BEN_CUST_TYPE='1' --个人
			BEGIN
				SET @V_BEN_CUST_TYPE_NAME='个人'
				SELECT @V_BEN_CARD_TYPE=TYPE_VALUE FROM TDICTPARAM WHERE TYPE_ID='1108' AND TYPE_CONTENT=@IN_BEN_CARD_TYPE_NAME
				IF @@ROWCOUNT=0
					SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:个人受益人证件名称['+@IN_BEN_CARD_TYPE_NAME+']不存在，请核对'
			END
			ELSE IF @IN_BEN_CUST_TYPE='2' --机构
			BEGIN
				SET @V_BEN_CUST_TYPE_NAME='机构'
				SELECT @V_BEN_CARD_TYPE=TYPE_VALUE FROM TDICTPARAM WHERE TYPE_ID='2108' AND TYPE_CONTENT=@IN_BEN_CARD_TYPE_NAME
				IF @@ROWCOUNT=0
					SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:机构受益人证件名称['+@IN_BEN_CARD_TYPE_NAME+']不存在，请核对'
			END
		END
		IF EXISTS (SELECT * FROM INTRUST..TBENIFITOR WHERE PRODUCT_ID=@V_PRODUCT_ID AND CONTRACT_BH=@V_CONTRACT_BH AND CUST_ID=
				ISNULL((SELECT CUST_ID FROM TCustomers WHERE CUST_NAME=@IN_BEN_CUST_NAME AND CUST_TYPE=@IN_BEN_CUST_TYPE AND CARD_TYPE=@V_BEN_CARD_TYPE AND CARD_ID=@IN_BEN_CARD_ID),0))
			SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:受益人['+@IN_BEN_CUST_NAME+']在受益表中已存在'
        IF @IN_BEN_CARD_ID=''
			SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:受益人证件编号必需填写'
        IF @IN_BEN_TOUCH_TYPE_NAME IN ('电话','传真','短信','手机')
        BEGIN
			IF @IN_BEN_TEL='' AND @IN_BEN_MOBILE=''
				SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:联系方式选择['+@IN_BEN_TOUCH_TYPE_NAME+']时，电话、手机至少填写一项'
        END
        ELSE IF @IN_BEN_TOUCH_TYPE_NAME='Email' AND @IN_BEN_EMAIL='' --Email的格式暂未验证
			SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:联系方式选择[Email]时，Email必需填写'
        IF @IN_BANK_NAME=''
			SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:开户银行名称必需填写'
		ELSE
		BEGIN
			SELECT @V_BANK_ID=TYPE_VALUE FROM TDICTPARAM WHERE TYPE_ID='1103' AND TYPE_CONTENT=@IN_BANK_NAME
			IF @@ROWCOUNT=0
				SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:开户银行名称['+@IN_BANK_NAME+']不存在，请核对'
		END
		IF @IN_BANK_ACCT=''
			SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:银行账号必需填写'
        IF @IN_BEN_DATE<>''
        BEGIN
			IF LEN(@IN_BEN_DATE)<>8 OR ISNUMERIC(@IN_BEN_DATE)=0
				SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:受益起始日期['+@IN_BEN_DATE+']不正确，请用格式：YYYYMMDD'
			ELSE SET @V_BEN_DATE=@IN_BEN_DATE
		END
		ELSE SET @V_BEN_DATE=@V_HT_START_DATE --受益起止日期没填写，则取合同的起止日期
        IF @IN_BEN_END_DATE<>''
        BEGIN
			IF LEN(@IN_BEN_END_DATE)<>8 OR ISNUMERIC(@IN_BEN_END_DATE)=0
				SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>第'+CAST(@V_MSG_COUNT AS VARCHAR)+'行:受益终止日期['+@IN_BEN_END_DATE+']不正确，请用格式：YYYYMMDD'
			ELSE SET @V_BEN_END_DATE=@IN_BEN_END_DATE
		END
		ELSE SET @V_BEN_END_DATE=@V_HT_END_DATE --受益起止日期没填写，则取合同的起止日期
        SELECT @V_MSG_COUNT = @V_MSG_COUNT + 1 --计数器累计
        IF @OUT_RETURN_MESG='' --没有错误，才处理新增功能
        BEGIN
			SET @V_BEN_MONEY=@IN_BEN_MONEY
			--金额单位为元时,设置开关IMPORT1 = 1;金额单位为万元，设置开关IMPORT1 = 2
			IF EXISTS(SELECT 1 FROM TSYSCONTROL WHERE FLAG_TYPE = 'IMPORT_BENIFITOR_UNIT' AND VALUE = 2)
				SELECT @V_BEN_MONEY = @V_BEN_MONEY * 10000
	        
			--处理受益人性别、年龄
			IF @IN_BEN_CARD_TYPE_NAME = N'身份证' AND @IN_BEN_CARD_ID<>''
			BEGIN
				SELECT @V_AGE=dbo.GETCUSTSEX_AGE(@IN_BEN_CARD_ID,1,@V_THIS_YEAR)
				SELECT @V_SEX=dbo.GETCUSTSEX_AGE(@IN_BEN_CARD_ID,2,@V_THIS_YEAR)
			END
			IF @V_SEX=1
				  SELECT @V_SEX_NAME = N'男'
			ELSE
				  SELECT @V_SEX_NAME = N'女'

			SELECT @V_CUST_ID = CUST_ID, @V_CUST_NO = CUST_NO
				FROM TCustomers
				WHERE CUST_NAME=@IN_BEN_CUST_NAME AND CARD_TYPE=@V_BEN_CARD_TYPE AND CARD_ID=@IN_BEN_CARD_ID AND STATUS <>'112805'
			IF @@ROWCOUNT=0
			BEGIN
				SELECT @V_CUST_ID2 = CUST_ID 
					FROM INTRUST..TCUSTOMERINFO
					WHERE BOOK_CODE=1 AND CUST_NAME=@IN_BEN_CUST_NAME AND CARD_TYPE=@V_BEN_CARD_TYPE AND CARD_ID=@IN_BEN_CARD_ID AND STATUS <>'112805'
				IF @@ROWCOUNT=0 --两个库中都不存在此客户
				BEGIN
					--获得INTRUST数据库CUST_ID
					SELECT @V_CUST_ID2 = ISNULL(MAX(CUST_ID),0) + 1 FROM INTRUST..TCUSTOMERINFO
					SELECT @V_CUST_LIST_ID2 = ISNULL(MAX(LIST_ID),0)+ 1 FROM INTRUST..TCUSTOMERINFO WHERE BOOK_CODE=1 AND CUST_TYPE=@IN_BEN_CUST_TYPE
					--获得本数据库CUST_ID
					SELECT @V_CUST_ID = ISNULL(MAX(CUST_ID),0)+ 1 FROM TCustomers
					SELECT @V_CUST_LIST_ID = ISNULL(MAX(LIST_ID),0)+ 1 FROM TCustomers
					IF @V_CUST_ID2 > @V_CUST_ID SET @V_CUST_ID = @V_CUST_ID2
					IF @V_CUST_LIST_ID2 > @V_CUST_LIST_ID SET @V_CUST_LIST_ID = @V_CUST_LIST_ID2
					--获得本次新建立的客户最大ID
					SELECT @V_CUST_ID2=ISNULL(MAX(CUST_ID),0)+1 FROM #TMP_NEW_CUST
					SELECT @V_CUST_LIST_ID2=ISNULL(MAX(LIST_ID),0)+1 FROM #TMP_NEW_CUST
					IF @V_CUST_ID2 > @V_CUST_ID SET @V_CUST_ID = @V_CUST_ID2
					IF @V_CUST_LIST_ID2 > @V_CUST_LIST_ID SET @V_CUST_LIST_ID = @V_CUST_LIST_ID2
					--生成客户编号
					SELECT @V_CUST_NO = CONVERT(CHAR(8),@IN_BEN_CUST_TYPE * 10000000+@V_CUST_LIST_ID)
					--个人客户编号以0开头，机构以J开头
					IF @IN_BEN_CUST_TYPE = 1 SELECT @V_CUST_NO = '0' + RIGHT(@V_CUST_NO,7)
					ELSE SELECT @V_CUST_NO = 'J' + RIGHT(@V_CUST_NO,7)
					--新客户信息保存入临时表
					INSERT INTO #TMP_NEW_CUST(BOOK_CODE,CUST_ID,CUST_NO,CUST_NAME,CUST_TEL,POST_ADDRESS, CARD_TYPE,CARD_TYPE_NAME,
						CARD_ID,AGE,SEX,SEX_NAME,O_TEL,CUST_TYPE,CUST_TYPE_NAME,WT_FLAG,WT_FLAG_NAME,TOUCH_TYPE,
						TOUCH_TYPE_NAME,CUST_SOURCE,CUST_SOURCE_NAME,SUMMARY,INPUT_MAN,CHECK_MAN,POST_CODE,CHECK_FLAG,
						INPUT_TIME,LEGAL_MAN,MOBILE,E_MAIL,LIST_ID,SERVICE_MAN)
					VALUES(@V_BOOK_CODE,@V_CUST_ID, @V_CUST_NO, @IN_BEN_CUST_NAME,@IN_BEN_TEL,@IN_BEN_ADDRESS,@V_BEN_CARD_TYPE,@IN_BEN_CARD_TYPE_NAME,
						@IN_BEN_CARD_ID,@V_AGE,@V_SEX,@V_SEX_NAME,@IN_BEN_TEL,@IN_BEN_CUST_TYPE,@V_BEN_CUST_TYPE_NAME,2, '否',@V_BEN_TOUCH_TYPE,
						@IN_BEN_TOUCH_TYPE_NAME, '111097', '第三方客户','从受益人为他益情况下导入的受益人',@IN_INPUT_MAN,@IN_INPUT_MAN, @IN_BEN_POST_CODE,2,
						GETDATE(), @IN_BEN_LEGAL_MAN, @IN_BEN_MOBILE,@IN_BEN_EMAIL,@V_CUST_LIST_ID,@V_SERVICE_MAN)
				END
				ELSE --业务系统中存在的客户，而CRM中不存在，则同步到CRM中
				BEGIN
					INSERT INTO TCustomers(ReceiveServices,IS_DEAL,CUST_ID,CUST_NO,CUST_NAME,CUST_TEL,POST_ADDRESS,
						CARD_TYPE,CARD_TYPE_NAME,CARD_ID,AGE,SEX,SEX_NAME,
						O_TEL,CUST_TYPE,CUST_TYPE_NAME,
						WT_FLAG,WT_FLAG_NAME,PASSWORD,
						TOUCH_TYPE,TOUCH_TYPE_NAME,CUST_SOURCE,CUST_SOURCE_NAME,
						SUMMARY,INPUT_MAN,CHECK_MAN,POST_CODE,CHECK_FLAG,INPUT_TIME,
						LEGAL_MAN,LEGAL_ADDRESS,REGISTRATION_ID,MOBILE,FAX,E_MAIL,LIST_ID,SERVICE_MAN)
					SELECT 895,1,CUST_ID,CUST_NO,CUST_NAME,CUST_TEL,POST_ADDRESS,
						CARD_TYPE,CARD_TYPE_NAME,CARD_ID,AGE,SEX,SEX_NAME,
						O_TEL,CUST_TYPE,CUST_TYPE_NAME,
						ISNULL(WT_FLAG,1),ISNULL(WT_FLAG_NAME,N'是'),PASSWORD,
						ISNULL(TOUCH_TYPE,'110901'),ISNULL(TOUCH_TYPE_NAME,N'电话'),ISNULL(CUST_SOURCE,'111004'),ISNULL(CUST_SOURCE_NAME,N'销售人员客户'),
						SUMMARY,INPUT_MAN,CHECK_MAN,POST_CODE,CHECK_FLAG,INPUT_TIME,
						LEGAL_MAN,LEGAL_ADDRESS,REGISTRATION_ID,MOBILE,FAX,E_MAIL,LIST_ID,SERVICE_MAN
					FROM INTRUST..TCUSTOMERINFO WHERE CUST_ID=@V_CUST_ID2
				END
			END
			
			SELECT @V_LIST_ID=ISNULL(MAX(LIST_ID),0)+1 FROM INTRUST..TBENIFITOR WHERE BOOK_CODE=@V_BOOK_CODE AND PRODUCT_ID=@V_PRODUCT_ID AND CONTRACT_BH=@V_CONTRACT_BH
			SELECT @V_LIST_ID2=ISNULL(MAX(LIST_ID),0)+1 FROM #TMP_BENIFITOR WHERE PRODUCT_ID=@V_PRODUCT_ID AND CONTRACT_BH=@V_CONTRACT_BH
			IF @V_LIST_ID2>@V_LIST_ID SET @V_LIST_ID=@V_LIST_ID2
			---银行账户类型(9920)暂未填写;BONUS_FLAG(1兑付2转份额,用默认1);BZJ_FLAG(是否保证金:1是2否,用默认2)-------------------------------------
			INSERT INTO #TMP_BENIFITOR(BOOK_CODE,PRODUCT_ID,CONTRACT_BH,CUST_ID,TO_AMOUNT, BEN_AMOUNT,BEN_MONEY,
					BEN_DATE,VALID_PERIOD,BEN_END_DATE,PROV_FLAG,PROV_LEVEL,PROV_LEVEL_NAME,
					JK_TYPE,JK_TYPE_NAME,INPUT_MAN,BANK_ID,BANK_NAME,BANK_ACCT,LIST_ID,
					BANK_SUB_NAME,BEN_STATUS,BEN_STATUS_NAME,CUST_ACCT_NAME,BANK_ACCT_TYPE,BONUS_FLAG,BONUS_RATE,BZJ_FLAG,SUB_PRODUCT_ID,
					MONEY_ORIGIN,MONEY_ORIGIN_NAME,SUB_MONEY_ORIGIN,SUB_MONEY_ORIGIN_NAME)
				VALUES(@V_BOOK_CODE,@V_PRODUCT_ID,@V_CONTRACT_BH,@V_CUST_ID,@V_BEN_MONEY,@IN_BEN_AMOUNT, @IN_BEN_MONEY,
					@V_BEN_DATE, @V_VALID_PERIOD, @V_BEN_END_DATE,@V_PROV_FLAG,@V_PROV_LEVEL,@V_PROV_LEVEL_NAME,
					@V_JK_TYPE,@V_JK_TYPE_NAME,@IN_INPUT_MAN,@V_BANK_ID,@IN_BANK_NAME,@IN_BANK_ACCT,@V_LIST_ID,
					@IN_BANK_SUB_NAME, '121101', N'正常', @IN_BEN_CUST_NAME, NULL,1,0,2,@V_SUB_PRODUCT_ID,
					NULL,NULL,NULL,NULL)
			----添加一条客户银行帐号信息--
			IF NOT EXISTS( SELECT * FROM INTRUST..TCUSTBANKACCT WHERE CUST_ID=@V_CUST_ID AND BANK_ID=@V_BANK_ID AND BANK_ACCT=@IN_BANK_ACCT )
				AND ISNULL(@V_BANK_ID,0)<>0 AND ISNULL(@IN_BANK_ACCT,'')<>''
			BEGIN
				INSERT INTO #TMP_TCUSTBANKACCT(CUST_ID, BANK_ID, BANK_NAME, BANK_ACCT, CARD_TYPE, CARD_ID, ACCT_NAME,SUB_BANK_NAME,BANK_PROVINCE,BANK_CITY)
					VALUES(@V_CUST_ID,@V_BANK_ID,@IN_BANK_NAME,@IN_BANK_ACCT,@V_BEN_CARD_TYPE,@IN_BEN_CARD_ID,@IN_BEN_CUST_NAME,@IN_BANK_SUB_NAME,@IN_BANK_PROVINCE,@IN_BANK_CITY)
			END
        END
        
        FETCH NEXT FROM CUR_IMPORTDATA INTO @IN_PRODUCT_NAME, @IN_SUB_PRODUCT_NAME, @IN_SUB_CONTRACT_BH,@IN_BEN_AMOUNT, @IN_BEN_MONEY,@IN_BEN_DATE,@IN_BEN_END_DATE,
            @IN_BEN_CUST_NAME, @IN_BEN_CUST_TYPE, @IN_BEN_TOUCH_TYPE_NAME, @IN_BEN_ADDRESS, @IN_BEN_POST_CODE,
            @IN_BEN_CARD_TYPE_NAME, @IN_BEN_CARD_ID, @IN_BEN_LEGAL_MAN,    @IN_BEN_TEL,     @IN_BEN_MOBILE,
            @IN_BEN_EMAIL, @IN_BANK_NAME,      @IN_BANK_SUB_NAME,          @IN_BANK_ACCT,   @IN_BANK_PROVINCE,
            @IN_BANK_CITY
    END
    CLOSE CUR_IMPORTDATA
    DEALLOCATE CUR_IMPORTDATA
	--检查是否有重复的受益人
	--北京信托：允许同一产品名下重名客户
	/*SELECT [产品名称],[子产品名称],[合同编号],[受益人],COUNT(0) CUST_NUM INTO #TMP_CHK_CUST FROM OLD_BENIFITOR GROUP BY [产品名称],[子产品名称],[合同编号],[受益人] HAVING COUNT(0)>1
	IF @@ROWCOUNT>0
	BEGIN
		DECLARE @V_TMP_CUST_NAME NVARCHAR(400)
		SET @V_TMP_CUST_NAME=''
		SELECT @V_TMP_CUST_NAME=@V_TMP_CUST_NAME+ISNULL([受益人],'')+',' FROM #TMP_CHK_CUST
		SET @V_TMP_CUST_NAME=SUBSTRING(@V_TMP_CUST_NAME,1,LEN(@V_TMP_CUST_NAME)-1)
		SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>以下受益人重复：'+@V_TMP_CUST_NAME
	END*/
	IF @OUT_RETURN_MESG='' --没有错误
	BEGIN --核对导入的金额合计，是否与认购相同
		DECLARE @V_BEN_MONEY_TOTAL DECIMAL(16,3),@V_MSG2 NVARCHAR(800)--@V_HT_MONEY
		SET @V_MSG2=''
		SELECT PRODUCT_ID,SUB_PRODUCT_ID,CONTRACT_BH,SUM(BEN_MONEY) BEN_MONEY_T INTO #TMP_BEN_T FROM #TMP_BENIFITOR GROUP BY PRODUCT_ID,SUB_PRODUCT_ID,CONTRACT_BH
		--加入已录入的受益人
		UPDATE #TMP_BEN_T SET BEN_MONEY_T=BEN_MONEY_T+ISNULL((SELECT SUM(BEN_MONEY) FROM INTRUST..TBENIFITOR WHERE PRODUCT_ID=#TMP_BEN_T.PRODUCT_ID AND SUB_PRODUCT_ID=#TMP_BEN_T.SUB_PRODUCT_ID AND CONTRACT_BH=#TMP_BEN_T.CONTRACT_BH),0)
		SELECT @V_MSG2=@V_MSG2+'合同号['+B.CONTRACT_SUB_BH+']合同金额['+CAST(B.RG_MONEY AS VARCHAR)+']<=>受益合计['+CAST(A.BEN_MONEY_T AS VARCHAR)+'];' 
			FROM #TMP_BEN_T A LEFT JOIN INTRUST..TCONTRACT B ON A.PRODUCT_ID=B.PRODUCT_ID AND A.SUB_PRODUCT_ID=B.SUB_PRODUCT_ID AND A.CONTRACT_BH=B.CONTRACT_BH
			WHERE A.BEN_MONEY_T>B.RG_MONEY
		IF @V_MSG2<>''
			SET @OUT_RETURN_MESG=@OUT_RETURN_MESG+'<BR>'+@V_MSG2+'金额不能大于合同认购金额，请核对'
		ELSE --金额也没错，则正式导入到相应的表中
		BEGIN
			DECLARE @V_INTRUST_INPUT_MAN INT
			SELECT @V_INTRUST_INPUT_MAN=@IN_INPUT_MAN--INTRUST_Operator FROM TOPERATOR_MAP WHERE CRM_Operator=@IN_INPUT_MAN AND INTRUST_BOOKCODE=1
			--客户表
			INSERT INTO INTRUST..TCUSTOMERINFO(BOOK_CODE,CUST_ID,CUST_NO,CUST_NAME,CUST_TEL,POST_ADDRESS, CARD_TYPE,CARD_TYPE_NAME,
						CARD_ID,AGE,SEX,SEX_NAME,O_TEL,CUST_TYPE,CUST_TYPE_NAME,WT_FLAG,WT_FLAG_NAME,TOUCH_TYPE,
						TOUCH_TYPE_NAME,CUST_SOURCE,CUST_SOURCE_NAME,SUMMARY,INPUT_MAN,CHECK_MAN,POST_CODE,CHECK_FLAG,
						INPUT_TIME,LEGAL_MAN,MOBILE,E_MAIL,LIST_ID,SERVICE_MAN)
				SELECT BOOK_CODE,CUST_ID,CUST_NO,CUST_NAME,CUST_TEL,POST_ADDRESS, CARD_TYPE,CARD_TYPE_NAME,
						CARD_ID,AGE,SEX,SEX_NAME,O_TEL,CUST_TYPE,CUST_TYPE_NAME,WT_FLAG,WT_FLAG_NAME,TOUCH_TYPE,
						TOUCH_TYPE_NAME,CUST_SOURCE,CUST_SOURCE_NAME,SUMMARY,@V_INTRUST_INPUT_MAN,CHECK_MAN,POST_CODE,CHECK_FLAG,
						INPUT_TIME,LEGAL_MAN,MOBILE,E_MAIL,LIST_ID,SERVICE_MAN
					FROM #TMP_NEW_CUST
			INSERT INTO TCustomers(CUST_ID,CUST_NO,CUST_NAME,CUST_TEL,POST_ADDRESS, CARD_TYPE,CARD_TYPE_NAME,
						CARD_ID,AGE,SEX,SEX_NAME,O_TEL,CUST_TYPE,CUST_TYPE_NAME,WT_FLAG,WT_FLAG_NAME,TOUCH_TYPE,
						TOUCH_TYPE_NAME,CUST_SOURCE,CUST_SOURCE_NAME,SUMMARY,INPUT_MAN,CHECK_MAN,POST_CODE,CHECK_FLAG,
						INPUT_TIME,LEGAL_MAN,MOBILE,E_MAIL,LIST_ID,SERVICE_MAN)
				SELECT CUST_ID,CUST_NO,CUST_NAME,CUST_TEL,POST_ADDRESS, CARD_TYPE,CARD_TYPE_NAME,
						CARD_ID,AGE,SEX,SEX_NAME,O_TEL,CUST_TYPE,CUST_TYPE_NAME,WT_FLAG,WT_FLAG_NAME,TOUCH_TYPE,
						TOUCH_TYPE_NAME,CUST_SOURCE,CUST_SOURCE_NAME,SUMMARY,INPUT_MAN,CHECK_MAN,POST_CODE,CHECK_FLAG,
						INPUT_TIME,LEGAL_MAN,MOBILE,E_MAIL,LIST_ID,SERVICE_MAN
					FROM #TMP_NEW_CUST
			--受益人表
			INSERT INTO INTRUST..TBENIFITOR(BOOK_CODE,PRODUCT_ID,CONTRACT_BH,CUST_ID,TO_AMOUNT, BEN_AMOUNT,BEN_MONEY,
					BEN_DATE,VALID_PERIOD,BEN_END_DATE,PROV_FLAG,PROV_LEVEL,PROV_LEVEL_NAME,
					JK_TYPE,JK_TYPE_NAME,INPUT_MAN,BANK_ID,BANK_NAME,BANK_ACCT,LIST_ID,
					BANK_SUB_NAME,BEN_STATUS,BEN_STATUS_NAME,CUST_ACCT_NAME,BANK_ACCT_TYPE,BONUS_FLAG,BONUS_RATE,BZJ_FLAG,SUB_PRODUCT_ID,
					MONEY_ORIGIN,MONEY_ORIGIN_NAME,SUB_MONEY_ORIGIN,SUB_MONEY_ORIGIN_NAME)
				SELECT BOOK_CODE,PRODUCT_ID,CONTRACT_BH,CUST_ID,TO_AMOUNT, BEN_AMOUNT,BEN_MONEY,
						BEN_DATE,VALID_PERIOD,BEN_END_DATE,PROV_FLAG,PROV_LEVEL,PROV_LEVEL_NAME,
						JK_TYPE,JK_TYPE_NAME,@V_INTRUST_INPUT_MAN,BANK_ID,BANK_NAME,BANK_ACCT,LIST_ID,
						BANK_SUB_NAME,BEN_STATUS,BEN_STATUS_NAME,CUST_ACCT_NAME,BANK_ACCT_TYPE,BONUS_FLAG,BONUS_RATE,BZJ_FLAG,SUB_PRODUCT_ID,
						MONEY_ORIGIN,MONEY_ORIGIN_NAME,SUB_MONEY_ORIGIN,SUB_MONEY_ORIGIN_NAME
					FROM #TMP_BENIFITOR
			--银行账户信息表
			INSERT INTO INTRUST..TCUSTBANKACCT(CUST_ID, BANK_ID, BANK_NAME, BANK_ACCT, CARD_TYPE, CARD_ID, ACCT_NAME,SUB_BANK_NAME,BANK_PROVINCE,BANK_CITY)
				SELECT CUST_ID, BANK_ID, BANK_NAME, BANK_ACCT, CARD_TYPE, CARD_ID, ACCT_NAME,SUB_BANK_NAME,BANK_PROVINCE,BANK_CITY FROM #TMP_TCUSTBANKACCT
			SET @V_MSG_COUNT=@V_MSG_COUNT-1
			SELECT @OUT_RETURN_MESG= N''
		END
	END
    
    SELECT @SSUMMARY = N'导入受益人信息：产品名称：' + RTRIM(@IN_PRODUCT_NAME)
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES('1',@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    
    COMMIT TRANSACTION
    END TRY

    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION-- TRANS_IMPORT
        IF EXISTS(SELECT * FROM MASTER.dbo.syscursors where cursor_name='CUR_IMPORTDATA')
        BEGIN
			CLOSE CUR_IMPORTDATA
			DEALLOCATE CUR_IMPORTDATA
        END
        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)

        SELECT @V_ERROR_STR = N'Message:%s,Procedure:%s,Line:%d',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()

        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_PROCEDURE,@V_ERROR_LINE)
        
        RETURN -100
    END CATCH
    RETURN 100
GO
