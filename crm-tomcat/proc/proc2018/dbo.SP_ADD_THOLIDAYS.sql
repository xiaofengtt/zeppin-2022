USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_ADD_THOLIDAYS @IN_HOLIDAY_NAME    NVARCHAR(60),   --节假日名称
                                  @IN_HOLIDAY_DAY     INT,            --节假日mmdd
                                  @IN_CAL_FLAG        TINYINT,        --1公历 2农历
                                  @IN_CREATE_TASK     INT,            --是否创建服务任务 1是 2否
                                  @IN_SMS_GREETING    NVARCHAR(500),  --节日短信祝词内容
                                  @IN_EMAIL_GREETING  NVARCHAR(4000), --节日邮件祝词内容
                                  @IN_INPUT_MAN       INT,
                                  @OUT_HOLIDAY_ID     INT OUTPUT     --节假日ID
WITH ENCRYPTION
AS
    SET NOCOUNT ON
    DECLARE @V_RET_CODE INT, @IBUSI_FLAG INT, @SBUSI_NAME NVARCHAR(40), @SSUMMARY NVARCHAR(200)
    SELECT @V_RET_CODE = -10130000, @IBUSI_FLAG = 10130
    SELECT @SBUSI_NAME = N'添加节假日', @SSUMMARY = N'添加节假日'

    DECLARE @V_HOLIDAY_ID INT
    SELECT @V_HOLIDAY_ID = MAX(HOLIDAY_ID) FROM THOLIDAYS
    SET @V_HOLIDAY_ID = ISNULL(@V_HOLIDAY_ID,0)+1
    IF EXISTS(SELECT 1 FROM THOLIDAYS WHERE HOLIDAY_NAME = @IN_HOLIDAY_NAME)
        RETURN @V_RET_CODE - 1 --节假日名称已经存在
    IF ISDATE(20000000+@IN_HOLIDAY_DAY) = 0
        RETURN @V_RET_CODE - 2 --输入节日日期无效，需要mmdd形式
    IF @IN_CAL_FLAG NOT IN (1,2)
        RETURN @V_RET_CODE - 3 --输入参数无效
    IF @IN_CREATE_TASK NOT IN (1,2)
        RETURN @V_RET_CODE - 3 --输入参数无效

    BEGIN TRANSACTION

    INSERT INTO THOLIDAYS(HOLIDAY_ID,HOLIDAY_NAME,HOLIDAY_DAY,CAL_FLAG,CREATE_TASK,SMS_GREETING,EMAIL_GREETING)
        VALUES(@V_HOLIDAY_ID,@IN_HOLIDAY_NAME,@IN_HOLIDAY_DAY,@IN_CAL_FLAG,@IN_CREATE_TASK,@IN_SMS_GREETING,@IN_EMAIL_GREETING)
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END
    SET @OUT_HOLIDAY_ID = @V_HOLIDAY_ID

    SELECT @SSUMMARY = N'添加节假日：' + @IN_HOLIDAY_NAME + CASE @IN_CAL_FLAG WHEN 1 THEN N'；公历：' ELSE N'；农历：' END + dbo.fn_getmonthday(@IN_HOLIDAY_DAY)
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END

    COMMIT TRANSACTION
    RETURN 100
GO
