USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_QUERY_MENURIGHT @IN_OP_CODE     INT,
                                    @IN_MENU_ID     NVARCHAR(10) = NULL,
                                    @IN_PARENT_ID   NVARCHAR(10) = NULL,
                                    @IN_LANGUAGE_TYPE INT = 1  --1.zh_CN;2.en_US;3.zh_CN(CAS用)
WITH ENCRYPTION
AS
    SET NOCOUNT ON

    DECLARE @V_USER_ID INT
    SELECT @V_USER_ID = USER_ID FROM TSYSTEMINFO
    DECLARE @V_TMP_MENU_RIGHT TABLE
    (
        OP_CODE     INT,
        MENU_ID     NVARCHAR(10) NOT NULL ,
        MENU_NAME   NVARCHAR(60) NOT NULL ,
        MENU_INFO   NVARCHAR(200),
        PARENT_ID   NVARCHAR(10),
        BOTTOM_FLAG INTEGER NOT NULL,
        URL         NVARCHAR(300),
        LINKTO      NVARCHAR(30),
        TREE_ORDER  NVARCHAR(10)  --排序依据字段
    )
    DECLARE @V_TEMPRIGHT TABLE(MENU_ID NVARCHAR(10))
    INSERT INTO @V_TEMPRIGHT SELECT MENU_ID FROM TROLERIGHT
    WHERE ROLE_ID IN(SELECT ROLE_ID FROM TOPROLE WHERE OP_CODE = @IN_OP_CODE)
    GROUP BY MENU_ID

    --插入操作员有权限的菜单，排除非可见功能类菜单
    IF EXISTS(SELECT * FROM TSYSTEMINFO WHERE INIT_FLAG = 1)
        INSERT INTO @V_TMP_MENU_RIGHT
            SELECT @IN_OP_CODE, B.MENU_ID, B.MENU_NAME, B.MENU_INFO, B.PARENT_ID, B.BOTTOM_FLAG, B.URL, B.LINKTO, B.TREE_ORDER
                FROM @V_TEMPRIGHT A, TMENUINFO B
                WHERE A.MENU_ID = B.MENU_ID AND (B.USER_ID = @V_USER_ID OR B.USER_ID = 0)
                    AND B.MENU_ID NOT LIKE '999%'
    ELSE
        INSERT INTO @V_TMP_MENU_RIGHT
            SELECT @IN_OP_CODE, B.MENU_ID, B.MENU_NAME, B.MENU_INFO, B.PARENT_ID, B.BOTTOM_FLAG, B.URL, B.LINKTO, B.TREE_ORDER
                FROM @V_TEMPRIGHT A, TMENUINFO B
                WHERE A.MENU_ID = B.MENU_ID AND (B.USER_ID = @V_USER_ID OR B.USER_ID = 0)
                    AND (B.MENU_ID NOT LIKE '999%' AND B.MENU_ID <> '10399')
    --插入菜单的父菜单
    INSERT INTO @V_TMP_MENU_RIGHT
        SELECT @IN_OP_CODE, B.MENU_ID, B.MENU_NAME, B.MENU_INFO, B.PARENT_ID, B.BOTTOM_FLAG, B.URL, B.LINKTO, B.TREE_ORDER
            FROM TMENUINFO B
            WHERE B.MENU_ID IN (SELECT DISTINCT PARENT_ID FROM @V_TMP_MENU_RIGHT)
                AND (B.USER_ID = @V_USER_ID OR B.USER_ID = 0)

    INSERT INTO @V_TMP_MENU_RIGHT
        SELECT @IN_OP_CODE, B.MENU_ID, B.MENU_NAME, B.MENU_INFO, B.PARENT_ID, B.BOTTOM_FLAG, B.URL, B.LINKTO, B.TREE_ORDER
            FROM TMENUINFO B
            WHERE B.MENU_ID IN (SELECT DISTINCT PARENT_ID FROM @V_TMP_MENU_RIGHT WHERE BOTTOM_FLAG <> 1)
                  AND B.MENU_ID NOT IN (SELECT MENU_ID FROM @V_TMP_MENU_RIGHT)
                  AND (B.USER_ID = @V_USER_ID OR B.USER_ID = 0)
   
    UPDATE @V_TMP_MENU_RIGHT SET URL = ISNULL(B.TYPE_CONTENT,'')+URL
        FROM @V_TMP_MENU_RIGHT A, TDICTPARAM B
        WHERE A.LINKTO = B.TYPE_VALUE AND B.TYPE_VALUE IN ('800001','800002')
    --20111219 dongyg 知识库菜单即文章分类，全员有权限
    INSERT INTO @V_TMP_MENU_RIGHT
        SELECT @IN_OP_CODE, B.MENU_ID, B.MENU_NAME, B.MENU_INFO, B.PARENT_ID, B.BOTTOM_FLAG, B.URL, B.LINKTO, B.TREE_ORDER
            FROM TMENUINFO B
            WHERE B.MENU_ID LIKE 'W%' AND NOT EXISTS(SELECT 1 FROM @V_TMP_MENU_RIGHT Z WHERE B.MENU_ID = Z.MENU_ID)
                AND (B.USER_ID = @V_USER_ID OR B.USER_ID = 0)
                AND @V_USER_ID<>7 AND USER_ID <> 2 --国联的，没给权限不能看这个菜单

    --  如果语言环境为英语
    IF @IN_LANGUAGE_TYPE = 2
    BEGIN
        UPDATE @V_TMP_MENU_RIGHT
        SET MENU_NAME = ISNULL(B.MENU_NAME_EN,B.MENU_NAME),
            MENU_INFO = ISNULL(B.MENU_INFO_EN,B.MENU_INFO)
        FROM @V_TMP_MENU_RIGHT A,TMENUINFO B
        WHERE A.MENU_ID = B.MENU_ID
    END

    IF @IN_LANGUAGE_TYPE=3
    BEGIN
		SELECT MENU_ID ,MENU_NAME ,'/addlog.jsp?menu_id='+MENU_ID SRC FROM @V_TMP_MENU_RIGHT
			WHERE (MENU_ID LIKE @IN_MENU_ID+'%' OR ISNULL(@IN_MENU_ID,'')='')
				AND (PARENT_ID = @IN_PARENT_ID OR ISNULL(@IN_PARENT_ID,'')='')
			ORDER BY MENU_ID
    END
    ELSE IF @IN_PARENT_ID IS NULL OR @IN_PARENT_ID = N''
        SELECT * FROM @V_TMP_MENU_RIGHT
            WHERE (MENU_ID LIKE @IN_MENU_ID+'%' OR @IN_MENU_ID IS NULL OR @IN_MENU_ID = N'')
                AND (PARENT_ID IS NULL OR PARENT_ID = N'')
            ORDER BY TREE_ORDER,MENU_ID
    ELSE
        SELECT * FROM @V_TMP_MENU_RIGHT
            WHERE (MENU_ID LIKE @IN_MENU_ID+'%' OR @IN_MENU_ID IS NULL OR @IN_MENU_ID = N'')
                AND (PARENT_ID LIKE @IN_PARENT_ID+'%')
            ORDER BY TREE_ORDER,MENU_ID

    RETURN @@ROWCOUNT
GO
