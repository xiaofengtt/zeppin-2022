USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_ADD_TCUSTADDRESS @IN_CUST_ID               INTEGER,        --客户ID
                                   @IN_PROVINCE                NVARCHAR(12),   --省份代码
                                   @IN_CITY                    NVARCHAR(12),   --市代码
                                   @IN_DISTRICT                NVARCHAR(12),   --县/区代码
                                   @IN_TOWN_DETAIL             NVARCHAR(60),   --详细街道地址
                                   @IN_POST_CODE               NVARCHAR(12),   --邮政编码
                                   @IN_CONTACT_MAN             NVARCHAR(40),   --联系人
                                   @IN_TEL                     NVARCHAR(40),   --联系电话
                                   @IN_FAX                     NVARCHAR(20),   --传真
                                   @IN_TOUCH_TYPE              NVARCHAR(10),   --联系方式(1109)
                                   @IN_O_TEL                   NVARCHAR(20),   --办公电话
                                   @IN_H_TEL                   NVARCHAR(20),   --家庭电话
                                   @IN_E_MAIL                  NVARCHAR(30),   --电子邮件
                                   @IN_INPUT_MAN               INTEGER         --操作员
WITH ENCRYPTION 
AS
    DECLARE @V_CUST_NAME NVARCHAR(80),@V_LIST_ID INT,@V_DEFAULT_FLAG INT
    DECLARE @V_PROVINCE_NAME NVARCHAR(20),@V_CITY_NAME NVARCHAR(20),@V_DISTRICT_NAME NVARCHAR(20),@V_POST_ADDRESS NVARCHAR(100)
    DECLARE @IBUSI_FLAG INT,@V_MODI_CHECK_FLAG INT 
    DECLARE @OLD_TOUCH_TYPE NVARCHAR(10),@OLD_O_TEL NVARCHAR(20),@OLD_H_TEL NVARCHAR(20),@OLD_E_MAIL NVARCHAR(30),@OLD_TOUCH_TYPE_NAME NVARCHAR(30)
    DECLARE @OLD_POST_ADDRESS NVARCHAR(100),@OLD_POST_ADDRESS2 NVARCHAR(100)
    DECLARE @SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)
    SET @IBUSI_FLAG = 20101
    SET @SBUSI_NAME = N'新增客户联系信息'
    
    BEGIN TRY
        BEGIN TRANSACTION
        
       --客户信息变更审核 1是 2否
		IF EXISTS (SELECT 1 FROM TSYSCONTROL WHERE FLAG_TYPE='CUSTINFO_CHANGE' AND VALUE=1)
			SET @V_MODI_CHECK_FLAG =1
		ELSE SET @V_MODI_CHECK_FLAG=0
        --错误验证
        SELECT @V_CUST_NAME=CUST_NAME,@OLD_TOUCH_TYPE=TOUCH_TYPE,@OLD_O_TEL=O_TEL,@OLD_H_TEL=H_TEL,@OLD_E_MAIL=E_MAIL,@OLD_TOUCH_TYPE_NAME=TOUCH_TYPE_NAME
				,@OLD_POST_ADDRESS=POST_ADDRESS,@OLD_POST_ADDRESS2=POST_ADDRESS2
			FROM TCustomers WHERE CUST_ID = @IN_CUST_ID
        IF @@ROWCOUNT=0
        BEGIN
            RAISERROR('客户不存在',16,1)
        END
        SELECT @V_PROVINCE_NAME=TYPE_CONTENT FROM TDICTPARAM WHERE TYPE_ID='9999' AND TYPE_VALUE=@IN_PROVINCE
		--SELECT @V_CITY_NAME =TYPE_CONTENT FROM TDICTPARAM WHERE TYPE_ID='9999' AND TYPE_VALUE=@IN_CITY
		SELECT @V_DISTRICT_NAME=TYPE_CONTENT FROM TDICTPARAM WHERE TYPE_ID='9999' AND TYPE_VALUE=@IN_DISTRICT
		SET @V_POST_ADDRESS=ISNULL(@V_PROVINCE_NAME,'')+ISNULL(@V_DISTRICT_NAME,'')+@IN_TOWN_DETAIL --合并联系地址
		
		IF LTRIM(RTRIM(ISNULL(@OLD_O_TEL,''))) <> LTRIM(RTRIM(ISNULL(@IN_O_TEL,'')))
		BEGIN
			INSERT INTO TCustomerChanges(CUST_ID,FIELD_NAME,FIELD_CN_NAME,OLD_FIELD_INFO,NEW_FIELD_INFO,INPUT_MAN,CHECK_FLAG)
				VALUES(@IN_CUST_ID,N'O_TEL',N'单位电话',@OLD_O_TEL,@IN_O_TEL,@IN_INPUT_MAN,@V_MODI_CHECK_FLAG)
		END
        IF LTRIM(RTRIM(ISNULL(@OLD_H_TEL,''))) <> LTRIM(RTRIM(ISNULL(@IN_H_TEL,'')))
		BEGIN
			INSERT INTO TCustomerChanges(CUST_ID,FIELD_NAME,FIELD_CN_NAME,OLD_FIELD_INFO,NEW_FIELD_INFO,INPUT_MAN,CHECK_FLAG)
				VALUES(@IN_CUST_ID,N'H_TEL',N'家庭电话',@OLD_H_TEL,@IN_H_TEL,@IN_INPUT_MAN,@V_MODI_CHECK_FLAG)
		END
		IF LTRIM(RTRIM(ISNULL(@OLD_E_MAIL,''))) <> LTRIM(RTRIM(ISNULL(@IN_E_MAIL,'')))
		BEGIN
			INSERT INTO TCustomerChanges(CUST_ID,FIELD_NAME,FIELD_CN_NAME,OLD_FIELD_INFO,NEW_FIELD_INFO,INPUT_MAN,CHECK_FLAG)
				VALUES(@IN_CUST_ID,N'E_MAIL',N'EMAIL',@OLD_E_MAIL,@IN_E_MAIL,@IN_INPUT_MAN,@V_MODI_CHECK_FLAG)
		END
		DECLARE @V_TOUCH_TYPE_NAME NVARCHAR(40)
		SELECT @V_TOUCH_TYPE_NAME = TYPE_CONTENT FROM TDICTPARAM  WHERE TYPE_VALUE = @IN_TOUCH_TYPE
		IF LTRIM(RTRIM(ISNULL(@OLD_TOUCH_TYPE,''))) <> LTRIM(RTRIM(ISNULL(@IN_TOUCH_TYPE,'')))
		BEGIN
			INSERT INTO TCustomerChanges(CUST_ID,FIELD_NAME,FIELD_CN_NAME,OLD_FIELD_INFO,NEW_FIELD_INFO,INPUT_MAN,CHECK_FLAG)
				VALUES(@IN_CUST_ID,N'TOUCH_TYPE_NAME',N'联系方式',@OLD_TOUCH_TYPE_NAME,@V_TOUCH_TYPE_NAME,@IN_INPUT_MAN,@V_MODI_CHECK_FLAG)
		END
		IF @V_DEFAULT_FLAG=1 AND LTRIM(RTRIM(ISNULL(@OLD_POST_ADDRESS,''))) <> LTRIM(RTRIM(ISNULL(@V_POST_ADDRESS,'')))
		BEGIN
			INSERT INTO TCustomerChanges(CUST_ID,FIELD_NAME,FIELD_CN_NAME,OLD_FIELD_INFO,NEW_FIELD_INFO,INPUT_MAN,CHECK_FLAG)
				VALUES(@IN_CUST_ID,N'POST_ADDRESS',N'邮寄地址',@OLD_POST_ADDRESS,@V_POST_ADDRESS,@IN_INPUT_MAN,@V_MODI_CHECK_FLAG)
		END
		IF @V_DEFAULT_FLAG=0 AND LTRIM(RTRIM(ISNULL(@OLD_POST_ADDRESS2,''))) <> LTRIM(RTRIM(ISNULL(@V_POST_ADDRESS,'')))
		BEGIN
			INSERT INTO TCustomerChanges(CUST_ID,FIELD_NAME,FIELD_CN_NAME,OLD_FIELD_INFO,NEW_FIELD_INFO,INPUT_MAN,CHECK_FLAG)
				VALUES(@IN_CUST_ID,N'POST_ADDRESS2',N'邮寄地址2',@OLD_POST_ADDRESS2,@V_POST_ADDRESS,@IN_INPUT_MAN,@V_MODI_CHECK_FLAG)
		END
		
        SELECT @V_LIST_ID=ISNULL(MAX(LIST_ID),0)+1 FROM TCUSTADDRESS WHERE CUST_ID=CUST_ID
        IF @V_LIST_ID=1 SET @V_DEFAULT_FLAG=1
        ELSE SET @V_DEFAULT_FLAG=0
        INSERT INTO TCUSTADDRESS(CUST_ID,LIST_ID,DEFAULT_FLAG,PROVINCE,CITY,DISTRICT,TOWN_DETAIL,POST_CODE,CONTACT_MAN,TEL,FAX)
            VALUES(@IN_CUST_ID,@V_LIST_ID,@V_DEFAULT_FLAG,@IN_PROVINCE,@IN_CITY,@IN_DISTRICT,@IN_TOWN_DETAIL,@IN_POST_CODE,@IN_CONTACT_MAN,@IN_TEL,@IN_FAX)
        
        --日志记录
        SET @SSUMMARY = @SBUSI_NAME + N'，客户ID：' + CAST(@IN_CUST_ID AS VARCHAR) 
                                    + N'，客户名称：' + ISNULL(@V_CUST_NAME,'')
        INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
            VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
            
        COMMIT TRANSACTION
    END TRY
    
    --异常处理
    BEGIN CATCH
        IF @@TRANCOUNT > 0 BEGIN
            ROLLBACK TRANSACTION
        END
        
        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)

        SELECT @V_ERROR_STR = N'Message:%s,Error:%d,Level:%d,State:%d,Procedure:%s,Line:%d',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()

        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_NUMBER,@V_ERROR_SEVERITY,@V_ERROR_STATE,
                  @V_ERROR_PROCEDURE,@V_ERROR_LINE)

        RETURN -100
    END CATCH
    
    RETURN 100
GO
