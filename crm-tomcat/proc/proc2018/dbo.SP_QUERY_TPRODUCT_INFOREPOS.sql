USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_QUERY_TPRODUCT_INFOREPOS @IN_PREPRODUCT_ID  INT,            --预发行产品ID
                                             @IN_PRODUCT_ID     INT,            --产品ID
                                             @IN_PRODUCT_CODE   NVARCHAR(6),    --产品编号
                                             @IN_PRODUCT_NAME   NVARCHAR(60),   --产品名称（模糊查询）
                                             @IN_CLASS_TYPE1    NVARCHAR(10),   --产品分类(1113)
                                             @IN_SEARCHKEY      NVARCHAR(200),  --搜索关键词
                                             @IN_INPUT_MAN      INT,
                                             @IN_PRODUCT_STATUS INT = NULL,     --产品状态（0全部 2推介期 3正常期 4结束）
                                             @IN_OPENFLAG       INT = NULL,     --产品发行方式（0全部 1开放式 2封闭式）
                                             @IN_SORTFIELD      NVARCHAR(200) = NULL,   --排序字段
                                             @IN_PRE_DATE1      INT = NULL,     --推介期下限
                                             @IN_PRE_DATE2      INT = NULL,     --推介期上限
                                             @IN_PRESTART_TIME1 DATETIME=NULL,  --预约开始时间下限
                                             @IN_PRESTART_TIME2 DATETIME=NULL,  --预约开始时间上限
                                             @IN_START_DATE1    INT = NULL,     --产品成立日期下限
                                             @IN_START_DATE2    INT = NULL,     --产品成立日期上限
                                             @IN_FACT_MONEY1    DEC(16,3) = NULL,   --产品规模下限
                                             @IN_FACT_MONEY2    DEC(16,3) = NULL    --产品规模上限
WITH ENCRYPTION
AS
    SET NOCOUNT ON
    SET @IN_PREPRODUCT_ID = ISNULL(@IN_PREPRODUCT_ID,0)
    SET @IN_PRODUCT_ID = ISNULL(@IN_PRODUCT_ID,0)
    SET @IN_PRODUCT_CODE = ISNULL(@IN_PRODUCT_CODE,'')
    SET @IN_PRODUCT_NAME = ISNULL(@IN_PRODUCT_NAME,'')
    SET @IN_CLASS_TYPE1 = ISNULL(@IN_CLASS_TYPE1,'')
    IF @IN_PRE_DATE1 IS NULL SET @IN_PRE_DATE1 = 0
    IF @IN_PRE_DATE2 = 0 OR @IN_PRE_DATE2 IS NULL SET @IN_PRE_DATE2 = 99999999
    IF @IN_PRESTART_TIME1 IS NULL OR @IN_PRESTART_TIME1=0 SET @IN_PRESTART_TIME1 = '1900-01-01'
    IF @IN_PRESTART_TIME2 IS NULL OR @IN_PRESTART_TIME2=0 SET @IN_PRESTART_TIME2 = '2199-12-31'
    SET @IN_START_DATE1 = ISNULL(@IN_START_DATE1,0)
    IF @IN_START_DATE2 = 0 OR @IN_START_DATE2 IS NULL SET @IN_START_DATE2 = 99999999
    SET @IN_FACT_MONEY1 = ISNULL(@IN_FACT_MONEY1,0)
    IF @IN_FACT_MONEY2 = 0 OR @IN_FACT_MONEY2 IS NULL SET @IN_FACT_MONEY2 = 9999999999999.0
    IF @IN_PRODUCT_STATUS IS NULL SET @IN_PRODUCT_STATUS = 0
    IF @IN_OPENFLAG IS NULL SET @IN_OPENFLAG = 0
    SET @IN_SORTFIELD = ISNULL(@IN_SORTFIELD,'')
    SET @IN_SEARCHKEY = ISNULL(@IN_SEARCHKEY,'')

    DECLARE @V_SQL NVARCHAR(MAX)
    DECLARE @V_SW20409 INT, @IN_BOOK_CODE INT
    SET @IN_BOOK_CODE = 1
    SELECT @V_SW20409 = VALUE FROM INTRUST..TSYSCONTROL WHERE FLAG_TYPE = 'SW20409'
    DECLARE @V_PRODUCTS TABLE(PRODUCT_ID INT)
    IF @V_SW20409 = 1
        INSERT INTO @V_PRODUCTS
            SELECT PRODUCT_ID FROM INTRUST..TPRODUCT WHERE BOOK_CODE = @IN_BOOK_CODE
                AND(INTRUST_FLAG1 IN ( SELECT -(FUNC_ID-2040900)%2+2 FROM INTRUST..TOPRIGHT WHERE MENU_ID = '20409' AND FUNC_ID >= 2040900 AND FUNC_ID <= 2040999 AND OP_CODE = @IN_INPUT_MAN )
                    OR PRODUCT_ID IN ( SELECT FUNC_ID FROM INTRUST..TOPRIGHT WHERE MENU_ID = '20409' AND FUNC_ID < 2040900 AND OP_CODE = @IN_INPUT_MAN)
                    OR ADMIN_MANAGER = @IN_INPUT_MAN OR INPUT_MAN = @IN_INPUT_MAN OR ISNULL(@IN_INPUT_MAN,0)=0
                    OR ADMIN_MANAGER IN(SELECT OP_CODE FROM INTRUST..TOPCUSTRIGHT WHERE ENABLE_OP_CODE = @IN_INPUT_MAN)
                    )

    CREATE TABLE #TMP_PRODUCT_INFOREPOS(
        PREPRODUCT_ID       INT,            --预发行产品ID
        PRODUCT_ID          INT,            --产品ID
        PRODUCT_NAME        NVARCHAR(60),   --产品名称
        PERIOD              NVARCHAR(500),  --期限
        EXPRE_START_DATE    DATETIME,       --开始预约日期
        PRE_MONEY           DECIMAL(16,3),  --预期发行金额
        PROFIT_DATE         NVARCHAR(4000), --收益分配时间
        PRE_RATE            NTEXT, 
        CASH_USETYPE        NVARCHAR(4000), --资金运用方式
        ENSURE_METHOD       NVARCHAR(4000), --保障措施
        PRE_START_DATE      INT,            --推介开始日期
        PRE_END_DATE        INT,            --推介结束日期
        CLASS_TYPE1         NVARCHAR(10),   --产品分类(1113)
        CLASS_TYPE1_NAME    NVARCHAR(30),   --产品分类：固定收益类产品、阳光私募类产品、PE类产品
        ADMIN_MANAGER       NVARCHAR(30),   --项目责任人
        DIRECT_SALE         INT,            --1代销 2直销
        ANNOUNCE_URL        NVARCHAR(200),  --信息披露地址
        SUMMARY             NTEXT,          --简要备注
        KFQ          NVARCHAR(2000),        --开放期
        SHSQTJQ      NVARCHAR(2000),        --赎回申请提交期
        SYZH         NVARCHAR(2000),        --账户信息
        DXJG         NVARCHAR(2000),        --代销机构
        TZGW         NVARCHAR(2000),        --投资顾问/受益人代表
        JJJL         NVARCHAR(2000),        --基金经理
        KFR          NVARCHAR(2000),        --开放日
        PRE_FACT_MONEY      DECIMAL(16,3),  --已预约金额
        INPUT_TIME          DATETIME,       --录入时间
        STATUS              NVARCHAR(30),   --产品状态(1102)
        STATUS_NAME         NVARCHAR(30),   --产品状态
    --以上是CRM库中预发行产品数据；以下是INTRUST库中部分产品信息
        PRODUCT_CODE        NVARCHAR(6),    --产品编号
        VALID_PERIOD        INT,            --期限
        PERIOD_UNIT         INT,            --期限单位 0无期限 1天 2月 3年
        PRE_MAX_MONEY       DECIMAL(16,3),  --最大预约规模
        FACT_MONEY          DECIMAL(16,3),  --实际发行金额
        BEN_PERIOD          INT,            --收益分配周期(月)
        INTRUST_TYPE        NVARCHAR(10),   --运用方式(1104)
        INTRUST_TYPE_NAME   NVARCHAR(30),   --运用方式名称
        EXP_RATE1           DECIMAL(7,6),   --预期收益率从
        EXP_RATE2           DECIMAL(7,6),   --预期收益率到
        START_DATE          INT,            --成立日期
        END_DATE            INT,            --结束日期
        EXPRE_END_TIME      DATETIME,       --预约截止时间
        PRE_VALID_DAYS      INT,            --预约有效天数
        TG_BANK_ID          NVARCHAR(10),   --托管行ID(1803)
        TG_BANK_NAME        NVARCHAR(30),   --托管行名称
        TG_BANK_SUB_NAME    NVARCHAR(30),   --托管行支行
        TG_BANK_ACCT        NVARCHAR(30),   --信托专户
        TG_ACCT_NAME        NVARCHAR(100),  --信托专户名称
        PRODUCT_INFO        NTEXT,          --简介（格式文本）
    --简短描述用于界面列表产品时显示的摘要
        BRIEF_SUMMARY       NVARCHAR(4000) --简短描述，用于列表页面简要列示关键信息（拼接字符串）
    )

    --先取“预发行产品”表数据
    INSERT INTO #TMP_PRODUCT_INFOREPOS(PREPRODUCT_ID,PRODUCT_ID,PRODUCT_NAME,PERIOD,EXPRE_START_DATE,PRE_MONEY,
            PROFIT_DATE,PRE_RATE,CASH_USETYPE,ENSURE_METHOD,PRE_START_DATE,PRE_END_DATE,CLASS_TYPE1,CLASS_TYPE1_NAME,
            ADMIN_MANAGER,DIRECT_SALE,ANNOUNCE_URL,SUMMARY,PRE_FACT_MONEY,INPUT_TIME,STATUS,STATUS_NAME,START_DATE,EXPRE_END_TIME,PRE_VALID_DAYS)
        SELECT A.PREPRODUCT_ID,A.BIND_PRODUCT_ID,A.PREPRODUCT_NAME,A.PERIOD,A.EXPRE_START_TIME,A.PRE_MONEY,
            A.PROFIT_DATE,A.PRE_RATE,A.CASH_USETYPE,A.ENSURE_METHOD,A.PRE_START_DATE,A.PRE_END_DATE,A.CLASS_TYPE1,A.CLASS_TYPE1_NAME,
            A.ADMIN_MANAGER,A.DIRECT_SALE,A.ANNOUNCE_URL,A.SUMMARY,A.PRE_FACT_MONEY,A.INPUT_TIME,A.STATUS,A.STATUS_NAME,A.START_DATE
            ,A.EXPRE_END_TIME,A.PRE_VALID_DAYS
        FROM EFCRM..TPREPRODUCT A
        WHERE (PREPRODUCT_ID = @IN_PREPRODUCT_ID OR @IN_PREPRODUCT_ID = 0)
            AND(A.BIND_PRODUCT_ID IS NULL OR A.BIND_PRODUCT_ID = @IN_PRODUCT_ID OR @IN_PRODUCT_ID = 0)
            AND(A.PREPRODUCT_NAME LIKE '%'+@IN_PRODUCT_NAME+'%' OR @IN_PRODUCT_NAME = '')
            AND(A.CLASS_TYPE1 = @IN_CLASS_TYPE1 OR @IN_CLASS_TYPE1 = '')
            AND(ISNULL(A.PRE_START_DATE,0) BETWEEN @IN_PRE_DATE1 AND @IN_PRE_DATE2 OR ISNULL(A.PRE_END_DATE,0) BETWEEN @IN_PRE_DATE1 AND @IN_PRE_DATE2)
            AND(ISNULL(A.EXPRE_START_TIME,0) BETWEEN @IN_PRESTART_TIME1 AND @IN_PRESTART_TIME2)
            AND(@IN_PRODUCT_STATUS=0 OR (@IN_PRODUCT_STATUS=2 AND A.STATUS='110202') OR (@IN_PRODUCT_STATUS=3 AND A.STATUS='110203') OR (@IN_PRODUCT_STATUS=4 AND A.STATUS='110204'))
            AND(A.PREPRODUCT_NAME LIKE '%'+@IN_SEARCHKEY+'%' OR A.SUMMARY LIKE '%'+@IN_SEARCHKEY+'%')
    --扩展字段
    --20111229 不控制这些产品要素的条件
    UPDATE #TMP_PRODUCT_INFOREPOS SET KFQ = B.EXTEND_VALUE
        FROM #TMP_PRODUCT_INFOREPOS A, TEXTENDINFO B, TEXTENDDEFINE C
        WHERE B.EXTEND_NO = C.EXTEND_NO AND C.TABLE_NAME = 'TPREPRODUCT' AND C.FIELD_NAME = N'开放期'
            --AND C.CONDITION = 'CLASS_TYPE1='''+A.CLASS_TYPE1+'''' AND CONVERT(NVARCHAR(60),A.PREPRODUCT_ID) = B.KEY_VALUE
            AND C.CONDITION = '' AND CONVERT(NVARCHAR(60),A.PREPRODUCT_ID) = B.KEY_VALUE
    UPDATE #TMP_PRODUCT_INFOREPOS SET SHSQTJQ = B.EXTEND_VALUE
        FROM #TMP_PRODUCT_INFOREPOS A, TEXTENDINFO B, TEXTENDDEFINE C
        WHERE B.EXTEND_NO = C.EXTEND_NO AND C.TABLE_NAME = 'TPREPRODUCT' AND C.FIELD_NAME = N'赎回申请提交期'
            AND C.CONDITION = '' AND CONVERT(NVARCHAR(60),A.PREPRODUCT_ID) = B.KEY_VALUE
    UPDATE #TMP_PRODUCT_INFOREPOS SET SYZH = B.EXTEND_VALUE
        FROM #TMP_PRODUCT_INFOREPOS A, TEXTENDINFO B, TEXTENDDEFINE C
        WHERE B.EXTEND_NO = C.EXTEND_NO AND C.TABLE_NAME = 'TPREPRODUCT' AND C.FIELD_NAME = N'账户信息'
            AND C.CONDITION = '' AND CONVERT(NVARCHAR(60),A.PREPRODUCT_ID) = B.KEY_VALUE
    UPDATE #TMP_PRODUCT_INFOREPOS SET DXJG = B.EXTEND_VALUE
        FROM #TMP_PRODUCT_INFOREPOS A, TEXTENDINFO B, TEXTENDDEFINE C
        WHERE B.EXTEND_NO = C.EXTEND_NO AND C.TABLE_NAME = 'TPREPRODUCT' AND C.FIELD_NAME = N'代销机构'
            AND C.CONDITION = '' AND CONVERT(NVARCHAR(60),A.PREPRODUCT_ID) = B.KEY_VALUE
    UPDATE #TMP_PRODUCT_INFOREPOS SET TZGW = B.EXTEND_VALUE
        FROM #TMP_PRODUCT_INFOREPOS A, TEXTENDINFO B, TEXTENDDEFINE C
        WHERE B.EXTEND_NO = C.EXTEND_NO AND C.TABLE_NAME = 'TPREPRODUCT' AND C.FIELD_NAME = N'投资顾问/受益人代表'
            AND C.CONDITION = '' AND CONVERT(NVARCHAR(60),A.PREPRODUCT_ID) = B.KEY_VALUE
    UPDATE #TMP_PRODUCT_INFOREPOS SET JJJL = B.EXTEND_VALUE
        FROM #TMP_PRODUCT_INFOREPOS A, TEXTENDINFO B, TEXTENDDEFINE C
        WHERE B.EXTEND_NO = C.EXTEND_NO AND C.TABLE_NAME = 'TPREPRODUCT' AND C.FIELD_NAME = N'基金经理'
            AND C.CONDITION = '' AND CONVERT(NVARCHAR(60),A.PREPRODUCT_ID) = B.KEY_VALUE
    UPDATE #TMP_PRODUCT_INFOREPOS SET KFR = B.EXTEND_VALUE
        FROM #TMP_PRODUCT_INFOREPOS A, TEXTENDINFO B, TEXTENDDEFINE C
        WHERE B.EXTEND_NO = C.EXTEND_NO AND C.TABLE_NAME = 'TPREPRODUCT' AND C.FIELD_NAME = N'开放日'
            AND C.CONDITION = '' AND CONVERT(NVARCHAR(60),A.PREPRODUCT_ID) = B.KEY_VALUE
    --补充INTRUST..TPRODUCT中的记录
    INSERT INTO #TMP_PRODUCT_INFOREPOS(PRODUCT_ID,PRODUCT_NAME,INPUT_TIME,PRE_MONEY,PRE_START_DATE,PRE_END_DATE,ADMIN_MANAGER,
            SUMMARY,PERIOD,PRE_RATE,START_DATE)
        SELECT A.PRODUCT_ID,A.PRODUCT_NAME,A.INPUT_TIME,A.PRE_MONEY,A.PRE_START_DATE,A.PRE_END_DATE,B.OP_NAME,A.SUMMARY,
               CASE PERIOD_UNIT WHEN 1 THEN CONVERT(NVARCHAR(20),ISNULL(VALID_PERIOD,''))+N'日' WHEN 2 THEN CONVERT(NVARCHAR(20),ISNULL(VALID_PERIOD,''))+N'个月' WHEN 3 THEN CONVERT(NVARCHAR(20),ISNULL(VALID_PERIOD,''))+N'年' ELSE N'无期限' END,
               CONVERT(NVARCHAR(20),CONVERT(DEC(16,2),ISNULL(EXP_RATE1,0)))+'-'+CONVERT(NVARCHAR(20),CONVERT(DEC(16,2),ISNULL(EXP_RATE2,0))),A.START_DATE
        FROM INTRUST..TPRODUCT A, INTRUST..TOPERATOR B
        WHERE A.ADMIN_MANAGER = B.OP_CODE AND A.INTRUST_FLAG1 = 2 --仅集合产品
            AND NOT EXISTS(SELECT 1 FROM EFCRM..TPREPRODUCT Z WHERE A.PRODUCT_ID = Z.BIND_PRODUCT_ID)
            AND(A.PRODUCT_ID = @IN_PRODUCT_ID OR @IN_PRODUCT_ID = 0)
            AND(A.PRODUCT_CODE = @IN_PRODUCT_CODE OR @IN_PRODUCT_CODE = '')
            AND(A.PRODUCT_NAME LIKE '%'+@IN_PRODUCT_NAME+'%')
            AND(ISNULL(A.PRE_START_DATE,0) BETWEEN @IN_PRE_DATE1 AND @IN_PRE_DATE2 OR ISNULL(A.PRE_END_DATE,0) BETWEEN @IN_PRE_DATE1 AND @IN_PRE_DATE2)
            AND(ISNULL(A.START_DATE,0) BETWEEN @IN_START_DATE1 AND @IN_START_DATE2)
            AND(ISNULL(A.FACT_MONEY,0) BETWEEN @IN_FACT_MONEY1 AND @IN_FACT_MONEY2)
            AND(A.PRODUCT_NAME LIKE '%'+@IN_SEARCHKEY+'%' OR A.SUMMARY LIKE '%'+@IN_SEARCHKEY+'%' OR A.PRODUCT_INFO LIKE '%'+@IN_SEARCHKEY+'%')
            AND(@V_SW20409 = 0 OR @V_SW20409 IS NULL OR A.PRODUCT_ID IN (SELECT PRODUCT_ID FROM @V_PRODUCTS))
            AND(@IN_PRODUCT_STATUS=0 OR (@IN_PRODUCT_STATUS=2 AND A.PRODUCT_STATUS='110202') OR (@IN_PRODUCT_STATUS=3 AND A.PRODUCT_STATUS='110203') OR (@IN_PRODUCT_STATUS=4 AND A.PRODUCT_STATUS='110204'))
            AND(@IN_OPENFLAG=0 OR (@IN_OPENFLAG=1 AND A.OPEN_FLAG=1) OR (@IN_OPENFLAG=2 AND A.OPEN_FLAG=2))
    --从INTRUST..TPRODUCT更新部分字段
    UPDATE #TMP_PRODUCT_INFOREPOS
        SET DIRECT_SALE = B.TA_FLAG
        FROM #TMP_PRODUCT_INFOREPOS A, INTRUST..TPRODUCTLIMIT B
        WHERE A.PRODUCT_ID = B.PRODUCT_ID AND A.PREPRODUCT_ID IS NULL
    UPDATE #TMP_PRODUCT_INFOREPOS
        SET PRODUCT_CODE = B.PRODUCT_CODE, VALID_PERIOD = B.VALID_PERIOD, PERIOD_UNIT = B.PERIOD_UNIT, PRE_MAX_MONEY = B.PRE_MAX_MONEY,
            FACT_MONEY = B.FACT_MONEY, BEN_PERIOD = B.BEN_PERIOD, INTRUST_TYPE = B.INTRUST_TYPE, INTRUST_TYPE_NAME = B.INTRUST_TYPE_NAME,
            EXP_RATE1 = B.EXP_RATE1, EXP_RATE2 = B.EXP_RATE2, END_DATE = B.END_DATE,
            TG_BANK_ID = B.TG_BANK_ID, TG_BANK_NAME = B.TG_BANK_NAME, TG_BANK_SUB_NAME = B.TG_BANK_SUB_NAME, TG_BANK_ACCT = B.TG_BANK_ACCT,
            TG_ACCT_NAME = B.TG_ACCT_NAME, PRODUCT_INFO = B.PRODUCT_INFO
        FROM #TMP_PRODUCT_INFOREPOS A, INTRUST..TPRODUCT B
        WHERE A.PRODUCT_ID = B.PRODUCT_ID
    UPDATE #TMP_PRODUCT_INFOREPOS
        SET STATUS = B.PRODUCT_STATUS, STATUS_NAME = B.PRODUCT_STATUS_NAME, START_DATE = B.START_DATE,
            PERIOD = CASE B.PERIOD_UNIT WHEN 1 THEN CONVERT(NVARCHAR(20),ISNULL(B.VALID_PERIOD,''))+N'日' WHEN 2 THEN CONVERT(NVARCHAR(20),ISNULL(B.VALID_PERIOD,''))+N'个月' WHEN 3 THEN CONVERT(NVARCHAR(20),ISNULL(B.VALID_PERIOD,''))+N'年' ELSE N'无期限' END,
            PRE_MONEY = B.PRE_MONEY, CASH_USETYPE = B.INTRUST_TYPE1_NAME, PRE_START_DATE = B.PRE_START_DATE, PRE_END_DATE = B.PRE_END_DATE,
            PRE_RATE = CONVERT(NVARCHAR(20),CONVERT(DEC(16,2),ISNULL(B.EXP_RATE1,0)))+'-'+CONVERT(NVARCHAR(20),CONVERT(DEC(16,2),ISNULL(B.EXP_RATE2,0))),
            PROFIT_DATE = CASE WHEN B.BEN_PERIOD IS NOT NULL THEN CONVERT(NVARCHAR(30),B.BEN_PERIOD)+N'个月' ELSE N'无' END
        FROM #TMP_PRODUCT_INFOREPOS A, INTRUST..TPRODUCT B
        WHERE A.PRODUCT_ID = B.PRODUCT_ID AND A.PREPRODUCT_ID IS NULL
    --拼接简短描述
    UPDATE #TMP_PRODUCT_INFOREPOS SET BRIEF_SUMMARY = SUBSTRING(
          N'<b>期限：</b>' + CASE WHEN PERIOD IS NULL THEN N'无' ELSE dbo.clearhtml(PERIOD) END + CASE WHEN LEN(dbo.clearhtml(PERIOD))>50 THEN + N'；<BR>' ELSE N'；' END
        + N'<b>预约开始日：</b>' + ISNULL(CONVERT(VARCHAR(10),EXPRE_START_DATE,120),N'无') + N'；'
        + N'<b>预募集规模：</b>' + CONVERT(NVARCHAR(20),CONVERT(DEC(16,2),ISNULL(PRE_MONEY,0)/10000)) + N'万元；'
        + N'<b>推介期：</b>' + dbo.GETDATESTRYMD(PRE_START_DATE) + N'-' + dbo.GETDATESTRYMD(PRE_END_DATE) + N'；<BR>'
        + N'<b>收益分配时间：</b>' + CASE WHEN PROFIT_DATE IS NULL THEN N'无' ELSE dbo.clearhtml(PROFIT_DATE) END + CASE WHEN LEN(dbo.clearhtml(PROFIT_DATE))>50 THEN + N'；<BR>' ELSE N'；' END
		--+ N'<b>预期收益率：</b>' + CASE WHEN PRE_RATE IS NULL THEN N'无' ELSE dbo.clearhtml(PRE_RATE) END + CASE WHEN LEN(dbo.clearhtml(PRE_RATE))>50 THEN + N'；<BR>' ELSE N'；' END
        + N'<b>资金运用方式：</b>' + CASE WHEN CASH_USETYPE IS NULL THEN N'无' ELSE dbo.clearhtml(CASH_USETYPE) END + CASE WHEN LEN(dbo.clearhtml(CASH_USETYPE))>50 THEN + N'；<BR>' ELSE N'；' END
        + N'<b>保障措施：</b>' + CASE WHEN ENSURE_METHOD IS NULL THEN N'无' ELSE dbo.clearhtml(ENSURE_METHOD) END + CASE WHEN LEN(dbo.clearhtml(ENSURE_METHOD))>50 THEN + N'；<BR>' ELSE N'；' END
        , 1, 500)

    SET @V_SQL = 'SELECT *,EXPRE_START_DATE AS EXPRE_START_TIME FROM #TMP_PRODUCT_INFOREPOS WHERE 1 = 1 and PREPRODUCT_ID not in (13,111,115,117,118,139,262,357) '
    IF @IN_CLASS_TYPE1 <> ''
        SET @V_SQL = @V_SQL + ' AND(CLASS_TYPE1 = ''' + @IN_CLASS_TYPE1 + ''')'
    IF @IN_PREPRODUCT_ID <> ''
        SET @V_SQL = @V_SQL + ' AND(PREPRODUCT_ID = ' + CONVERT(NVARCHAR(30),@IN_PREPRODUCT_ID) + ')'
    IF @IN_PRODUCT_ID <> ''
        SET @V_SQL = @V_SQL + ' AND(PRODUCT_ID = ' + CONVERT(NVARCHAR(30),@IN_PRODUCT_ID) + ')'
    IF @IN_SORTFIELD IS NOT NULL AND @IN_SORTFIELD <> ''
        SET @V_SQL = @V_SQL + ' ORDER BY ' + @IN_SORTFIELD
    ELSE
        SET @V_SQL = @V_SQL + ' ORDER BY INPUT_TIME DESC'
    EXECUTE( @V_SQL )
GO
