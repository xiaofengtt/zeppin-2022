USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_AFFIRM_TQUOTIENTCHANGE @IN_SERIAL_NO    INT,   --主键
                                           @IN_CHECK_FLAG   INT,   --2审核通过1审核不通过
                                           @IN_INPUT_MAN    INT    --录入操作员
WITH ENCRYPTION
AS
    SET NOCOUNT ON
    DECLARE @V_RET_CODE INT,@IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)
    DECLARE @V_SUBSCRIBE_ID INT, @V_CUST_ID INT, @V_QUOTIENT_AMOUNT DEC(16,3), @V_NONPRODUCT_ID INT
            , @V_QUOTIENT_MONEY DEC(16,3), @V_START_DATE INT, @V_END_DATE INT
            , @V_BANK_ID NVARCHAR(10), @V_BANK_NAME NVARCHAR(30)
            , @V_BANK_SUB_NAME NVARCHAR(60), @V_BANK_ACCT NVARCHAR(60)
    DECLARE @V_VALID_PRIOD INT ,@V_VALID_PRIOD_UNIT INT
    SELECT @SBUSI_NAME = N'非信托产品份额确认'
    SELECT @SSUMMARY = N'非信托产品份额确认'
    SELECT @IBUSI_FLAG = 39011
    SELECT @V_RET_CODE = -39011000

    IF NOT EXISTS(SELECT 1 FROM TQUOTIENTCHANGE WHERE SERIAL_NO = @IN_SERIAL_NO)
        RETURN @V_RET_CODE - 11   --非信托产品份额信息不存在

    SELECT @V_SUBSCRIBE_ID = A.SUBSCRIBE_ID, @V_CUST_ID = A.CUST_ID, @V_QUOTIENT_AMOUNT = A.CHANGE_AMOUNT
             , @V_QUOTIENT_MONEY = A.CHANGE_MONEY, @V_START_DATE = A.CHANGE_DATE,@V_VALID_PRIOD_UNIT = C.VALID_PRIOD_UNIT,
            @V_VALID_PRIOD = C.VALID_PRIOD, @V_BANK_ID = B.BANK_ID, @V_BANK_NAME = B.BANK_NAME,
            @V_BANK_SUB_NAME = B.BANK_SUB_NAME, @V_BANK_ACCT = B.BANK_ACCT, @V_NONPRODUCT_ID = C.NONPRODUCT_ID
        FROM TQUOTIENTCHANGE A, TSUBSCRIBE B, TNONPRODUCT C
            WHERE (A.SERIAL_NO = @IN_SERIAL_NO AND A.SUBSCRIBE_ID = B.SUBSCRIBE_ID AND B.NONPRODUCT_ID = C.NONPRODUCT_ID)

    IF @V_VALID_PRIOD_UNIT = 1
        SELECT @V_END_DATE = dbo.GETDAY(@V_START_DATE,@V_VALID_PRIOD)
    ELSE IF @V_VALID_PRIOD_UNIT = 2
        SELECT @V_END_DATE = dbo.GETMONTH(@V_START_DATE,@V_VALID_PRIOD)
    ELSE IF @V_VALID_PRIOD_UNIT = 3
        SELECT @V_END_DATE = dbo.GETMONTH(@V_START_DATE,@V_VALID_PRIOD*3)
    ELSE
        SELECT @V_END_DATE = (@V_START_DATE + @V_VALID_PRIOD *10000)

    BEGIN TRANSACTION

    IF @IN_CHECK_FLAG = 2 --审核通过
    BEGIN
        INSERT INTO TQUOTIENT(SUBSCRIBE_ID,CUST_ID,QUOTIENT_AMOUNT,QUOTIENT_MONEY,START_DATE,END_DATE
                   ,BANK_ID,BANK_NAME,BANK_SUB_NAME,BANK_ACCT,STATUS,STATUS_NAME,INPUT_MAN,INPUT_TIME)
                VALUES(@V_SUBSCRIBE_ID,@V_CUST_ID,@V_QUOTIENT_AMOUNT,@V_QUOTIENT_MONEY,@V_START_DATE
                    ,@V_END_DATE,@V_BANK_ID,@V_BANK_NAME,@V_BANK_SUB_NAME,@V_BANK_ACCT
                    ,121101, '正常', @IN_INPUT_MAN, GETDATE())
        IF @@ERROR <> 0
        BEGIN
            ROLLBACK TRANSACTION
            RETURN -100
        END
        UPDATE TQUOTIENTCHANGE SET CHECK_FLAG = 2,CHECK_TIME = GETDATE(),CHECK_MAN = @IN_INPUT_MAN
            WHERE SERIAL_NO = @IN_SERIAL_NO
        IF @@ERROR <> 0
        BEGIN
            ROLLBACK TRANSACTION
            RETURN -100
        END
        UPDATE TNONPRODUCT SET FACE_MONEY = ISNULL(FACE_MONEY,0) + @V_QUOTIENT_AMOUNT WHERE NONPRODUCT_ID = @V_NONPRODUCT_ID
        IF @@ERROR <> 0
        BEGIN
            ROLLBACK TRANSACTION
            RETURN -100
        END
    END
    ELSE
    BEGIN
        DELETE FROM TQUOTIENTCHANGE WHERE SERIAL_NO = @IN_SERIAL_NO
        IF @@ERROR <> 0
        BEGIN
            ROLLBACK TRANSACTION
            RETURN -100
        END
        UPDATE TSUBSCRIBE SET CHECK_FLAG = 1 WHERE SUBSCRIBE_ID = @V_SUBSCRIBE_ID
        IF @@ERROR <> 0
        BEGIN
            ROLLBACK TRANSACTION
            RETURN -100
        END
    END

    SET @SSUMMARY = N'非信托产品份额确认，操作员：' + CAST(@IN_INPUT_MAN AS NVARCHAR(10))
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END

    COMMIT TRANSACTION
    RETURN 100
GO
