USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_ADD_TCOMAINTAINMONEYMX @IN_MAINTAIN_ID   INT,              --维护费(维护合同)ID，对应合同TCOCONTRACTMAINTAIN. MAINTAIN_ID
                                           @IN_ARRIVE_TIME   INT,              --到款时间
                                           @IN_ARRIVE_MONEY  DECIMAL(16,3),    --到款金额
                                           @IN_ARRIVE_SUMMARY  NVARCHAR(2000), --备注
                                           @IN_INPUT_MAN     INT,              --录入人员，对应表TOPERATOR.OP_CODE
                                           @IN_FLAG          INT               --操作员可自己修改的到账标志，用于折扣等，即使到账金额不等于合同金额也可以更改
WITH ENCRYPTION
AS
    DECLARE @V_RET_CODE INT,@IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)
    DECLARE @V_WH_MONEY   DECIMAL(16,3)         --维护费金额
    DECLARE @V_COCONTRACTMAINTAIN_SUB_BH NVARCHAR(200)   --维护合同编号
    DECLARE @V_ARRIVE_MONEY DECIMAL(16,3)       --实际已到帐金额

    SELECT @SBUSI_NAME = N'合同管理之新增维护合同到帐明细'
    SELECT @SSUMMARY = N'合同管理之新增维护合同到帐明细'
    SELECT @IBUSI_FLAG = 50001
    SELECT @V_RET_CODE = -50001000

    SELECT @V_COCONTRACTMAINTAIN_SUB_BH = COCONTRACTMAINTAIN_SUB_BH,@V_WH_MONEY = WH_MONEY,@V_ARRIVE_MONEY = ARRIVE_MONEY
        FROM TCOCONTRACTMAINTAIN WHERE MAINTAIN_ID =@IN_MAINTAIN_ID

    BEGIN TRANSACTION

    INSERT INTO TCOMAINTAINMONEYMX(MAINTAIN_ID,ARRIVE_TIME,ARRIVE_MONEY,INPUT_MAN,ARRIVE_SUMMARY)
        VALUES(@IN_MAINTAIN_ID,@IN_ARRIVE_TIME,@IN_ARRIVE_MONEY,@IN_INPUT_MAN,@IN_ARRIVE_SUMMARY)
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END
    --更新付款计划表中到帐金额
    IF @IN_FLAG = 1
    BEGIN
        UPDATE TCOCONTRACTMAINTAIN
            SET ARRIVE_MONEY = ISNULL(ARRIVE_MONEY,0) + ISNULL(@IN_ARRIVE_MONEY,0),
                ARRIVEMONEY_FLAG = 3
            WHERE MAINTAIN_ID = @IN_MAINTAIN_ID
    END
    ELSE
    BEGIN
    UPDATE TCOCONTRACTMAINTAIN
        SET ARRIVE_MONEY = ISNULL(ARRIVE_MONEY,0) + ISNULL(@IN_ARRIVE_MONEY,0),
            ARRIVEMONEY_FLAG = CASE WHEN (@V_WH_MONEY <= (ISNULL(@V_ARRIVE_MONEY,0) + ISNULL(@IN_ARRIVE_MONEY,0))) THEN 3 ELSE 2 END
        WHERE MAINTAIN_ID = @IN_MAINTAIN_ID
    END
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END
    --更新收款计划提醒表(TMONEYTASKINFO,TMONEYTASKLIST)中已读标志，如果全部到账了就将该标志改为不再提示
    IF EXISTS(SELECT 1 FROM TMONEYTASKINFO WHERE RECORD_ID = @IN_MAINTAIN_ID) AND EXISTS(SELECT 1 FROM TCOCONTRACTMAINTAIN WHERE MAINTAIN_ID = @IN_MAINTAIN_ID AND ARRIVEMONEY_FLAG = 3)
    BEGIN
        DECLARE @V_REC_NO INT,@V_SERIAL_NO INT
        SELECT @V_REC_NO = SERIAL_NO FROM TMONEYTASKINFO WHERE RECORD_ID = @IN_MAINTAIN_ID
        SELECT @V_SERIAL_NO = SERIAL_NO FROM TMONEYTASKLIST WHERE REC_NO = @V_REC_NO
        UPDATE TMONEYTASKLIST SET READ_FLAG = 3 WHERE SERIAL_NO = @V_SERIAL_NO
    END
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END
    --
    SELECT @SSUMMARY = N'合同管理之新增维护合同到帐明细，合同编号：'+@V_COCONTRACTMAINTAIN_SUB_BH + '，到帐日期：'+CONVERT(NVARCHAR(8),@IN_ARRIVE_TIME)+'，到帐金额：'+CONVERT(VARCHAR(30),@IN_ARRIVE_MONEY)
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END

    COMMIT TRANSACTION
    RETURN 100
GO
