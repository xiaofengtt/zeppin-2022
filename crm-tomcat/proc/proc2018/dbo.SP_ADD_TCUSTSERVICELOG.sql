USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE  SP_ADD_TCUSTSERVICELOG	@IN_CUST_ID              INT,              --客户ID
											@IN_CUST_NO              NVARCHAR(8),      --客户编号
											@IN_SERVICE_TIME         DATETIME ,        --服务、维护时间
											@IN_SERVICE_INFO         NVARCHAR(100),    --服务项目
											@IN_SERVICE_SUMMARY      NVARCHAR(500),    --服务项目的详细描述
											@IN_SERVICE_MAN			 INT,			   --客户经理
											@IN_EXECUTOR			 INT,			   --执行人
											@IN_CONTENT				 NVARCHAR(MAX),	   --交流内容
											@IN_SUBJECT				 NVARCHAR(60),	   --交流主题
											@IN_STEP_FLAG			 NVARCHAR(10),     --工作进展标志
											@IN_INFO_LEVEL			 NVARCHAR(10),	   --信息等级
											@IN_INPUT_MAN            INT               --录入操作员

WITH ENCRYPTION
AS
    DECLARE @V_RET_CODE INT, @IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40), @SSUMMARY NVARCHAR(200)
	DECLARE @V_STEP_FLAG_NAME NVARCHAR(30),@V_INFO_LEVEL_NAME NVARCHAR(30)
	DECLARE	@V_SERVICE_MAN_NAME NVARCHAR(200),@V_EXECUTOR_NAME NVARCHAR(200),@V_CUST_NAME NVARCHAR(90)
    SELECT @V_RET_CODE = -40501000, @IBUSI_FLAG = 40501
    SELECT @SBUSI_NAME = N'添加客户维护记录', @SSUMMARY = N'添加客户维护记录'
	SELECT @V_STEP_FLAG_NAME = TYPE_CONTENT FROM TDICTPARAM WHERE TYPE_VALUE = @IN_STEP_FLAG
	SELECT @V_INFO_LEVEL_NAME = TYPE_CONTENT FROM TDICTPARAM WHERE TYPE_VALUE = @IN_INFO_LEVEL
	SELECT @V_SERVICE_MAN_NAME = OP_NAME FROM TOPERATOR WHERE OP_CODE = @IN_SERVICE_MAN
	SELECT @V_EXECUTOR_NAME = OP_NAME FROM TOPERATOR WHERE OP_CODE = @IN_EXECUTOR
	SELECT @V_CUST_NAME=CUST_NAME FROM TCustomers WHERE CUST_ID=@IN_CUST_ID
    
    BEGIN TRY
    BEGIN TRANSACTION
    INSERT INTO TCUSTSERVICELOG(CUST_ID,CUST_NO,CUST_NAME,SERVICE_TIME,SERVICE_INFO,SERVICE_SUMMARY,CONTENT,STEP_FLAG,INFO_LEVEL,STEP_FLAG_NAME,INFO_LEVEL_NAME,SUBJECT,EXECUTOR,SERVICE_MAN,EXECUTOR_NAME,SERVICE_MAN_NAME)
		VALUES(@IN_CUST_ID,@IN_CUST_NO,@V_CUST_NAME,@IN_SERVICE_TIME,@IN_SERVICE_INFO,@IN_SERVICE_SUMMARY,@IN_CONTENT,@IN_STEP_FLAG,@IN_INFO_LEVEL,@V_STEP_FLAG_NAME,@V_INFO_LEVEL_NAME,@IN_SUBJECT,@IN_EXECUTOR,@IN_SERVICE_MAN,@V_EXECUTOR_NAME,@V_SERVICE_MAN_NAME)
    
    SET @SSUMMARY = N'添加客户维护记录，操作员：' + CAST(@IN_INPUT_MAN AS NVARCHAR(10))
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    
    COMMIT TRANSACTION
        RETURN 100
    END TRY
    
    --异常处理
    BEGIN CATCH
        IF @@TRANCOUNT > 0 BEGIN
            ROLLBACK TRANSACTION
        END
        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)

        SELECT @V_ERROR_STR = N'Message:%s,Error:%d,Level:%d,State:%d,Procedure:%s,Line:%d',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()

        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_NUMBER,@V_ERROR_SEVERITY,@V_ERROR_STATE,
                  @V_ERROR_PROCEDURE,@V_ERROR_LINE)

        RETURN -100
    END CATCH
GO
