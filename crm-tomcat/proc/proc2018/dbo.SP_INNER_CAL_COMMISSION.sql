USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_INNER_CAL_COMMISSION @IN_PRODUCT_ID     INT,                        --产品ID
                                         @IN_SUBPRODUCT_ID  INT,                        --子产品ID
                                         @IN_TO_MONEY       DECIMAL(16,2),              --待算金额
                                         @IN_START_DATE     INTEGER ,                   --合同起止日期
                                         @IN_END_DATE       INTEGER ,                   --合同终止日期
                                         @OUT_COMMIS_FEE    DECIMAL(16,3) = NULL OUTPUT,--提成金额
                                         @OUT_COMMIS_RATE   DECIMAL(5,4) = NULL OUTPUT  --提成比率
                                         
WITH ENCRYPTION
AS
    DECLARE @V_TO_AMOUNT DECIMAL(16,2),@V_FEE_MONEY DECIMAL(16,2),@V_FEE_RATE DECIMAL(5,4),@V_PURCHANSE_FEE_TYPE INT,
            @V_HOLD_DAYS INT, @V_PERIOD_UNIT INT, @V_PERIOD INT

    SELECT TOP 1 @V_PERIOD_UNIT =PERIOD_UNIT FROM TCOMMISSIONRATE WHERE PRODUCT_ID = @IN_PRODUCT_ID AND SUBPRODUCT_ID = @IN_SUBPRODUCT_ID
    SET @V_PERIOD_UNIT = ISNULL(@V_PERIOD_UNIT,0)
    
    --计算合同的实际天数
    SET @V_HOLD_DAYS =dbo.GETDATEDIFF(@IN_START_DATE, @IN_END_DATE)
    --select TOP 1 *,PERIOD_UNIT from TCOMMISSIONRATE where PRODUCT_ID = 1 AND SUBPRODUCT_ID = 0 AND PERIOD>=100 ORDER BY PERIOD
    IF @V_PERIOD_UNIT =0 --和合同期限无关
		SELECT @V_PERIOD =0
	ELSE IF @V_PERIOD_UNIT =1 --天数
		SELECT TOP 1 @V_PERIOD =PERIOD FROM TCOMMISSIONRATE WHERE PRODUCT_ID = @IN_PRODUCT_ID AND SUBPRODUCT_ID = @IN_SUBPRODUCT_ID AND PERIOD>=@V_HOLD_DAYS ORDER BY PERIOD
    ELSE IF @V_PERIOD_UNIT =2 --月份
		SELECT TOP 1 @V_PERIOD =PERIOD FROM TCOMMISSIONRATE WHERE PRODUCT_ID = @IN_PRODUCT_ID AND SUBPRODUCT_ID = @IN_SUBPRODUCT_ID AND PERIOD*30>=@V_HOLD_DAYS ORDER BY PERIOD
    ELSE IF @V_PERIOD_UNIT =3 --年份
		SELECT TOP 1 @V_PERIOD =PERIOD FROM TCOMMISSIONRATE WHERE PRODUCT_ID = @IN_PRODUCT_ID AND SUBPRODUCT_ID = @IN_SUBPRODUCT_ID AND PERIOD*360>=@V_HOLD_DAYS ORDER BY PERIOD
    --找出费率
    SELECT @V_FEE_RATE = FEE_RATE,@V_FEE_MONEY = FEE_AMOUNT
        FROM TCOMMISSIONRATE 
        WHERE PRODUCT_ID = @IN_PRODUCT_ID AND SUBPRODUCT_ID = @IN_SUBPRODUCT_ID 
            AND (PERIOD = @V_PERIOD OR @V_PERIOD=0)
            AND @IN_TO_MONEY >= TRADE_START_MONEY*10000.0 AND @IN_TO_MONEY < TRADE_END_MONEY*10000.0
    
    SET @V_FEE_RATE = ISNULL(@V_FEE_RATE,0)
    --计算提成
    IF @V_FEE_RATE>0
		SET @OUT_COMMIS_FEE = @V_FEE_RATE *@IN_TO_MONEY
    ELSE
		SET @OUT_COMMIS_FEE = ISNULL(@V_FEE_MONEY,0)
    SET @OUT_COMMIS_RATE = @V_FEE_RATE
    
GO
