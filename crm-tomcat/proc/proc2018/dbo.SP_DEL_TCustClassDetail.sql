USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_DEL_TCustClassDetail @IN_CLASSDETAIL_ID     INTEGER,        --分级定义ID
                                         @IN_INPUT_MAN          INTEGER         --操作员
WITH ENCRYPTION
AS
    DECLARE @V_RET_CODE INT,@IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)
    SELECT @SBUSI_NAME = N'删除客户分级定义明细'
    SELECT @SSUMMARY = N'删除客户分级定义明细'
    SELECT @IBUSI_FLAG = 20205
    SELECT @V_RET_CODE = -20205000

    DECLARE @V_CANMODI INTEGER, --是否可删除标志
            @V_CLASSDETAIL_NAME NVARCHAR(60)

    IF NOT EXISTS(SELECT * FROM TCustClassDetail WHERE CLASSDETAIL_ID = @IN_CLASSDETAIL_ID)
        RETURN @V_RET_CODE - 4 --记录不存在
    SELECT @V_CANMODI = CANMODI, @V_CLASSDETAIL_NAME = CLASSDETAIL_NAME FROM TCustClassDetail WHERE CLASSDETAIL_ID = @IN_CLASSDETAIL_ID
    IF @V_CANMODI <> 1
        RETURN @V_RET_CODE - 5 --记录不能删除

    BEGIN TRANSACTION

    DELETE FROM TCUSTOMERCLASS WHERE CLASSDETAIL_ID = @IN_CLASSDETAIL_ID
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END
    DELETE FROM TCustClassDetail WHERE CLASSDETAIL_ID = @IN_CLASSDETAIL_ID
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END

    SELECT @SSUMMARY = N'删除客户分级定义明细：ID:'+RTRIM(CONVERT(NVARCHAR(16),@IN_CLASSDETAIL_ID))+N'|名称:'+@V_CLASSDETAIL_NAME
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END

    COMMIT TRANSACTION
    RETURN 100
GO
