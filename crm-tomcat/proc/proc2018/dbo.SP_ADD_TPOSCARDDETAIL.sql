USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_ADD_TPOSCARDDETAIL 
							@IN_PRODUCT_ID     INTEGER,       --产品ID
							@IN_CUST_ID        INTEGER,       --客户ID
							@IN_AMOUNT         DECIMAL(12,2), --刷卡金额
							@IN_CARD_NO        NVARCHAR(20),  --刷卡卡号
							@IN_CARD_BANK_NO   NVARCHAR(20),  --刷卡卡号所属银行代码(代码为POS机返回)
							@IN_CARD_BANK_NAME NVARCHAR(20),  --刷卡卡号所属银行名称
							@IN_STATUS         NVARCHAR(20),  --刷卡状态
							@IN_STATUS_DESC    NVARCHAR(200), --刷卡状态说明
                            @IN_INPUT_MAN      INTEGER        --操作员

WITH ENCRYPTION 
AS
    DECLARE @V_LIST_ID INT
    DECLARE @IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)
    SET @IBUSI_FLAG=30204
    SET @SBUSI_NAME = '新增POS机刷卡明细'
    
    BEGIN TRY
    BEGIN TRANSACTION
		SELECT @V_LIST_ID=ISNULL(MAX(LIST_ID),0)+1 FROM TPOSCARDDETAIL WHERE PRODUCT_ID=@IN_PRODUCT_ID AND CUST_ID=@IN_CUST_ID
        INSERT INTO TPOSCARDDETAIL(PRODUCT_ID,CUST_ID,LIST_ID,AMOUNT,CARD_NO,INPUT_TIME,CARD_BANK_NO,CARD_BANK_NAME,STATUS,STATUS_DESC,INPUT_MAN)
            SELECT @IN_PRODUCT_ID,@IN_CUST_ID,@V_LIST_ID,@IN_AMOUNT,@IN_CARD_NO,GETDATE(),@IN_CARD_BANK_NO,@IN_CARD_BANK_NAME,@IN_STATUS,@IN_STATUS_DESC,@IN_INPUT_MAN
            
        SELECT @SSUMMARY = '客户ID：' + CAST(@IN_CUST_ID AS VARCHAR) + '，产品ID：' + CAST(@IN_PRODUCT_ID AS VARCHAR)
                          + '，刷卡金额：' + CAST(@IN_AMOUNT AS VARCHAR) + '，刷卡状态：' + @IN_STATUS_DESC
        INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
            VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
        
        COMMIT TRANSACTION
        RETURN 100
    END TRY
    
    BEGIN CATCH
        IF @@TRANCOUNT > 0 BEGIN
            ROLLBACK TRANSACTION
        END
        
        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)

        SELECT @V_ERROR_STR = N'Message:%s,Error:%d,Level:%d,State:%d,Procedure:%s,Line:%d',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()

        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_NUMBER,@V_ERROR_SEVERITY,@V_ERROR_STATE,
                  @V_ERROR_PROCEDURE,@V_ERROR_LINE)
        
        RETURN -100
    END CATCH
GO
