USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_MODI_TPOSTSEND  @IN_INPUT_DATE       INTEGER,	       --邮寄日期
									@IN_PRODUCT_ID       INTEGER,		   --产品ID
									@IN_CONTRACT_SUB_BH  NVARCHAR(200),    --合同编号
									@IN_POST_NO		     NVARCHAR(30),     --邮寄单号
									@IN_POST_CONTENT	 NVARCHAR(30),     --邮寄内容（1成立公告2确认单3管理报告4临时信息披露5终止公告6其他。可能有多个，则用|间隔）
									@IN_SUMMARY			 NVARCHAR(500),    --备注
									@IN_INPUT_MAN        INTEGER,          --操作员
									@IN_SERIAL_NO        INTEGER           --记录序号
WITH ENCRYPTION
AS
    SET NOCOUNT ON

    DECLARE @V_ERROR NVARCHAR(200), @IBUSI_FLAG INT, @SBUSI_NAME NVARCHAR(40), @SSUMMARY NVARCHAR(200)
    
    BEGIN TRY
    
    SET @IBUSI_FLAG = 30601
    SET @SBUSI_NAME = '修改邮寄信息'
    
    --校验输入数据
    IF ISNULL(@IN_POST_NO,'') = ''
	BEGIN
		SET @V_ERROR = N'没有填写邮寄单号'
		RAISERROR(@V_ERROR,16,3)
	END
    IF ISNULL(@IN_INPUT_DATE,0)=0
	BEGIN
		SET @V_ERROR = N'没有输入邮寄日期！'
		RAISERROR(@V_ERROR,16,1)
	END
	IF ISNULL(@IN_PRODUCT_ID,0) = 0
	BEGIN
		SET @V_ERROR = N'没有选择产品'
		RAISERROR(@V_ERROR,16,2)
	END
    IF ISNULL(@IN_CONTRACT_SUB_BH,'') = ''
	BEGIN
		SET @V_ERROR = N'没有指定合同'
		RAISERROR(@V_ERROR,16,3)
	END
	IF ISNULL(@IN_POST_CONTENT,'') = ''
	BEGIN
		SET @V_ERROR = N'没有选择邮寄内容'
		RAISERROR(@V_ERROR,16,3)
	END
    BEGIN TRANSACTION
    --
    UPDATE TPOSTSEND
		SET POST_NO = @IN_POST_NO,			--邮寄单号
		POST_CONTENT = @IN_POST_CONTENT,    --邮寄内容（1成立公告2确认单3管理报告4临时信息披露5终止公告6其他。可能有多个，则用|间隔） 
		SUMMARY = @IN_SUMMARY,				--备注 
		INPUT_MAN = @IN_INPUT_MAN,          --操作员
		INPUT_DATE=@IN_INPUT_DATE           --邮寄日期
        WHERE SERIAL_NO=@IN_SERIAL_NO
    IF @@ROWCOUNT=0 --如果邮寄表中没记录，则加上
		INSERT INTO TPOSTSEND(INPUT_DATE, PRODUCT_ID, CONTRACT_SUB_BH, POST_NO, POST_CONTENT, SUMMARY, INPUT_MAN)
			VALUES(@IN_INPUT_DATE,@IN_PRODUCT_ID,@IN_CONTRACT_SUB_BH,@IN_POST_NO,@IN_POST_CONTENT,@IN_SUMMARY,@IN_INPUT_MAN)
    
    --日志记录
    SET @SSUMMARY = @SBUSI_NAME + ',修改,邮寄单号:'+@IN_POST_NO
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    COMMIT TRANSACTION
    END TRY

    --3.异常处理
    BEGIN CATCH
        IF @@TRANCOUNT > 0
            ROLLBACK TRANSACTION
        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)
        SELECT @V_ERROR_STR = N'Message:%s<BR><font color = "white">Error:%d,Level:%d,State:%d,Procedure:%s,Line:%d</font>',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()
        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_NUMBER,@V_ERROR_SEVERITY,@V_ERROR_STATE,
                  @V_ERROR_PROCEDURE,@V_ERROR_LINE)
        RETURN -100
    END CATCH
    RETURN 100
GO
