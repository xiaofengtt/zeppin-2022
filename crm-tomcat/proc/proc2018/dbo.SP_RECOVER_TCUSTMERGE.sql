USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_RECOVER_TCUSTMERGE
							@IN_SERIAL_NO        INTEGER,      --记录序号
							@IN_INPUT_MAN        INTEGER       --操作员
WITH ENCRYPTION
AS
    SET NOCOUNT ON

    DECLARE @V_ERROR NVARCHAR(200), @IBUSI_FLAG INT, @SBUSI_NAME NVARCHAR(40), @SSUMMARY NVARCHAR(200)
    DECLARE @V_FROM_CUST_ID INT,@V_TO_CUST_ID INT,@V_SERIAL_NOS NVARCHAR(800),@V_COLUMN_NAME NVARCHAR(80)
    DECLARE @V_SQL NVARCHAR(800),@V_TABEL_NAME NVARCHAR(80),@V_FROM_CUST_NAME NVARCHAR(100)
    
    BEGIN TRY
    
    SET @IBUSI_FLAG = 30601
    SET @SBUSI_NAME = '客户信息合并后恢复'
    
    BEGIN TRANSACTION
   		SELECT @V_FROM_CUST_ID=FROM_CUST_ID, @V_TO_CUST_ID=TO_CUST_ID 
			FROM TCUSTMERGE WHERE SERIAL_NO=@IN_SERIAL_NO
		IF EXISTS (SELECT 1 FROM TCustomers WHERE CUST_ID=@V_TO_CUST_ID AND STATUS='112805')
		BEGIN
			SET @V_ERROR ='恢复到的客户['+CAST(@V_TO_CUST_ID AS VARCHAR)+']已删除，或者已被其他客户合并'
			RAISERROR(@V_ERROR,16,1)
		END
		DECLARE CUR22 CURSOR FOR SELECT TABEL_NAME,COLUMN_NAME,RECORD_ID FROM TCUSTMERGEDETAIL WHERE MERGE_SERIAL_NO=@IN_SERIAL_NO
		OPEN CUR22
		FETCH NEXT FROM CUR22 INTO @V_TABEL_NAME,@V_COLUMN_NAME,@V_SERIAL_NOS
		WHILE @@FETCH_STATUS=0
		BEGIN
			SET @V_SERIAL_NOS=SUBSTRING(@V_SERIAL_NOS,0,LEN(@V_SERIAL_NOS)) --把最后一个逗号去掉
			--特殊处理的几张表：INTRUST..TAsstActPostList,INTRUST..TAsstActTypeDetails,INTRUSTHistory..TAssActDetailYE
			IF @V_TABEL_NAME='INTRUST..TAsstActPostList'
			BEGIN
				SELECT @V_FROM_CUST_NAME=CUST_NAME FROM TCustomers WHERE CUST_ID=@V_FROM_CUST_ID
				SET @V_SQL='UPDATE INTRUST..TAsstActPostList SET AATDETAIL_NO='''+@V_COLUMN_NAME+''',AATDETAIL_NAME='''+@V_FROM_CUST_NAME+''' WHERE SERIAL_NO IN ('+@V_SERIAL_NOS+')'
			END
			ELSE IF @V_TABEL_NAME='INTRUST..TAsstActTypeDetails'
			BEGIN
				SELECT @V_FROM_CUST_NAME=CUST_NAME FROM TCustomers WHERE CUST_ID=@V_FROM_CUST_ID
				SET @V_SQL='UPDATE INTRUST..TAsstActTypeDetails SET AATDETAIL_NO='''+@V_COLUMN_NAME+''',AATDETAIL_NAME='''+@V_FROM_CUST_NAME+''' WHERE SERIAL_NO IN ('+@V_SERIAL_NOS+')'
			END
			ELSE IF @V_TABEL_NAME='INTRUSTHistory..TAssActDetailYE'
			BEGIN
				SELECT @V_FROM_CUST_NAME=CUST_NAME FROM TCustomers WHERE CUST_ID=@V_FROM_CUST_ID
				SET @V_SQL='UPDATE INTRUSTHistory..TAssActDetailYE SET AATDETAIL_NO='''+@V_COLUMN_NAME+''',AATDETAIL_NAME='''+@V_FROM_CUST_NAME+''' WHERE SERIAL_NO IN ('+@V_SERIAL_NOS+')'
			END
			ELSE IF @V_TABEL_NAME='TSUBSCRIBE'
			BEGIN
				SET @V_SQL='UPDATE TSUBSCRIBE SET CUST_ID='+CAST(@V_FROM_CUST_ID AS VARCHAR)+' WHERE SUBSCRIBE_ID IN ('+@V_SERIAL_NOS+')'
			END
			ELSE
			BEGIN
				SET @V_SQL='UPDATE '+@V_TABEL_NAME+' SET '+@V_COLUMN_NAME+'='+CAST(@V_FROM_CUST_ID AS VARCHAR)+' WHERE SERIAL_NO IN ('+@V_SERIAL_NOS+')'
			END
			EXEC (@V_SQL)
			FETCH NEXT FROM CUR22 INTO @V_TABEL_NAME,@V_COLUMN_NAME,@V_SERIAL_NOS
		END
		CLOSE CUR22
		DEALLOCATE CUR22
		
		/*--客户预约记录恢复
		SET @V_SERIAL_NOS='' --把待修改的记录号以逗号间隔的字符串保存
		SELECT @V_SERIAL_NOS=RECORD_ID,@V_COLUMN_NAME=COLUMN_NAME FROM TCUSTMERGEDETAIL WHERE MERGE_SERIAL_NO=@IN_SERIAL_NO AND TABEL_NAME='TPRECONTRACT' AND COLUMN_NAME='CUST_ID'
		IF @@ROWCOUNT>0
		BEGIN
			SET @V_SERIAL_NOS=SUBSTRING(@V_SERIAL_NOS,0,LEN(@V_SERIAL_NOS)) --把最后一个逗号去掉
			SET @V_SQL='UPDATE TPRECONTRACT SET CUST_ID='+CAST(@V_FROM_CUST_ID AS VARCHAR)+' WHERE SERIAL_NO IN ('+@V_SERIAL_NOS+')'
			EXEC (@V_SQL) --
			--RAISERROR('人为的test',16,1)
		END
		*/
		--恢复时，客户各字段信息合并后还原，此处未实现
		--
		--恢复时，源客户还原成正常客户
		UPDATE TCustomers SET STATUS = N'112803', STATUS_NAME = N'修改'  WHERE CUST_ID = @V_FROM_CUST_ID
		UPDATE INTRUST..TCUSTOMERINFO SET STATUS = N'112803', STATUS_NAME = N'修改'  WHERE CUST_ID = @V_FROM_CUST_ID
		UPDATE TCUSTMERGE SET CHECK_FLAG=11 WHERE SERIAL_NO=@IN_SERIAL_NO
		--恢复后，原来做过的合并明细，暂保留
	
    --日志记录
    SET @SSUMMARY = @SBUSI_NAME 
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    COMMIT TRANSACTION
    END TRY

    --异常处理
    BEGIN CATCH
        IF @@TRANCOUNT > 0
            ROLLBACK TRANSACTION
        IF EXISTS(SELECT * FROM MASTER.dbo.syscursors where cursor_name='CUR22')
        BEGIN
			CLOSE CUR22
			DEALLOCATE CUR22
        END
        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)
        SELECT @V_ERROR_STR = N'Message:%s<BR><font color = "white">Error:%d,Level:%d,State:%d,Procedure:%s,Line:%d</font>',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()
        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_NUMBER,@V_ERROR_SEVERITY,@V_ERROR_STATE,
                  @V_ERROR_PROCEDURE,@V_ERROR_LINE)
        RETURN -100
    END CATCH
    RETURN 100
GO
