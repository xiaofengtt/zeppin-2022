USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_CHECK_TBENCHANGES @IN_SERIAL_NO     INTEGER,
                                      @IN_CHECK_FLAG    INTEGER,
                                      @IN_SUMMARY       NVARCHAR(400),
                                      @IN_INPUT_MAN     INTEGER
WITH ENCRYPTION
AS
    DECLARE @V_TO_CUST_NAME NVARCHAR(80)
    DECLARE @V_PRODUCT_ID INT,@V_FROM_CUST_ID INT,@V_TO_CUST_ID INT,@V_CHECK_FLAG INT,@V_DF_REC_NO INT,
            @V_SUB_PRODUCT_ID INT,@V_PRODUCT_NAME NVARCHAR(60),@V_TRANS_TYPE NVARCHAR(10)
    DECLARE @V_TO_AMOUNT DECIMAL(16,3),@V_INPUT_MAN INT,@V_CONTRACT_BH NVARCHAR(16)
    DECLARE @V_FROM_LIST_ID INT,@V_TO_LIST_ID INT,@V_PRODUCT_CODE NVARCHAR(6),@V_CONTRACT_SUB_BH NVARCHAR(50)
    DECLARE @V_FROM_CUST_NAME NVARCHAR(60)
    DECLARE @V_RET_CODE INT,@IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)
    SET @IBUSI_FLAG = 21704
    SET @SBUSI_NAME = N'审核受益权变更'
    SET @SSUMMARY = N'审核受益权变更'

    BEGIN TRY
    BEGIN TRANSACTION

    SELECT @V_CHECK_FLAG = CHECK_FLAG,@V_PRODUCT_ID = PRODUCT_ID,@V_CONTRACT_BH = CONTRACT_BH,
           @V_FROM_CUST_ID = FROM_CUST_ID,@V_TO_AMOUNT = TO_AMOUNT,@V_TO_CUST_ID = TO_CUST_ID,
           @V_INPUT_MAN = INPUT_MAN,@V_TRANS_TYPE = TRANS_TYPE,
           @V_FROM_LIST_ID = FROM_LIST_ID, @V_TO_LIST_ID = TO_LIST_ID
        FROM INTRUST..TBENCHANGES WHERE SERIAL_NO = @IN_SERIAL_NO
    IF @@ROWCOUNT=0
		RAISERROR('记录不存在！',16,1) -- 记录不存在
    IF @V_CHECK_FLAG <>1 --1未审核4审核不通过
        RAISERROR('记录已审核！',16,1) -- 已审核
    IF EXISTS( SELECT 1 FROM INTRUST..TSYSCONTROL WHERE FLAG_TYPE = 'C2030302' AND VALUE = 1 ) AND @V_TRANS_TYPE NOT IN('181511','181512','181513','181514')
        IF @IN_INPUT_MAN = @V_INPUT_MAN
            RAISERROR('受益权转让: 复核人员不能与录入人员相同！',16,1)
    
    IF @IN_CHECK_FLAG IN (3,4) --3作废4审核不通过
    BEGIN
		UPDATE INTRUST..TBENCHANGES SET CHECK_FLAG=@IN_CHECK_FLAG,SUMMARY=@IN_SUMMARY,CHECK_TIME=GETDATE(),CHECK_MAN=@IN_INPUT_MAN WHERE SERIAL_NO=@IN_SERIAL_NO
    END
    ELSE IF @IN_CHECK_FLAG=2
    BEGIN
		--审核通过，调用业务系统中的审核过程
		EXEC INTRUST..SP_CHECK_TBENCHANGES @IN_SERIAL_NO,@IN_INPUT_MAN
    END
	ELSE
		RAISERROR('受益权转让: 审核操作异常：不存在的审核状态！',16,1)
    SELECT @V_PRODUCT_CODE = PRODUCT_CODE, @V_PRODUCT_NAME = PRODUCT_NAME FROM INTRUST..TPRODUCT WHERE PRODUCT_ID = @V_PRODUCT_ID
    SELECT @V_CONTRACT_SUB_BH = CONTRACT_SUB_BH FROM INTRUST..TCONTRACT WHERE PRODUCT_ID = @V_PRODUCT_ID AND CONTRACT_BH = @V_CONTRACT_BH
    
    SELECT @V_SUB_PRODUCT_ID = SUB_PRODUCT_ID
        FROM INTRUST..TBENIFITOR WHERE PRODUCT_ID = @V_PRODUCT_ID AND CONTRACT_BH = @V_CONTRACT_BH  AND LIST_ID = @V_FROM_LIST_ID
    
    SELECT @V_TO_CUST_NAME = CUST_NAME FROM TCustomers WHERE CUST_ID = @V_TO_CUST_ID
    SELECT @V_FROM_CUST_NAME = CUST_NAME FROM TCustomers WHERE CUST_ID = @V_FROM_CUST_ID
    SET @SSUMMARY = @SBUSI_NAME + N'，产品编号: ' + @V_PRODUCT_CODE + 
                                N'，产品名称: ' + @V_PRODUCT_NAME + 
                                N'，合同编号：' + @V_CONTRACT_SUB_BH + 
                                N'，受益序号：' + RTRIM(CONVERT(CHAR,@V_FROM_LIST_ID)) +
                                N'，转让人：'   + @V_FROM_CUST_NAME + 
                                N'，变更份额：' + RTRIM(CONVERT(CHAR,@V_TO_AMOUNT)) +
                                N'，受让人：' + ISNULL(@V_TO_CUST_NAME,'')

    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)

    COMMIT TRANSACTION
    END TRY

    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION 
        
        BEGIN
            DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                    @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)
            SELECT @V_ERROR_STR = N',Message:%s<BR>Error %d,Level %d,State %d,Procedure %s,Line %d',
                   @V_ERROR_NUMBER = ERROR_NUMBER(),
                   @V_ERROR_SEVERITY = ERROR_SEVERITY(),
                   @V_ERROR_STATE = ERROR_STATE(),
                   @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
                   @V_ERROR_LINE = ERROR_LINE(),
                   @V_ERROR_MESSAGE = ERROR_MESSAGE()

            RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_NUMBER,@V_ERROR_SEVERITY,
                      @V_ERROR_STATE,@V_ERROR_PROCEDURE,@V_ERROR_LINE)
            RETURN -100
        END

    END CATCH
    RETURN 100
GO
