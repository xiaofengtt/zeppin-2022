USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE SP2_CHECK_LOGIN2
                      @IN_LOGIN_USER          NVARCHAR(80),        --
                      @IN_PASSWORD            NVARCHAR(60),        --
                      @IN_IP_ADDRESS          NVARCHAR(60)
                      
WITH ENCRYPTION
AS
    DECLARE @V_PASSWORD NVARCHAR(40),@V_OP_CODE NVARCHAR(80)
    DECLARE @V_STATUS INT
    DECLARE @IBUSI_FLAG INT
    DECLARE @SBUSI_NAME NVARCHAR(40)
    DECLARE @SSUMMARY NVARCHAR(200)
    SELECT @IBUSI_FLAG = 10104
    SELECT @SBUSI_NAME = N'操作员APP登录'
    SELECT @SSUMMARY = N'操作员APP登录'
    BEGIN TRY
		IF ISNULL(@IN_LOGIN_USER,'')='' OR ISNULL(@IN_PASSWORD,'')=''
			RAISERROR('用户名或者密码不能为空',16,1)
		SELECT @V_STATUS=STATUS,@V_OP_CODE=OP_CODE FROM TOPERATOR WHERE LOGIN_USER=@IN_LOGIN_USER
		IF @@ROWCOUNT=0 OR @V_STATUS IS NULL
			RAISERROR('用户名或者密码不正确',16,1)
		IF NOT EXISTS(SELECT 1 FROM TOPERATOR WHERE LOGIN_USER=@IN_LOGIN_USER AND pwdcompare(@IN_PASSWORD,ENCRYPT_PASSWORD)=1 )
			RAISERROR('用户名或者密码不正确',16,1)
		IF @V_STATUS <> 1
			RAISERROR('本登录账号已冻结或注销，请联系管理员',16,1)
		
		BEGIN TRANSACTION

		UPDATE TOPERATOR SET LOGIN_TIME = CURRENT_TIMESTAMP, IP_ADDRESS = @IN_IP_ADDRESS WHERE LOGIN_USER=@IN_LOGIN_USER
		
		SELECT @SSUMMARY = N'操作员APP登录，操作员编号：' + CONVERT(NVARCHAR(16),@V_OP_CODE)
		INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
			VALUES(@IBUSI_FLAG,@SBUSI_NAME,@V_OP_CODE,@SSUMMARY)
		

    COMMIT TRANSACTION
    SELECT OP_CODE,OP_NAME,LOGIN_USER,EMAIL,O_TEL,MOBILE FROM TOPERATOR WHERE LOGIN_USER=@IN_LOGIN_USER
    END TRY

    --异常处理
    BEGIN CATCH
        IF @@TRANCOUNT > 0
            ROLLBACK TRANSACTION
        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)
        SELECT @V_ERROR_STR = N'Message:%s<BR><font color = "white">Error:%d,Level:%d,State:%d,Procedure:%s,Line:%d</font>',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()
        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_NUMBER,@V_ERROR_SEVERITY,@V_ERROR_STATE,
                  @V_ERROR_PROCEDURE,@V_ERROR_LINE)
        RETURN -100
    END CATCH
    
GO
