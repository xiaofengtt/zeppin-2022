USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_STAT_BJITIC_CONTRACT1 @IN_PRODUCT_NAME  NVARCHAR(100),  --产品名称
                                          @IN_DATE1         INT,
                                          @IN_DATE2         INT,
                                          @IN_INPUT_MAN     INT
WITH ENCRYPTION
AS
    SET NOCOUNT ON
    SET @IN_PRODUCT_NAME = ISNULL(@IN_PRODUCT_NAME,'')
    SET @IN_PRODUCT_NAME = RTRIM(LTRIM(@IN_PRODUCT_NAME))
    SET @IN_DATE1 = ISNULL(@IN_DATE1,0)
    SET @IN_DATE2 = ISNULL(@IN_DATE2,20991231)
    IF CAST (@IN_DATE2 AS INT) = 0
		SET @IN_DATE2 =20991231
	CREATE TABLE #TMP_STAT_BJITIC_CONTRACT1(
	    PRODUCT_ID          INT,
	    PRODUCT_NAME        NVARCHAR(60),
	    CUST_ID             INT,
	    CUST_NAME           NVARCHAR(100),
	    CONTRACT_BH         NVARCHAR(20),
	    CONTRACT_SUB_BH     NVARCHAR(60),
	    DZ_DATE             INT,
	    CONTRACT_INPUT_TIME INT,
	    SERVICE_MAN         INT,
	    OP_NAME             NVARCHAR(30)
	)
    --满足条件的认购数据，及相关要素
    INSERT INTO #TMP_STAT_BJITIC_CONTRACT1(PRODUCT_ID,PRODUCT_NAME,CUST_ID,CUST_NAME,CONTRACT_BH,CONTRACT_SUB_BH,DZ_DATE,CONTRACT_INPUT_TIME,SERVICE_MAN,OP_NAME)
        SELECT A.PRODUCT_ID, A.PRODUCT_NAME, A.CUST_ID, B.CUST_NAME, A.CONTRACT_BH, A.CONTRACT_SUB_BH, null AS DZ_DATE, 
               dbo.GETDATEINT(A.INPUT_TIME) AS CONTRACT_INPUT_TIME, B.SERVICE_MAN, O.OP_NAME
            FROM INTRUST..TCONTRACT A, EFCRM..TCustomers B LEFT JOIN EFCRM..TOPERATOR O ON B.SERVICE_MAN = O.OP_CODE
            WHERE A.CUST_ID = B.CUST_ID AND A.HT_STATUS NOT IN ('120104','120105') --AND Intrust_flag1 = 2
                AND A.PRODUCT_NAME LIKE '%'+@IN_PRODUCT_NAME+'%' 
    --根据CRM预约到账信息更新到账日期
    UPDATE #TMP_STAT_BJITIC_CONTRACT1
        SET DZ_DATE = B.JK_DATE
        FROM #TMP_STAT_BJITIC_CONTRACT1 A, INTRUST..TCONTRACT B, INTRUST..TPRECONTRACT C, EFCRM..TPRECONTRACT D
        WHERE A.PRODUCT_ID = B.PRODUCT_ID AND A.CONTRACT_BH = B.CONTRACT_BH
		    AND B.PRE_CODE = D.PRE_CODE AND B.PRODUCT_ID = C.PRODUCT_ID AND C.SERIAL_NO = D.BIND_SERIAL_NO
    --无CRM预约到账数据的认购数据，取认购合同之缴款日期作为到账日期
    UPDATE #TMP_STAT_BJITIC_CONTRACT1
        SET DZ_DATE = B.JK_DATE
        FROM #TMP_STAT_BJITIC_CONTRACT1 A, INTRUST..TCONTRACT B
        WHERE A.DZ_DATE is null AND A.PRODUCT_ID = B.PRODUCT_ID AND A.CONTRACT_BH = B.CONTRACT_BH
    --插入CRM预约到账数据（未录入认购的，注：如果不是通过“现场签约”功能转签约的，而是直接录入认购的，无法判断出两边的对应关系，在这种情况下，结果记录看起来是未录入认购的但实际上已经录入了）
    --增加预约表中的数据:未绑定到INTRUST..TPRECONTRACT表中的记录,或者绑定值有而未对应上INTRUST..TPRECONTRACT表中的记录
	INSERT INTO #TMP_STAT_BJITIC_CONTRACT1(PRODUCT_ID,PRODUCT_NAME,CUST_ID,CUST_NAME,CONTRACT_BH,CONTRACT_SUB_BH,DZ_DATE,CONTRACT_INPUT_TIME,SERVICE_MAN,OP_NAME)
        SELECT ISNULL(B.BIND_PRODUCT_ID,0), ISNULL(C.PRODUCT_NAME,B.PREPRODUCT_NAME), A.CUST_ID, D.CUST_NAME, '' AS CONTRACT_BH, '' AS CONTRACT_SUB_BH, 
               null AS DZ_DATE, null AS CONTRACT_INPUT_TIME, D.SERVICE_MAN, O.OP_NAME
        FROM EFCRM..TPRECONTRACT A, EFCRM..TPREPRODUCT B LEFT JOIN INTRUST..TPRODUCT C ON B.BIND_PRODUCT_ID = C.PRODUCT_ID,
             EFCRM..TCustomers D LEFT JOIN EFCRM..TOPERATOR O ON D.SERVICE_MAN = O.OP_CODE
        WHERE A.PREPRODUCT_ID = B.PREPRODUCT_ID AND A.CUST_ID = D.CUST_ID AND A.PRE_STATUS = '111301'
            AND (A.BIND_SERIAL_NO IS NULL OR NOT EXISTS(SELECT 1 FROM INTRUST..TPRECONTRACT Z WHERE A.BIND_SERIAL_NO = Z.SERIAL_NO) )
            AND B.PREPRODUCT_NAME LIKE '%'+@IN_PRODUCT_NAME+'%'

    --返回结果
    SELECT *, EFCRM.dbo.GETDATEDIFF(DZ_DATE,CONTRACT_INPUT_TIME) AS DAYS
        FROM #TMP_STAT_BJITIC_CONTRACT1
        WHERE (DZ_DATE BETWEEN @IN_DATE1 AND @IN_DATE2) OR (CONTRACT_INPUT_TIME BETWEEN @IN_DATE1 AND @IN_DATE2) OR (DZ_DATE is null AND CONTRACT_INPUT_TIME is null)
        ORDER BY PRODUCT_ID
    RETURN @@ROWCOUNT
GO
