USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_ADD_TCUSTMERGE
							@IN_FROM_CUST_ID     INTEGER,      --源客户ID
							@IN_TO_CUST_ID       INTEGER,      --目的客户ID
							@IN_INPUT_MAN        INTEGER       --操作员
WITH ENCRYPTION
AS
    SET NOCOUNT ON
    DECLARE @V_ERROR NVARCHAR(200), @IBUSI_FLAG INT, @SBUSI_NAME NVARCHAR(40), @SSUMMARY NVARCHAR(200)
    
    BEGIN TRY    
    SET @IBUSI_FLAG = 30601
    SET @SBUSI_NAME = '新增客户信息合并'
    
    --校验输入数据
    IF NOT EXISTS (SELECT 1 FROM TCustomers WHERE CUST_ID=@IN_FROM_CUST_ID)
	BEGIN
		SET @V_ERROR = N'源客户不存在！'
		RAISERROR(@V_ERROR,16,1)
	END
	IF NOT EXISTS (SELECT 1 FROM TCustomers WHERE CUST_ID=@IN_TO_CUST_ID)
	BEGIN
		SET @V_ERROR = N'目的客户不存在！'
		RAISERROR(@V_ERROR,16,1)
	END
    
    IF EXISTS (SELECT * FROM TCUSTMERGE WHERE FROM_CUST_ID=@IN_FROM_CUST_ID AND TO_CUST_ID=@IN_TO_CUST_ID AND CHECK_FLAG=1)
	BEGIN
		SET @V_ERROR = N'已存在待审核的相同的合并记录！'
		RAISERROR(@V_ERROR,16,1)
	END
	
    BEGIN TRANSACTION
    
    --
    INSERT INTO TCUSTMERGE(FROM_CUST_ID, TO_CUST_ID, INPUT_MAN, INPUT_TIME, CHECK_FLAG)
        VALUES(@IN_FROM_CUST_ID,@IN_TO_CUST_ID,@IN_INPUT_MAN,GETDATE(), 1)
    
    --日志记录
    SET @SSUMMARY = @SBUSI_NAME + ',新增,从客户ID['+CAST(@IN_FROM_CUST_ID AS VARCHAR)+']合并到客户['+CAST(@IN_TO_CUST_ID AS VARCHAR)+']'
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    COMMIT TRANSACTION
    END TRY

    --异常处理
    BEGIN CATCH
        IF @@TRANCOUNT > 0
            ROLLBACK TRANSACTION
        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)
        SELECT @V_ERROR_STR = N'Message:%s<BR><font color = "white">Error:%d,Level:%d,State:%d,Procedure:%s,Line:%d</font>',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()
        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_NUMBER,@V_ERROR_SEVERITY,@V_ERROR_STATE,
                  @V_ERROR_PROCEDURE,@V_ERROR_LINE)
        RETURN -100
    END CATCH
    RETURN 100
GO
