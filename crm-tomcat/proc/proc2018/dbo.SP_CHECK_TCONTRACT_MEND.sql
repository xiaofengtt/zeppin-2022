USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_CHECK_TCONTRACT_MEND @IN_SERIAL_NO     INTEGER,      --TCONTRACT.SERIAL_NO
                                         @IN_CHECK_FLAG    INTEGER,      --审核标记：2通过；其他不通过
                                         @IN_CHECK_SUMMARY NVARCHAR(400),--审核说明
                                         @IN_INPUT_MAN     INTEGER       --操作员
WITH ENCRYPTION
AS
    DECLARE @V_PRODUCT_ID INTEGER,@V_PRODUCT_CODE NVARCHAR(6),@V_PRODUCT_NAME NVARCHAR(120),@V_CONTRACT_BH NVARCHAR(16),@V_CONTRACT_SUB_BH NVARCHAR(120)
    DECLARE @V_RET_CODE INT,@IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)
    DECLARE @V_SUB_PRODUCT_ID INT,@V_CUST_ID INT
    
    SET @V_RET_CODE = -20809000
    SET @IBUSI_FLAG = 20809
    SET @SBUSI_NAME = N'认购合同补录审核'
	
    BEGIN TRY
        BEGIN TRANSACTION
        SELECT @V_PRODUCT_ID =PRODUCT_ID,@V_PRODUCT_CODE = PRODUCT_CODE,@V_PRODUCT_NAME = PRODUCT_NAME,@V_CONTRACT_BH = CONTRACT_BH
				,@V_CONTRACT_SUB_BH = CONTRACT_SUB_BH,@V_SUB_PRODUCT_ID=SUB_PRODUCT_ID,@V_CUST_ID=CUST_ID
            FROM INTRUST..TCONTRACT WHERE SERIAL_NO = @IN_SERIAL_NO
        
        IF @IN_CHECK_FLAG<>2
        BEGIN
			UPDATE INTRUST..TCONTRACT 
				SET MEND_CHECK_FLAG = 1,    --审核不通过时，返回未审核状态
					CHECK_SUMMARY = @IN_CHECK_SUMMARY,
					CHECK_MAN = @IN_INPUT_MAN
				WHERE SERIAL_NO = @IN_SERIAL_NO
			--退回到待合同信息补全
			UPDATE TPRECONTRACT SET FLOW_STATUS=6 WHERE PRODUCT_ID=@V_PRODUCT_ID AND SUB_PRODUCT_ID=@V_SUB_PRODUCT_ID AND CUST_ID=@V_CUST_ID
        END
        ELSE
        BEGIN
			--审核通过时，调用业务系统的审核过程
			EXEC INTRUST..SP_CHECK_TCONTRACT_MEND @IN_SERIAL_NO,@IN_INPUT_MAN
        END
        --日志记录
        SELECT @SSUMMARY = @SBUSI_NAME + N'，产品编号：' + @V_PRODUCT_CODE
                                       + N'，产品名称：' + @V_PRODUCT_NAME 
                                       + N'，合同序号：' + @V_CONTRACT_BH
                                       + N'，合同编号：' + @V_CONTRACT_SUB_BH
                                        
        INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
            VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
        
        COMMIT TRANSACTION
    END TRY
    
    BEGIN CATCH
        IF @@TRANCOUNT > 0 BEGIN
            ROLLBACK TRANSACTION
        END

        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)

        SELECT @V_ERROR_STR = N'Message:%s<br><font color ="white">Error:%d,Level:%d,State:%d,Procedure:%s,Line:%d</font>',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()

        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_NUMBER,@V_ERROR_SEVERITY,@V_ERROR_STATE,
                  @V_ERROR_PROCEDURE,@V_ERROR_LINE)

        RETURN -100
    END CATCH
    
    RETURN 100
GO
