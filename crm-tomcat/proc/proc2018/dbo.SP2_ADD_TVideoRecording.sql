USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
--  PROCEDURE SP2_ADD_TVideoRecording
--select * from TVideoRecording
-- SP2_ADD_TVideoRecording 'kljd','',0,40221,0,888--
CREATE PROCEDURE SP2_ADD_TVideoRecording
                      @IN_SAVENAME            NVARCHAR(400),--保存文件名URL
                      @IN_ORIGINNAME          NVARCHAR(400),--真实文件名
                      @IN_PRECONTRACT_ID      INT,        --预约ID
                      @IN_CONTRACT_ID         INT,        --合同ID
                      @IN_VTYPE               INT,        --1预约2签约
                      @IN_INPUT_MAN           INT         --操作员
                      
WITH ENCRYPTION
AS
    
    DECLARE @IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)
    DECLARE @V_PREPRODUCT_ID INT,@V_PRE_STATUS NVARCHAR(20),@V_INPUTMANNAME NVARCHAR(60)
    DECLARE @V_PRODUCT_ID INT,@V_SUB_PRODUCT_ID INT,@V_HT_STATUS NVARCHAR(20),@V_CheckFlag INT
    SELECT @SBUSI_NAME = N'保存双录信息'
    SELECT @SSUMMARY = N'保存双录信息'
    SELECT @IBUSI_FLAG = 30243
    
    --默认为签约
    IF ISNULL(@IN_VTYPE,0)<>1
		SET @IN_VTYPE=2
	BEGIN TRY
    BEGIN TRANSACTION
    
    IF ISNULL(@IN_SAVENAME,'')=''
		RAISERROR('未指定保存文件名URL',16,1)
    --预约
    IF @IN_VTYPE=1
    BEGIN
		SELECT @V_PREPRODUCT_ID=PREPRODUCT_ID,@V_PRE_STATUS=PRE_STATUS FROM TPRECONTRACT WHERE SERIAL_NO=@IN_PRECONTRACT_ID
		IF @@ROWCOUNT=0
			RAISERROR('预约信息不存在',16,1)
		IF @V_PRE_STATUS='111304'
			RAISERROR('预约信息已作废',16,1)
		IF @V_PRE_STATUS='111305'
			RAISERROR('预约信息已退款',16,1)
		SELECT @V_CheckFlag=CheckFlag FROM TVideoRecording WHERE PreContractID=@IN_PRECONTRACT_ID
		IF ISNULL(@V_CheckFlag,0)=2
			RAISERROR('预约的双录已审核通过，不能再次上传',16,1)
		SELECT @V_INPUTMANNAME=OP_NAME FROM TOPERATOR WHERE OP_CODE=@IN_INPUT_MAN
		UPDATE TVideoRecording SET SaveName=@IN_SAVENAME,OriginName=@IN_ORIGINNAME,InputTime=GETDATE()
			,InputMan=@IN_INPUT_MAN,InputManName=@V_INPUTMANNAME,CheckFlag=1 WHERE PreContractID=@IN_PRECONTRACT_ID AND VType=@IN_VTYPE
		IF @@ROWCOUNT=0
		BEGIN
			INSERT INTO TVideoRecording(ProductID,PreProductID,SubProductID,SaveName,OriginName,PreContractID,ContractID,VType
					,InputTime,InputMan,InputManName,CheckFlag)
				SELECT 0,@V_PREPRODUCT_ID,0,@IN_SAVENAME,@IN_ORIGINNAME,@IN_PRECONTRACT_ID,0,@IN_VTYPE
					,GETDATE(),@IN_INPUT_MAN,@V_INPUTMANNAME,1
		END
    END
    ELSE --签约
	BEGIN
		SELECT @V_PRODUCT_ID=PRODUCT_ID,@V_SUB_PRODUCT_ID=SUB_PRODUCT_ID,@V_HT_STATUS=HT_STATUS FROM INTRUST..TCONTRACT WHERE SERIAL_NO=@IN_CONTRACT_ID
		IF @@ROWCOUNT=0
			RAISERROR('合同信息不存在',16,1)
		IF @V_PRE_STATUS='120104'
			RAISERROR('合同已中止',16,1)
		IF @V_PRE_STATUS='120105'
			RAISERROR('合同已到期结算',16,1)
		SELECT @V_CheckFlag=CheckFlag FROM TVideoRecording WHERE ContractID=@IN_CONTRACT_ID
		IF ISNULL(@V_CheckFlag,0)=2
			RAISERROR('合同的双录已审核通过，不能再次上传',16,1)
		SELECT @V_INPUTMANNAME=OP_NAME FROM TOPERATOR WHERE OP_CODE=@IN_INPUT_MAN
		UPDATE TVideoRecording SET SaveName=@IN_SAVENAME,OriginName=@IN_ORIGINNAME,InputTime=GETDATE()
			,InputMan=@IN_INPUT_MAN,InputManName=@V_INPUTMANNAME,CheckFlag=1 WHERE ContractID=@IN_CONTRACT_ID AND VType=@IN_VTYPE
		IF @@ROWCOUNT=0
		BEGIN
			INSERT INTO TVideoRecording(ProductID,PreProductID,SubProductID,SaveName,OriginName,PreContractID,ContractID,VType
					,InputTime,InputMan,InputManName,CheckFlag)
				SELECT @V_PRODUCT_ID,0,@V_SUB_PRODUCT_ID,@IN_SAVENAME,@IN_ORIGINNAME,0,@IN_CONTRACT_ID,@IN_VTYPE
					,GETDATE(),@IN_INPUT_MAN,@V_INPUTMANNAME,1
		END
	END
	    
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    
    COMMIT TRANSACTION
    END TRY

    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION

        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)

        SELECT @V_ERROR_STR = N'Message:%s,Procedure:%s,Line:%d',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()

        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_PROCEDURE,@V_ERROR_LINE)

        RETURN -100
    END CATCH
    RETURN 100
GO
