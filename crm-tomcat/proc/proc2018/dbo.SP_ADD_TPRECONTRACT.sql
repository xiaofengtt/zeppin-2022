USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_ADD_TPRECONTRACT @IN_PREPRODUCT_ID  INT,                    --预发行产品ID
                                     @IN_CUST_ID        INT,                    --客户ID
                                     @IN_PRE_MONEY      DECIMAL(16,3),          --预约金额
                                     @IN_LINK_MAN       INT,                    --销售人员（联系人）
                                     @IN_VALID_DAYS     INT,                    --预约有效天数
                                     @IN_PRE_TYPE       NVARCHAR(10),           --预约方式(1112)
                                     @IN_SUMMARY        NVARCHAR(200),
                                     @IN_PRE_NUM        INT,                    --预约份数
                                     @IN_INPUT_MAN      INT,
                                     @IN_PRE_DATE       INT,                    --预约日期
                                     @IN_EXP_REG_DATE   INT = NULL,             --预计认购日期
                                     @IN_CUST_SOURCE    NVARCHAR(10) = NULL,    --客户来源(1110)
                                     @OUT_PRE_CODE      NVARCHAR(16) = NULL OUTPUT , --预约编号
                                     @OUT_SERIAL_NO     INT = NULL OUTPUT,
                                     @IN_CHANNEL_TYPE   NVARCHAR(10) = NULL,    --渠道类别(5500)
                                     @IN_CHANNEL_FARE   DEC(16,3) = NULL,       --渠道费用
                                     @IN_PRE_LEVEL      NVARCHAR(10) = NULL,    --预约类别(1182)用于区分预约的级别
                                     @IN_PRODUCT_ID     INT=0,                  --正式产品ID
                                     @IN_SUB_PRODUCT_ID INT = 0,                --子产品ID
                                     @IN_TEAM_ID        INT=0                   --团队ID，为了适应一个操作员加入多个团队的情况，必需指明团队
WITH ENCRYPTION
AS
    SET NOCOUNT ON
    DECLARE @V_ERROR NVARCHAR(200),@V_CUST_STATUS NVARCHAR(40)
    DECLARE @V_RET_CODE INT,@IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)
    SELECT @V_RET_CODE = -20701000
    SELECT @IBUSI_FLAG = 20701
    SELECT @SBUSI_NAME = N'客户预约销售'
    SELECT @SSUMMARY = N'客户预约销售'

    DECLARE @V_END_DATE INT, @V_PRE_CODE_MAX NVARCHAR(16),@V_NUM_MAX INT,@V_CUST_TYPE INT
    DECLARE @V_PRE_TYPE_NAME NVARCHAR(30),@V_PRE_STATUS NVARCHAR(10),@V_PRE_STATUS_NAME NVARCHAR(30)
    DECLARE @V_PREPRODUCT_NAME NVARCHAR(100),@V_NUM INT,@V_CUST_SOURCE_NAME NVARCHAR(30), @V_PRE_LEVEL_NAME NVARCHAR(30)
    DECLARE @V_NEED_CHECK INT,@V_PRODUCT_STATUS VARCHAR(20)
    DECLARE @OLD_SERVICE_MAN INT,@V_CUST_NAME NVARCHAR(200)
    DECLARE @V_PRE_MAX_NUM INT,@V_FACT_PRE_NUM INT,@V_FACT_PRE_MONEY DECIMAL(18,3),@V_PRE_MAX_MONEY DECIMAL(18,3), @V_FPRG_FLAG INT, @V_OPEN_FLAG INT
    DECLARE @V_PRE_START_DATE INT,@V_PRE_END_DATE INT,@V_NOW INT,@V_PRE_START_TIME DATETIME,@V_PRE_END_TIME DATETIME
    DECLARE @V_QUOTA_MONEY DECIMAL(16,3),@V_QUOTA_NUM INT,@V_TEAM_ID INT
    DECLARE @V_PREPRODUCT_STATUS VARCHAR(40),@V_USER_ID INT,@V_PRODUCT_ID INT,@V_PRE_MIN_AMOUNT INT,@V_MSG NVARCHAR(200)
    DECLARE @V_MORE_THEN_FULL INT,@V_PRODUCT_MONEY DECIMAL(18,3),@V_PRE_MONEY_TOTAL DECIMAL(18,3)
    DECLARE @V_PRE_MONEY_WAIT DECIMAL(18,3),@V_PRE_MONEY DECIMAL(18,3),@V_SMALL_DEFINE INT
    ------------------------------------------------------------------------
    SET @IN_PREPRODUCT_ID=ISNULL(@IN_PREPRODUCT_ID,0)
    SET @IN_PRODUCT_ID =ISNULL(@IN_PRODUCT_ID,0)
    SET @IN_SUB_PRODUCT_ID =ISNULL(@IN_SUB_PRODUCT_ID,0)
    SELECT @V_USER_ID=USER_ID FROM TSYSTEMINFO
    IF ISNULL(@IN_PRE_TYPE,'')='' SET @IN_PRE_TYPE='111203' --默认预约方式为"现场预约"
    
    BEGIN TRY
	SELECT @V_MORE_THEN_FULL=VALUE FROM TSYSCONTROL WHERE FLAG_TYPE='PRECONTRACT_MORE_THEN_FULL'--1能预约;其他不能预约
	SET @V_MORE_THEN_FULL=ISNULL(@V_MORE_THEN_FULL,0)
	SELECT @V_SMALL_DEFINE=VALUE FROM TSYSCONTROL WHERE FLAG_TYPE='SMALL_DEFINE'--1小于300万(不含)的自然人;2小于300万(不含)的自然人和机构
    SET @V_SMALL_DEFINE=ISNULL(@V_SMALL_DEFINE,1)
    --1.业务逻辑与操作
    --校验
    SELECT @V_CUST_STATUS=STATUS FROM TCustomers WHERE CUST_ID = @IN_CUST_ID-- AND STATUS <> '112805'/*删除*/ )
    IF @@ROWCOUNT=0 OR @V_CUST_STATUS='112805'/*删除*/
        RAISERROR(N'客户不存在！',16,3)
    IF @V_CUST_STATUS='112806'/*禁购*/
		RAISERROR(N'禁购客户！不能做预约',16,3)
    IF @IN_PRE_MONEY IS NULL OR @IN_PRE_MONEY <= 0
        RAISERROR(N'预约金额应大于0！',16,3)
    IF @IN_PRE_MONEY - ROUND(@IN_PRE_MONEY,0) > 0
        RAISERROR(N'预约金额不能是小数！',16,3)

    IF @IN_PREPRODUCT_ID>0 
    BEGIN
        SELECT @V_PREPRODUCT_NAME=PREPRODUCT_NAME,@V_PREPRODUCT_STATUS=STATUS,@V_PRODUCT_ID=BIND_PRODUCT_ID FROM TPREPRODUCT WHERE PREPRODUCT_ID = @IN_PREPRODUCT_ID
		IF @@ROWCOUNT=0
            RAISERROR(N'产品编号不存在！',16,3)
        IF @V_PREPRODUCT_STATUS <> '110202'
            RAISERROR(N'产品不在推介期不能预约！',16,3)
        --IF EXISTS (SELECT * FROM TPRECONTRACT WHERE PREPRODUCT_ID=@IN_PREPRODUCT_ID AND CUST_ID=@IN_CUST_ID AND PRE_STATUS<>'111304')
		--	RAISERROR(N'客户已预约过本产品，请对原预约进行操作！',16,3)
		--取得本产品已预约总额
		SELECT @V_PRE_MONEY_TOTAL=SUM(PRE_MONEY) FROM TPRECONTRACT WHERE PREPRODUCT_ID=@IN_PREPRODUCT_ID AND PRE_STATUS NOT IN ('111303','111304')
    END  
    ELSE --IF @IN_PRODUCT_ID>0
    BEGIN
		SET @V_PRODUCT_ID=@IN_PRODUCT_ID
        --IF EXISTS (SELECT * FROM TPRECONTRACT WHERE PRODUCT_ID=@IN_PRODUCT_ID AND SUB_PRODUCT_ID=@IN_SUB_PRODUCT_ID AND CUST_ID=@IN_CUST_ID AND PRE_STATUS<>'111304')
		--	RAISERROR(N'客户已预约过本产品，请对原预约进行操作！',16,3)
		--不是子产品
        IF @IN_SUB_PRODUCT_ID=0 
        BEGIN
			SELECT @V_PRE_START_TIME=PRE_START_TIME,@V_PRE_END_TIME=PRE_END_TIME,@V_PRE_MIN_AMOUNT=PRE_MIN_AMOUNT FROM TPRODUCT WHERE PRODUCT_ID = @IN_PRODUCT_ID
			SELECT @V_PRODUCT_STATUS=PRODUCT_STATUS, @V_PREPRODUCT_NAME = PRODUCT_NAME,@V_FACT_PRE_NUM=ISNULL(FACT_PRE_NUM,0), @V_PRE_MAX_NUM= ISNULL(PRE_MAX_NUM,0)
				,@V_FACT_PRE_MONEY=ISNULL(FACT_PRE_MONEY,0),@V_PRE_MAX_MONEY=ISNULL(PRE_MAX_MONEY,0), @V_PRE_START_DATE=PRE_START_DATE, @V_PRE_END_DATE=PRE_END_DATE
				,@V_FPRG_FLAG=FPRG_FLAG,@V_OPEN_FLAG=OPEN_FLAG,@V_PRODUCT_MONEY=PRE_MONEY
				FROM INTRUST..TPRODUCT A WHERE PRODUCT_ID = @IN_PRODUCT_ID
			IF @@ROWCOUNT=0
				RAISERROR(N'产品不存在！',16,3)
			IF NOT (@V_PRODUCT_STATUS='110202' OR (@V_PRODUCT_STATUS='110203' AND (@V_FPRG_FLAG=1 OR @V_OPEN_FLAG=1)))
				RAISERROR(N'产品不在推介期不能预约！',16,3)
			IF @V_MORE_THEN_FULL<>1
			BEGIN
				IF @V_PRE_MAX_NUM<>0 AND @V_FACT_PRE_NUM + @IN_PRE_NUM> @V_PRE_MAX_NUM
					RAISERROR(N'预约份数已满不能再预约！',16,3)
				IF @V_PRE_MAX_MONEY<>0 AND @V_FACT_PRE_MONEY + @IN_PRE_MONEY> @V_PRE_MAX_MONEY
					RAISERROR(N'预约金额已满不能再预约！',16,3)
			END
			IF @V_PRE_START_DATE>@V_NOW
				RAISERROR('预约日期还没到，暂不开放预约',16,1)
			ELSE IF @V_PRE_START_DATE=@V_NOW AND (dbo.GetDateTime(@V_NOW)+ISNULL(@V_PRE_START_TIME,'00:00'))>GETDATE()
				RAISERROR('预约时间还没到，请稍候',16,1)
			IF @V_PRE_END_DATE<@V_NOW OR (@V_PRE_END_DATE=@V_NOW AND (dbo.GetDateTime(@V_NOW)+ISNULL(@V_PRE_START_TIME,'23:59'))<GETDATE())
				RAISERROR('预约时间已结束',16,1)
			IF ISNULL(@V_PRE_MIN_AMOUNT,0)>0 AND @V_PRE_MIN_AMOUNT>@IN_PRE_MONEY
			BEGIN
				SET @V_MSG='预约金额必需大于最低预约金额'+CAST(@V_PRE_MIN_AMOUNT AS VARCHAR)
				RAISERROR(@V_MSG,16,1)
			END
        END
        ELSE --IF @IN_SUB_PRODUCT_ID<>0 --SELECT * FROM INTRUST..TSUBPRODUCT
        BEGIN --子产品
			SELECT @V_PREPRODUCT_NAME=PRODUCT_NAME FROM INTRUST..TPRODUCT WHERE PRODUCT_ID = @IN_PRODUCT_ID
			SET @V_PREPRODUCT_NAME = ISNULL(@V_PREPRODUCT_NAME,'')
			SELECT @V_PRE_START_TIME=PRE_START_TIME,@V_PRE_END_TIME=PRE_END_TIME,@V_PRE_MIN_AMOUNT=PRE_MIN_AMOUNT
			  FROM TSUBPRODUCT 
			  WHERE SUB_PRODUCT_ID=@IN_SUB_PRODUCT_ID
			SELECT @V_PRODUCT_STATUS=SUB_PRODUCT_STATUS, @V_PREPRODUCT_NAME = @V_PREPRODUCT_NAME+'['+LIST_NAME+']',@V_FACT_PRE_NUM=ISNULL(PRE_NUM,0)
				,@V_FACT_PRE_MONEY=ISNULL(PRE_MONEY,0), @V_PRE_START_DATE=PRE_START_DATE, @V_PRE_END_DATE=PRE_END_DATE,@V_PRODUCT_MONEY=PRE_MONEY
			  FROM INTRUST..TSUBPRODUCT 
			  WHERE SUB_PRODUCT_ID=@IN_SUB_PRODUCT_ID
			IF @@ROWCOUNT=0
				RAISERROR(N'子产品不存在！',16,3)
			IF @V_PRODUCT_STATUS<> '110202'
				RAISERROR(N'产品不在推介期不能预约！',16,3)
			IF @V_PRE_START_DATE>@V_NOW
				RAISERROR('预约日期还没到，暂不开放预约',16,1)
			ELSE IF @V_PRE_START_DATE=@V_NOW AND (dbo.GetDateTime(@V_NOW)+ISNULL(@V_PRE_START_TIME,'00:00'))>GETDATE()
				RAISERROR('预约时间还没到，请稍候',16,1)
			IF @V_PRE_END_DATE<@V_NOW OR (@V_PRE_END_DATE=@V_NOW AND (dbo.GetDateTime(@V_NOW)+ISNULL(@V_PRE_START_TIME,'23:59'))<GETDATE())
				RAISERROR('预约时间已结束',16,1)
			IF ISNULL(@V_PRE_MIN_AMOUNT,0)>0 AND @V_PRE_MIN_AMOUNT>@IN_PRE_MONEY
			BEGIN
				SET @V_MSG='预约金额必需大于最低预约金额'+CAST(@V_PRE_MIN_AMOUNT AS VARCHAR)
				RAISERROR(@V_MSG,16,1)
			END
        END
        --取得本产品已预约总额
		SELECT @V_PRE_MONEY_TOTAL=SUM(PRE_MONEY) FROM TPRECONTRACT WHERE PRODUCT_ID=@IN_PRODUCT_ID AND SUB_PRODUCT_ID=@IN_SUB_PRODUCT_ID AND PRE_STATUS NOT IN ('111303','111304')
    END
    
    SELECT @V_CUST_TYPE = CUST_TYPE,@OLD_SERVICE_MAN=SERVICE_MAN,@V_CUST_NAME=CUST_NAME FROM TCUSTOMERS WHERE CUST_ID = @IN_CUST_ID 
    DECLARE @V_NEED_QUOTA INT
    SELECT @V_NEED_QUOTA=VALUE FROM TSYSCONTROL WHERE FLAG_TYPE='NEED_QUOTA'
    IF @V_MORE_THEN_FULL<>1/*预约额满后不能预约*/ AND (@V_NEED_QUOTA=1/*需要设置销售配额*/ OR EXISTS (SELECT * FROM TTEAMQUOTA WHERE ISNULL(PREPRODUCT_ID,0)=@IN_PREPRODUCT_ID AND PRODUCT_ID=@IN_PRODUCT_ID AND ISNULL(SUB_PRODUCT_ID,0)=ISNULL(@IN_SUB_PRODUCT_ID,0))/*设置过配额*/)
    BEGIN
		--判断是否超配额
		SELECT @V_QUOTA_MONEY=ISNULL(QUOTAMONEY,0)-ISNULL(PRE_SALEMONEY,0),@V_QUOTA_NUM=ISNULL(QUOTA_QUALIFIED_NUM,0)-ISNULL(PRE_QUALIFIEDNUM,0) FROM TPERSONALQUOTA WHERE ((@IN_PRODUCT_ID>0 AND PRODUCT_ID=@IN_PRODUCT_ID) OR (@IN_PREPRODUCT_ID>0 AND PREPRODUCT_ID=@IN_PREPRODUCT_ID)) AND SUB_PRODUCT_ID=@IN_SUB_PRODUCT_ID AND OP_CODE=@IN_LINK_MAN
		IF @@ROWCOUNT>0 --个人配额部分
		BEGIN
			IF @V_QUOTA_MONEY-@IN_PRE_MONEY<0
				RAISERROR('个人配额已不足，不能再预约',16,1)
			--个人预约金额小于300W记录小额预约份数
			IF ((@V_SMALL_DEFINE=1 AND @V_CUST_TYPE = 1) OR @V_SMALL_DEFINE=2) AND @IN_PRE_MONEY < 3000000 AND @V_QUOTA_NUM-@IN_PRE_NUM<0
				RAISERROR('个人配额的合同份数已不足，不能再预约',16,1)
		END
		ELSE IF EXISTS (SELECT * FROM TPERSONALQUOTA WHERE ((@IN_PRODUCT_ID>0 AND PRODUCT_ID=@IN_PRODUCT_ID) OR (@IN_PREPRODUCT_ID>0 AND PREPRODUCT_ID=@IN_PREPRODUCT_ID)) AND SUB_PRODUCT_ID=@IN_SUB_PRODUCT_ID AND QUOTAMONEY>0)
			RAISERROR('个人配额未设置，不能预约',16,1)
		IF ISNULL(@IN_TEAM_ID,0)=0 --没指明团队时，取所在的所在的一支团队
			SELECT TOP 1 @V_TEAM_ID=TEAM_ID FROM TManagerTeamMembers WHERE MANAGERID=@IN_LINK_MAN
		ELSE
			SET @V_TEAM_ID=@IN_TEAM_ID
		--团队配额部分
		SELECT @V_QUOTA_MONEY=ISNULL(QUOTAMONEY,0)-ISNULL(PRE_SALEMONEY,0),@V_QUOTA_NUM=ISNULL(QUOTA_QUALIFIED_NUM,0)-ISNULL(PRE_QUALIFIEDNUM,0) FROM TTEAMQUOTA WHERE ((@IN_PRODUCT_ID>0 AND PRODUCT_ID=@IN_PRODUCT_ID) OR (@IN_PREPRODUCT_ID>0 AND PREPRODUCT_ID=@IN_PREPRODUCT_ID)) AND ISNULL(SUB_PRODUCT_ID,0)=@IN_SUB_PRODUCT_ID AND TEAM_ID=@V_TEAM_ID
		IF @@ROWCOUNT>0
		BEGIN
			IF @V_QUOTA_MONEY-@IN_PRE_MONEY<0
				RAISERROR('本团队配额已不足，不能再预约',16,1)
			IF ((@V_SMALL_DEFINE=1 AND @V_CUST_TYPE = 1) OR @V_SMALL_DEFINE=2) AND @IN_PRE_MONEY < 3000000 AND @V_QUOTA_NUM-@IN_PRE_NUM<0 --个人预约金额小于300W记录小额预约份数
				RAISERROR('本团队配额的合同份数已不足，不能再预约',16,1)
		END
		ELSE IF EXISTS (SELECT * FROM TTEAMQUOTA WHERE ((@IN_PRODUCT_ID>0 AND PRODUCT_ID=@IN_PRODUCT_ID) OR (@IN_PREPRODUCT_ID>0 AND PREPRODUCT_ID=@IN_PREPRODUCT_ID)) AND ISNULL(SUB_PRODUCT_ID,0)=@IN_SUB_PRODUCT_ID AND QUOTAMONEY>0)
			RAISERROR('本团队配额未设置，不能预约',16,1)
	END	
    -- 判断有没有输入预约编号，如果没有则自动生成一个预约编号，如果有输入则预约编号为用户输入的
    IF ISNULL(@OUT_PRE_CODE,'') = ''
    BEGIN
        IF @IN_PRODUCT_ID > 0
            SELECT @V_PRE_CODE_MAX=MAX_PRE_CODE
             FROM (SELECT ISNULL(MAX(PRE_CODE),'000') AS MAX_PRE_CODE,SUM(PRE_MONEY) PRE_MONEY_TOTAL
                  FROM TPRECONTRACT
                  WHERE PRODUCT_ID = @IN_PRODUCT_ID
                UNION
                SELECT ISNULL(MAX(PRE_CODE),'000') AS MAX_PRE_CODE,SUM(PRE_MONEY) PRE_MONEY_TOTAL
                  FROM INTRUST..TPRECONTRACT
                  WHERE PRODUCT_ID = @IN_PRODUCT_ID) TEMP
             ORDER BY TEMP.MAX_PRE_CODE        
        ELSE
            SELECT @V_PRE_CODE_MAX=ISNULL(MAX(PRE_CODE),'000')
              FROM TPRECONTRACT
              WHERE PREPRODUCT_ID = @IN_PREPRODUCT_ID
                  
        SELECT @V_NUM = CONVERT(INT,@V_PRE_CODE_MAX) + 1
    END
    ELSE
    BEGIN
        SELECT @V_NUM = CONVERT(INT,@OUT_PRE_CODE)
    END
      
    IF @V_NUM < 10
        SELECT @OUT_PRE_CODE = '00'+CONVERT(VARCHAR(8),@V_NUM)
    ELSE IF @V_NUM < 100
        SELECT @OUT_PRE_CODE = '0'+CONVERT(VARCHAR(8),@V_NUM)
    ELSE
        SELECT @OUT_PRE_CODE = CONVERT(VARCHAR(8),@V_NUM)
        
    -- 如果用户输入的预约编号存在则提示已存在
    IF @IN_PREPRODUCT_ID>0 
    BEGIN
        IF EXISTS (SELECT * FROM TPRECONTRACT WHERE PRE_CODE = @OUT_PRE_CODE AND PREPRODUCT_ID = @IN_PREPRODUCT_ID)
            RAISERROR(N'预约编号已存在！',16,3)
    END
    ELSE
    BEGIN
        IF EXISTS (SELECT * FROM TPRECONTRACT WHERE PRE_CODE = @OUT_PRE_CODE AND PRODUCT_ID = @IN_PRODUCT_ID)
				OR EXISTS (SELECT * FROM INTRUST..TPRECONTRACT WHERE PRE_CODE = @OUT_PRE_CODE AND PRODUCT_ID = @IN_PRODUCT_ID)
            RAISERROR(N'预约编号已存在！',16,3)
    END
    ------------------------------------------------------------------------
    SELECT @OUT_SERIAL_NO = 0
    IF @IN_PRE_DATE IS NULL OR @IN_PRE_DATE = 0
        SELECT @IN_PRE_DATE = CONVERT(INT,CONVERT(CHAR(8),GETDATE(),112))
    IF @IN_VALID_DAYS <= 0
        SELECT @V_END_DATE = 0
    ELSE
        SELECT @V_END_DATE = dbo.GETDAY(@IN_PRE_DATE,@IN_VALID_DAYS - 1)
    SELECT @V_PRE_TYPE_NAME = TYPE_CONTENT FROM TDICTPARAM WHERE TYPE_VALUE = @IN_PRE_TYPE
    IF @@ROWCOUNT=0
		RAISERROR(N'预约方式不存在！请联系管理员',16,3)
    
    SELECT @V_PRE_STATUS = '111301'
    SELECT @V_PRE_STATUS_NAME = N'有效'
    SET @V_PRE_MONEY=@IN_PRE_MONEY
    SET @V_PRE_MONEY_WAIT=0
    IF @V_PRE_MONEY_TOTAL+@IN_PRE_MONEY>@V_PRODUCT_MONEY --预约总金额已大于产品规模
    BEGIN
		SELECT @V_PRE_STATUS = '111302'
		SELECT @V_PRE_STATUS_NAME = N'非保证有效'
		IF @V_PRE_MONEY_TOTAL>@V_PRODUCT_MONEY SET @V_PRE_MONEY_TOTAL=@V_PRODUCT_MONEY --如果有效预约总金额大于产品规模，则有效总预约设为产品规模
		SET @V_PRE_MONEY_WAIT=@IN_PRE_MONEY-(@V_PRODUCT_MONEY-@V_PRE_MONEY_TOTAL)
		SET @V_PRE_MONEY=@IN_PRE_MONEY-@V_PRE_MONEY_WAIT
    END
    SELECT @V_CUST_SOURCE_NAME = TYPE_CONTENT FROM TDICTPARAM WHERE TYPE_VALUE = @IN_CUST_SOURCE
    IF NOT EXISTS (SELECT 1 FROM TDICTPARAM WHERE TYPE_VALUE = @IN_PRE_LEVEL)
        SELECT @IN_PRE_LEVEL = N'118201', @V_PRE_LEVEL_NAME = N'A级'
    ELSE
        SELECT @V_PRE_LEVEL_NAME = TYPE_CONTENT FROM TDICTPARAM WHERE TYPE_VALUE = @IN_PRE_LEVEL

    --预约是否要求审核 1是
    SELECT @V_NEED_CHECK=ISNULL(MAX(VALUE),0) FROM TSYSCONTROL WHERE FLAG_TYPE='PRECONTRACT_CHECK'
    
    BEGIN TRANSACTION

    INSERT INTO TPRECONTRACT(PREPRODUCT_ID, CUST_ID, PRE_CODE,FLOW_STATUS,PRE_MONEY_WAIT,
            PRE_MONEY, LINK_MAN, PRE_DATE,VALID_DAYS, END_DATE, PRE_TYPE,PRE_TYPE_NAME,
            PRE_STATUS,PRE_STATUS_NAME,INPUT_MAN,SUMMARY,PRE_NUM,EXP_REG_DATE,CUST_SOURCE,CUST_SOURCE_NAME,
            CHANNEL_TYPE,CHANNEL_FARE,PRE_LEVEL,PRE_LEVEL_NAME,PRODUCT_ID,SUB_PRODUCT_ID,CHECK_FLAG)
        VALUES(@IN_PREPRODUCT_ID,@IN_CUST_ID,@OUT_PRE_CODE,1,@V_PRE_MONEY_WAIT,
            @V_PRE_MONEY,@IN_LINK_MAN,@IN_PRE_DATE,@IN_VALID_DAYS,@V_END_DATE,@IN_PRE_TYPE,@V_PRE_TYPE_NAME,
            @V_PRE_STATUS,@V_PRE_STATUS_NAME,@IN_INPUT_MAN,@IN_SUMMARY,@IN_PRE_NUM,@IN_EXP_REG_DATE,@IN_CUST_SOURCE,@V_CUST_SOURCE_NAME,
            @IN_CHANNEL_TYPE,@IN_CHANNEL_FARE,@IN_PRE_LEVEL,@V_PRE_LEVEL_NAME,@V_PRODUCT_ID,@IN_SUB_PRODUCT_ID,@V_NEED_CHECK)

    SELECT @OUT_SERIAL_NO = @@IDENTITY
    IF @IN_PREPRODUCT_ID<>0
		UPDATE TPREPRODUCT 
		  SET PRE_FACT_MONEY = ISNULL(PRE_FACT_MONEY,0) + @IN_PRE_MONEY 
		  WHERE PREPRODUCT_ID = @IN_PREPRODUCT_ID
	ELSE IF @IN_PRODUCT_ID<>0
	    IF @V_NEED_CHECK<>1 AND @V_USER_ID NOT IN (15,21)/*建信、金汇要求不同步给业务系统*/ --不要求审核，同步给信托库
	    BEGIN
		    INSERT INTO INTRUST..TPRECONTRACT(BOOK_CODE,PRODUCT_ID, SUB_PRODUCT_ID, CUST_ID, PRE_CODE,
                PRE_MONEY, LINK_MAN, PRE_DATE,VALID_DAYS, END_DATE, PRE_TYPE,PRE_TYPE_NAME,
                PRE_STATUS,PRE_STATUS_NAME,INPUT_MAN,SUMMARY,PRE_NUM,EXP_REG_DATE,CUST_SOURCE,CUST_SOURCE_NAME,CHANNEL_TYPE)
            VALUES(1,@IN_PRODUCT_ID, @IN_SUB_PRODUCT_ID, @IN_CUST_ID,@OUT_PRE_CODE,
                @IN_PRE_MONEY,@IN_LINK_MAN,@IN_PRE_DATE,@IN_VALID_DAYS,@V_END_DATE,@IN_PRE_TYPE,@V_PRE_TYPE_NAME,
                @V_PRE_STATUS,@V_PRE_STATUS_NAME,@IN_INPUT_MAN,@IN_SUMMARY,@IN_PRE_NUM,@IN_EXP_REG_DATE,@IN_CUST_SOURCE,@V_CUST_SOURCE_NAME,@IN_CHANNEL_TYPE)
            
            UPDATE TPRECONTRACT
              SET BIND_SERIAL_NO=@@IDENTITY
              WHERE PRODUCT_ID=@IN_PRODUCT_ID AND PRE_CODE=@OUT_PRE_CODE
              
            IF @IN_SUB_PRODUCT_ID=0
			    UPDATE INTRUST..TPRODUCT SET FACT_PRE_NUM = @V_FACT_PRE_NUM+@IN_PRE_NUM, FACT_PRE_MONEY = ISNULL(FACT_PRE_MONEY,0) + @IN_PRE_MONEY
				    WHERE PRODUCT_ID = @IN_PRODUCT_ID
		    ELSE
			    UPDATE INTRUST..TSUBPRODUCT SET PRE_NUM = @V_FACT_PRE_NUM+@IN_PRE_NUM, PRE_MONEY = ISNULL(PRE_MONEY,0) + @IN_PRE_MONEY
				    WHERE PRODUCT_ID = @IN_PRODUCT_ID
	    END
	
    --修改销售人员预约配额完成情况
    IF ((@V_SMALL_DEFINE=1 AND @V_CUST_TYPE = 1) OR @V_SMALL_DEFINE=2) AND @IN_PRE_MONEY < 3000000 --个人预约金额小于300W记录小额预约份数
        SET @V_QUOTA_NUM = 1
    ELSE
        SET @V_QUOTA_NUM = 0
    --SELECT @V_TEAM_ID = A.TEAM_ID FROM TManagerTeams A
    --   WHERE A.LEADER = @IN_LINK_MAN OR EXISTS(SELECT 1 FROM TManagerTeamMembers Z WHERE A.TEAM_ID = Z.TEAM_ID AND Z.MANAGERID = @IN_LINK_MAN)
    IF @V_TEAM_ID IS NULL SET @V_TEAM_ID = -1
    IF EXISTS(SELECT 1 FROM TTEAMQUOTA WHERE TEAM_ID = @V_TEAM_ID AND PREPRODUCT_ID = @IN_PREPRODUCT_ID)
        UPDATE TTEAMQUOTA SET PRE_SALEMONEY = ISNULL(PRE_SALEMONEY,0) + @IN_PRE_MONEY, PRE_QUALIFIEDNUM = ISNULL(PRE_QUALIFIEDNUM,0) + @V_QUOTA_NUM
            WHERE TEAM_ID = @V_TEAM_ID AND PREPRODUCT_ID = @IN_PREPRODUCT_ID
    ELSE
        INSERT INTO TTEAMQUOTA(PRODUCT_ID,TEAM_ID,PREPRODUCT_ID,PRE_SALEMONEY,PRE_QUALIFIEDNUM)
            VALUES(0,@V_TEAM_ID,@IN_PREPRODUCT_ID,@IN_PRE_MONEY,@V_QUOTA_NUM)
    --不是客户经理自己修改，则通知客户经理
	IF @OLD_SERVICE_MAN<>@IN_INPUT_MAN
	BEGIN
		IF @IN_PREPRODUCT_ID>0
		INSERT INTO TSYSMESSAGE(TITLE,MSG,FROM_OPCODE,TO_OPCODE,INPUT_TIME,IS_READ)
			SELECT '新增客户预约','新增客户预约，客户名称：'+ISNULL(@V_CUST_NAME,'')+'，产品名称：[预发行]'+ISNULL(@V_PREPRODUCT_NAME,'')+ N'；预约号：' + @OUT_PRE_CODE+'；预约金额：'+CAST(ISNULL(@IN_PRE_MONEY,0) AS VARCHAR),@IN_INPUT_MAN,@OLD_SERVICE_MAN,GETDATE(),1
		ELSE
		INSERT INTO TSYSMESSAGE(TITLE,MSG,FROM_OPCODE,TO_OPCODE,INPUT_TIME,IS_READ)
			SELECT '新增客户预约','新增客户预约，客户名称：'+ISNULL(@V_CUST_NAME,'')+'，产品名称：'+ISNULL(@V_PREPRODUCT_NAME,'')+ N'；预约号：' + @OUT_PRE_CODE+'；预约金额：'+CAST(ISNULL(@IN_PRE_MONEY,0) AS VARCHAR),@IN_INPUT_MAN,@OLD_SERVICE_MAN,GETDATE(),1
    END
    --2.日志
    SELECT @SSUMMARY = N'客户预约销售，产品名称：' + @V_PREPRODUCT_NAME + N'预约号：' + @OUT_PRE_CODE
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY) VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    COMMIT TRANSACTION
    END TRY
    --3.异常处理
    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION
        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)
        SELECT @V_ERROR_STR = N'Message:%s,Procedure:%s,Line:%d',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()
        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_PROCEDURE,@V_ERROR_LINE)
        RETURN -100
    END CATCH
    RETURN 100
GO
