USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE SP_MODI_TPREMONEYDETAIL @IN_SERIAL_NO  INT, --序号
                                         @IN_DZ_TIME    DATETIME, --到帐时间,精确到分
                                         @IN_DZ_MONEY   DEC(16,3),    --到帐金额
                                         @IN_JK_TYPE    NVARCHAR(10), --缴款方式(1114)
                                         @IN_INPUT_MAN  INT,
                                         @IN_ONWAY_FLAG INT = 0       --在途资金标志:1是
WITH ENCRYPTION
AS
    SET NOCOUNT ON
    DECLARE @V_ERROR NVARCHAR(200)
    DECLARE @V_RET_CODE INT,@IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)
    SELECT @V_RET_CODE = -20701000
    SELECT @IBUSI_FLAG = 20701
    SELECT @SBUSI_NAME = N'修改客户预约到帐'
    SELECT @SSUMMARY = N'修改客户预约到帐'

    DECLARE @V_JK_TYPE_NAME NVARCHAR(30)
    DECLARE @V_END_DATE INT, @V_PRE_CODE NVARCHAR(16), @V_PREPRODUCT_ID INT, @V_PREPRODUCT_NAME NVARCHAR(100),
            @V_PRE_MONEY DEC(16,3), @V_RG_MONEY DEC(16,3), @V_PRE_SERIAL_NO INT, @V_DZ_MONEY DEC(16,3)

    ------------------------------------------------------------------------
    BEGIN TRY
    --1.业务逻辑与操作
    --校验
    SELECT @V_JK_TYPE_NAME = TYPE_CONTENT FROM TDICTPARAM WHERE TYPE_VALUE = @IN_JK_TYPE
    IF @V_JK_TYPE_NAME IS NULL
    BEGIN
        SET @V_ERROR = N'缴款方式【'+@IN_JK_TYPE+N'】不存在！'
        RAISERROR(@V_ERROR,16,3)
    END
    IF NOT EXISTS(SELECT 1 FROM TPREMONEYDETAIL WHERE SERIAL_NO = @IN_SERIAL_NO)
    BEGIN
        SET @V_ERROR = N'记录不存在！'
        RAISERROR(@V_ERROR,16,3)
    END
    SELECT @V_PRE_SERIAL_NO = PRE_SERIAL_NO, @V_DZ_MONEY = DZ_MONEY FROM TPREMONEYDETAIL WHERE SERIAL_NO = @IN_SERIAL_NO
    SELECT @V_PREPRODUCT_ID = PREPRODUCT_ID, @V_PRE_MONEY = PRE_MONEY, @V_RG_MONEY = ISNULL(RG_MONEY,0), @V_PRE_CODE = PRE_CODE
        FROM TPRECONTRACT WHERE SERIAL_NO = @V_PRE_SERIAL_NO
    SELECT @V_PREPRODUCT_NAME = PREPRODUCT_NAME FROM TPREPRODUCT WHERE PREPRODUCT_ID = @V_PREPRODUCT_ID
    IF @V_RG_MONEY - @V_DZ_MONEY + @IN_DZ_MONEY > @V_PRE_MONEY
    BEGIN
        SET @V_ERROR = N'修改后到帐总金额大于预约金额！'
        RAISERROR(@V_ERROR,16,3)
    END

    ------------------------------------------------------------------------
    BEGIN TRANSACTION

    UPDATE TPRECONTRACT SET RG_MONEY = ISNULL(RG_MONEY,0) - @V_DZ_MONEY + @IN_DZ_MONEY, RG_DATE = DBO.GETDATEINT(@IN_DZ_TIME) WHERE SERIAL_NO = @V_PRE_SERIAL_NO
    UPDATE TPREMONEYDETAIL SET DZ_MONEY = @IN_DZ_MONEY, DZ_DATE = @IN_DZ_TIME, JK_TYPE = @IN_JK_TYPE, JK_TYPE_NAME = @V_JK_TYPE_NAME, ONWAY_FLAG=@IN_ONWAY_FLAG
        WHERE SERIAL_NO = @IN_SERIAL_NO
    UPDATE TSALESRESULTFORSTATISTIC SET DZ_MONEY = @IN_DZ_MONEY, DZ_DATE = @IN_DZ_TIME, JK_TYPE = @IN_JK_TYPE, JK_TYPE_NAME = @V_JK_TYPE_NAME, ONWAY_FLAG=@IN_ONWAY_FLAG
        WHERE FK_TPREMONEYDETAIL = @IN_SERIAL_NO AND RECORDS_COUNT = 1

    --2.日志
    SELECT @SSUMMARY = N'修改客户预约到帐，产品名称：' + @V_PREPRODUCT_NAME
                                         + N'预约号：' + @V_PRE_CODE
                                       + N'到帐金额：' + CONVERT(NVARCHAR(30),@IN_DZ_MONEY)
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY) VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    COMMIT TRANSACTION
    END TRY
    --3.异常处理
    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION
        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)
        SELECT @V_ERROR_STR = N'Message:%s,Procedure:%s,Line:%d',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()
        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_PROCEDURE,@V_ERROR_LINE)
        RETURN -100
    END CATCH
    RETURN 100
GO
