USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_ADD_TCONTRACTSTAT @IN_PRODUCT_ID        INT,
                                   @IN_SERVICE_MAN          INT,	--客户经理编号
                                   @IN_FLAG					INT,	--合同状态
                                   @IN_SUMMARY              NVARCHAR(500),--备注
                                   @IN_INPUT_MAN            INT,		--操作员
                                   @IN_SERIAL_NO			INT = NULL	--序号
WITH ENCRYPTION
AS
    SET XACT_ABORT ON

    DECLARE @V_SERIAL INT
    DECLARE @IBUSI_FLAG INT, @SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)

    SELECT @IBUSI_FLAG = 20601
    SELECT @SBUSI_NAME = N'增加合同统计', @SSUMMARY = N'增加合同统计'
	SET @IN_SERIAL_NO = ISNULL(@IN_SERIAL_NO,0)

    DECLARE @RET INT, @V_DT_INTRUST INT
    
    BEGIN TRY
	BEGIN TRANSACTION
	IF @IN_SERIAL_NO=0 OR NOT EXISTS (SELECT * FROM [TCONTRACTSTAT] WHERE PRODUCT_ID=@IN_PRODUCT_ID AND SERIAL_NO=@IN_SERIAL_NO)
	BEGIN
		--生成SERIAL_NO
		SELECT @V_SERIAL = ISNULL(MAX(SERIAL_NO),0)+ 1 FROM TCONTRACTSTAT WHERE PRODUCT_ID = @IN_PRODUCT_ID
		INSERT INTO [TCONTRACTSTAT]([PRODUCT_ID],[SERIAL_NO],[SERVICE_MAN],[FLAG],[SUMMARY],[INPUT_MAN],[INPUT_TIME])
			VALUES(@IN_PRODUCT_ID, @V_SERIAL, @IN_SERVICE_MAN, @IN_FLAG, @IN_SUMMARY, @IN_INPUT_MAN, CURRENT_TIMESTAMP)
    END
    ELSE
    BEGIN
		UPDATE [TCONTRACTSTAT]
			SET SERVICE_MAN=@IN_SERVICE_MAN,
			FLAG = @IN_FLAG,
			SUMMARY = @IN_SUMMARY,
			INPUT_MAN = @IN_INPUT_MAN,
			INPUT_TIME = CURRENT_TIMESTAMP
		WHERE PRODUCT_ID=@IN_PRODUCT_ID AND SERIAL_NO=@IN_SERIAL_NO
		SELECT @SBUSI_NAME = N'修改合同统计', @SSUMMARY = N'修改合同统计：修改人ID：'+CAST(@IN_INPUT_MAN AS VARCHAR)
	END
    --保存日志
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME, @IN_INPUT_MAN,@SSUMMARY)
    
    COMMIT TRANSACTION
    END TRY
    
    BEGIN CATCH
        IF @@TRANCOUNT > 0 BEGIN
            ROLLBACK TRANSACTION
        END
        
        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)

        SELECT @V_ERROR_STR = N'Message:%s,Error:%d,Level:%d,State:%d,Procedure:%s,Line:%d',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()

        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_NUMBER,@V_ERROR_SEVERITY,@V_ERROR_STATE,
                  @V_ERROR_PROCEDURE,@V_ERROR_LINE)
        
        RETURN -100
    END CATCH

    SET XACT_ABORT OFF
    RETURN 100
GO
