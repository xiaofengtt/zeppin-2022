USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_ADD_TCOMMISSIONRATE
							@IN_PRODUCT_ID         INTEGER       ,       --产品ID
							@IN_SUBPRODUCT_ID      INTEGER       ,       --子产品ID
							@IN_PERIOD             INTEGER       ,       --期限值
							@IN_PERIOD_UNIT        INTEGER       ,       -- 0无期限 1天 2月 3年
							@IN_TRADE_START_MONEY  INTEGER       ,       --合同份额下限(万元)
							@IN_TRADE_END_MONEY    INTEGER       ,       --合同份额上限(万元)
							@IN_FEE_RATE           DECIMAL(5,4),         --提成比例
							@IN_FEE_AMOUNT         DECIMAL(16,2),        --提成固定金额
							@IN_SUMMARY            NVARCHAR(400),        --备注
							@IN_INPUT_MAN          INTEGER               --录入人
							
WITH ENCRYPTION
AS

    DECLARE @V_DATE INT, @V_PERIOD_UNIT INT,@V_ERROR NVARCHAR(200)
    
    DECLARE @V_RET_CODE INT,@IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)
    SELECT @IBUSI_FLAG = 50102
    SELECT @SBUSI_NAME = N'销售提成比率设置'
    SELECT @SSUMMARY = N'销售提成比率设置'
    SET @V_DATE = CONVERT(INT,CONVERT(CHAR(8),GETDATE(),112))
    SET @IN_TRADE_START_MONEY = ISNULL(@IN_TRADE_START_MONEY,0)
    SET @IN_TRADE_END_MONEY = ISNULL(@IN_TRADE_END_MONEY,0)
    
    BEGIN TRY
    
    IF ISNULL(@IN_FEE_RATE,0)=0 AND ISNULL(@IN_FEE_AMOUNT,0)=0
        RAISERROR('未设置提成比率或者金额',16,1)
    IF @IN_TRADE_END_MONEY=0
		RAISERROR('金额上限不能为零',16,1)
    IF @IN_TRADE_START_MONEY>=@IN_TRADE_END_MONEY
		RAISERROR('金额上限必需大于下限',16,1)
    --期限统一
    SELECT TOP 1 @V_PERIOD_UNIT=PERIOD_UNIT FROM TCOMMISSIONRATE WHERE PRODUCT_ID = @IN_PRODUCT_ID AND SUBPRODUCT_ID = @IN_SUBPRODUCT_ID
    IF @@ROWCOUNT>0 AND @V_PERIOD_UNIT<>@IN_PERIOD_UNIT
    BEGIN
		IF @V_PERIOD_UNIT =0
			SET @V_ERROR = '期限未统一，请设置为：无期限'
		IF @V_PERIOD_UNIT =1
			SET @V_ERROR = '期限未统一，请设置为：天'
		IF @V_PERIOD_UNIT =2
			SET @V_ERROR = '期限未统一，请设置为：月'
		IF @V_PERIOD_UNIT =3
			SET @V_ERROR = '期限未统一，请设置为：年'
		RAISERROR(@V_ERROR,16,1)
    END
	--验证是否重复设置(合同份额下限)
    IF EXISTS(SELECT 1 FROM TCOMMISSIONRATE WHERE PRODUCT_ID = @IN_PRODUCT_ID AND SUBPRODUCT_ID = @IN_SUBPRODUCT_ID AND PERIOD=@IN_PERIOD AND @IN_TRADE_START_MONEY>= TRADE_START_MONEY AND @IN_TRADE_START_MONEY<TRADE_END_MONEY)
        RAISERROR('提成比率设置重复！',16,1)
	--验证是否重复设置(合同份额下限)
    IF EXISTS(SELECT 1 FROM TCOMMISSIONRATE WHERE PRODUCT_ID = @IN_PRODUCT_ID AND SUBPRODUCT_ID = @IN_SUBPRODUCT_ID AND PERIOD=@IN_PERIOD AND @IN_TRADE_END_MONEY>= TRADE_START_MONEY AND @IN_TRADE_END_MONEY<TRADE_END_MONEY)
        RAISERROR('提成比率设置重复！',16,1)
    
    BEGIN TRANSACTION 
    
    INSERT INTO TCOMMISSIONRATE(PRODUCT_ID, SUBPRODUCT_ID, PERIOD, PERIOD_UNIT, TRADE_START_MONEY, TRADE_END_MONEY,
            FEE_RATE, FEE_AMOUNT, SUMMARY, INPUT_MAN, INPUT_DATE,CHECK_FLAG)
        VALUES (@IN_PRODUCT_ID, @IN_SUBPRODUCT_ID, @IN_PERIOD, @IN_PERIOD_UNIT, @IN_TRADE_START_MONEY, @IN_TRADE_END_MONEY,
            @IN_FEE_RATE, @IN_FEE_AMOUNT, @IN_SUMMARY, @IN_INPUT_MAN, @V_DATE, 0)
    

    SELECT @SSUMMARY = N'销售提成比率设置，产品ID：' + RTRIM(CONVERT(CHAR,@IN_PRODUCT_ID))
                                     
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME, @IN_INPUT_MAN,@SSUMMARY)

    COMMIT TRANSACTION
    RETURN 100
    
    END TRY
    
    BEGIN CATCH
        IF @@TRANCOUNT > 0 BEGIN
            ROLLBACK TRANSACTION
        END
        
        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)

        SELECT @V_ERROR_STR = N'Message:%s,Error:%d,Level:%d,State:%d,Procedure:%s,Line:%d',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()

        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_NUMBER,@V_ERROR_SEVERITY,@V_ERROR_STATE,
                  @V_ERROR_PROCEDURE,@V_ERROR_LINE)
        
        RETURN -100
    END CATCH
GO
