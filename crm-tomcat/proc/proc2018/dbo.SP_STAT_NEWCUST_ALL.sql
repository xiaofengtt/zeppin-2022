USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_STAT_NEWCUST_ALL @IN_BEGIN_DATE INT, --开始日期
                                     @IN_END_DATE   INT, --结日期
                                     @IN_CUST_TYPE  INT, --客户类型1个人2机构
                                     @IN_SERVICE_MAN_NAME NVARCHAR(20), --客户经理
                                     @IN_TEAM_NAME NVARCHAR(80),-- 团队名称
									 @IN_INPUT_MAN  INT   --操作员,
                                     
WITH ENCRYPTION
AS
    SET NOCOUNT ON
    DECLARE @V_GR_COUNT INT, @V_JG_COUNT INT,@V_TEAM_ID INT
    DECLARE @V_TEAM_MEMBERS TABLE(OP_CODE INT)
    
    SELECT @IN_BEGIN_DATE=0 WHERE @IN_BEGIN_DATE IS NULL
    SELECT @IN_END_DATE=20991231 WHERE @IN_END_DATE IS NULL
    SELECT @IN_CUST_TYPE=0 WHERE @IN_CUST_TYPE IS NULL OR @IN_CUST_TYPE NOT IN (0,1,2)
    SELECT @IN_SERVICE_MAN_NAME='' WHERE @IN_SERVICE_MAN_NAME IS NULL
    SELECT @IN_TEAM_NAME='' WHERE @IN_TEAM_NAME IS NULL
    
    SELECT @V_TEAM_ID=TEAM_ID FROM TManagerTeams WHERE TEAM_NAME=@IN_TEAM_NAME
    --团队成员列表
    INSERT INTO @V_TEAM_MEMBERS(OP_CODE)
		SELECT MANAGERID FROM TManagerTeamMembers where TEAM_ID=@V_TEAM_ID
		UNION ALL 
		SELECT MANAGERID FROM TManagerTeamMembers where TEAM_ID IN(SELECT TEAM_ID FROM dbo.GetTEAM_ID(@V_TEAM_ID))
    CREATE TABLE #TT_NEWCUST(
        CUST_ID         INTEGER,
        CUST_NO         NVARCHAR(8),
        CUST_NAME       NVARCHAR(100),
        CUST_TYPE       NVARCHAR(10),
        START_DATE      INTEGER,
        SERVICE_MAN     NVARCHAR(30),    --客户经理
        RECOMMENDED     NVARCHAR(60),    --联系人
        BUSI_DEAL       NVARCHAR(10)     --事实客户/潜在客户
    )
    --从客户表中查询符合条件的客户
    INSERT INTO #TT_NEWCUST (CUST_ID,CUST_NO, CUST_NAME, CUST_TYPE, START_DATE, SERVICE_MAN, RECOMMENDED,BUSI_DEAL)
         SELECT B.CUST_ID,B.CUST_NO, B.CUST_NAME, B.CUST_TYPE, dbo.GETDATEINT(B.INPUT_TIME),E.OP_NAME, B.RECOMMENDED,'潜在客户'
            FROM  TCUSTOMERS B LEFT JOIN TOPERATOR E ON B.SERVICE_MAN=E.OP_CODE
            WHERE (dbo.GETDATEINT(B.INPUT_TIME) BETWEEN ISNULL(@IN_BEGIN_DATE,0) AND ISNULL(@IN_END_DATE,20991231))
				AND(B.CUST_TYPE = @IN_CUST_TYPE OR ISNULL(@IN_CUST_TYPE,0) = 0)
                AND(E.OP_NAME = @IN_SERVICE_MAN_NAME OR @IN_SERVICE_MAN_NAME = '')
                AND(@IN_TEAM_NAME='' OR EXISTS(SELECT OP_CODE FROM @V_TEAM_MEMBERS Y WHERE Y.OP_CODE=B.SERVICE_MAN))
   UPDATE #TT_NEWCUST SET BUSI_DEAL='事实客户' WHERE EXISTS (SELECT 1 FROM INTRUST..TBENIFITOR WHERE CUST_ID=#TT_NEWCUST.CUST_ID)
   
	--输出
    --SELECT * FROM #TT_NEWCUST 
    SELECT CUST_NAME,START_DATE,SERVICE_MAN,RECOMMENDED,BUSI_DEAL FROM #TT_NEWCUST 
        ORDER BY CUST_NAME
    RETURN @@ROWCOUNT
GO
