USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE SP_CHECK_TPREDOUBLERECORDINFO @IN_SERIAL_NO  INT, --序号 
											  @IN_STATUS     INT, --审核状态
											  @IN_INPUT_MAN  INT  --操作员
WITH ENCRYPTION
AS
    SET NOCOUNT ON
    DECLARE @V_ERROR NVARCHAR(200)
    DECLARE @IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)
    
    SELECT @IBUSI_FLAG = 655350
    SELECT @SBUSI_NAME = N'审核客户预约双录'
    SELECT @SSUMMARY = N'审核客户预约双录'

    DECLARE @V_PRE_CODE NVARCHAR(16), @V_PREPRODUCT_NAME NVARCHAR(100), @V_PREPRODUCT_ID INT, @V_PRE_SERIAL_NO INT
    
    DECLARE @V_STATUS INT -- -1未审核 0审核未通过 1审核已通过
    DECLARE @V_STATUS_NAME NVARCHAR(50)
    DECLARE @V_SL_TIME NVARCHAR(50), @V_SL_TYPE_NAME NVARCHAR(50)
    ------------------------------------------------------------------------
    BEGIN TRY
    --1.业务逻辑与操作
    --校验
    IF @IN_STATUS < -1
    BEGIN
        SET @V_ERROR = N'审核状态错误！'
        RAISERROR(@V_ERROR,16,1)
    END
    
    IF NOT EXISTS(SELECT 1 FROM TPREDOUBLERECORDINFO WHERE SERIAL_NO = @IN_SERIAL_NO)
    BEGIN
        SET @V_ERROR = N'记录不存在！'
        RAISERROR(@V_ERROR,16,2)
    END
    
	SELECT @V_STATUS = [STATUS], @V_STATUS_NAME = STATUS_NAME, @V_PRE_SERIAL_NO = PRE_SERIAL_NO 
	,@V_SL_TIME = RECORD_TIME, @V_SL_TYPE_NAME = RECORD_TYPE_NAME
	FROM TPREDOUBLERECORDINFO WHERE SERIAL_NO = @IN_SERIAL_NO
	
	IF @V_STATUS = 1
	BEGIN
        SET @V_ERROR = N'记录已审核，不能审核！'
        RAISERROR(@V_ERROR,16,3)
    END
    IF @V_STATUS = 0
	BEGIN
        SET @V_ERROR = N'记录已审核，不能审核！'
        RAISERROR(@V_ERROR,16,3)
    END
    
	SELECT @V_PREPRODUCT_ID = PREPRODUCT_ID, @V_PRE_CODE = PRE_CODE
        FROM TPRECONTRACT WHERE SERIAL_NO = @V_PRE_SERIAL_NO
    SELECT @V_PREPRODUCT_NAME = PREPRODUCT_NAME FROM TPREPRODUCT WHERE PREPRODUCT_ID = @V_PREPRODUCT_ID
    
    IF @IN_STATUS = 1 
    BEGIN
		SET @V_STATUS_NAME = N'审核通过'
    END
    
    IF @IN_STATUS = 0 
    BEGIN
		 SET @V_STATUS_NAME = N'审核不通过'
    END
    ------------------------------------------------------------------------
    BEGIN TRANSACTION

    UPDATE TPREDOUBLERECORDINFO SET [STATUS] = @IN_STATUS, STATUS_NAME = @V_STATUS_NAME 
    ,CHECKER = @IN_INPUT_MAN, CHECKER_TIME = GETDATE() 
        WHERE SERIAL_NO = @IN_SERIAL_NO

    --2.日志
    SELECT @SSUMMARY = N'审核客户预约双录，产品名称：' + @V_PREPRODUCT_NAME
                                     + N'预约号：' + @V_PRE_CODE
                                   + N'双录时间：' + @V_SL_TIME
                                    + N'双录模式：' + @V_SL_TYPE_NAME
                                    + N'审核状态：' + @V_STATUS_NAME
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY) VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    COMMIT TRANSACTION
    END TRY
    --3.异常处理
    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION
        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)
        SELECT @V_ERROR_STR = N'Message:%s,Procedure:%s,Line:%d',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()
        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_PROCEDURE,@V_ERROR_LINE)
        RETURN -100
    END CATCH
    RETURN 100
GO
