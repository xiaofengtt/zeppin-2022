USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_MODI_TCUSTOMERS_SERVICE_MAN @IN_CUST_ID      INT,
                                                @IN_SERVICE_MAN  INT,
                                                @IN_INPUT_MAN    INT
WITH ENCRYPTION
AS
    DECLARE @V_STATUS NVARCHAR(10), @V_STATUS_NAME NVARCHAR(30), @V_CUST_NAME NVARCHAR(100), @V_BOOK_CODE INT
    DECLARE @OLD_SERVICE_MAN INT,@OLD_SERVICE_NAME NVARCHAR(20),@V_SERVICE_NAME NVARCHAR(20),@V_SERVICE_MAN_DEPART_ID INT
    DECLARE @V_RET_CODE INT, @IBUSI_FLAG INT,@V_USER_ID INT
    DECLARE @SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)
	DECLARE @V_ROLE_ID INT,@V_INPUT_MAN_DEPART_ID INT
    SELECT @V_RET_CODE = -20602000, @IBUSI_FLAG = 20602
    SELECT @SBUSI_NAME = N'修改客户客户经理', @SSUMMARY = N'修改客户客户经理'
    SELECT @V_USER_ID=USER_ID FROM TSYSTEMINFO

    SELECT @V_STATUS = STATUS, @V_CUST_NAME = CUST_NAME, @OLD_SERVICE_MAN = SERVICE_MAN FROM TCustomers WHERE CUST_ID = @IN_CUST_ID
    IF @@ROWCOUNT=0
		RETURN 100
    SELECT @V_BOOK_CODE = BOOK_CODE FROM INTRUST..TCUSTOMERINFO WHERE CUST_ID = @IN_CUST_ID
    IF @V_STATUS = '112805' RETURN 100
    IF @OLD_SERVICE_MAN = @IN_SERVICE_MAN RETURN 100
    SELECT @OLD_SERVICE_NAME = OP_NAME,@V_SERVICE_MAN_DEPART_ID=DEPART_ID FROM TOPERATOR WHERE OP_CODE = @OLD_SERVICE_MAN
    SELECT @V_SERVICE_NAME = OP_NAME FROM TOPERATOR WHERE OP_CODE = @IN_SERVICE_MAN
    IF @@ROWCOUNT=0
		RETURN 100
    SET @OLD_SERVICE_NAME=ISNULL(@OLD_SERVICE_NAME,'')
    
	BEGIN TRY
    BEGIN TRANSACTION

    SELECT @V_ROLE_ID=ROLE_ID,@V_INPUT_MAN_DEPART_ID=DEPART_ID FROM TOPERATOR WHERE OP_CODE=@IN_INPUT_MAN
    IF @V_USER_ID<>15 /*建信不操作业务系统数据*/
    BEGIN
		--不是同一部门的不能修改:ISNULL(@OLD_SERVICE_NAME,'')<>'' AND 
		IF @V_ROLE_ID<>1 AND @V_INPUT_MAN_DEPART_ID<>@V_SERVICE_MAN_DEPART_ID
		BEGIN
			DECLARE @V_ERR_MSG NVARCHAR(200)
			SET @V_ERR_MSG='原客户['+@V_CUST_NAME+']的客户经理['+ISNULL(@OLD_SERVICE_NAME,'')+']不在本部门，不能修改'
			RAISERROR(@V_ERR_MSG,16,1)
		END
		--修改信托业务库的客户经理
		INSERT INTO INTRUST..TCUSTINFODETAIL(BOOK_CODE,CUST_ID,FIELD_NAME,FIELD_CN_NAME,OLD_FIELD_INFO,NEW_FIELD_INFO,INPUT_MAN)
				VALUES(@V_BOOK_CODE,@IN_CUST_ID,'SERVICE_MAN',N'客户经理',@OLD_SERVICE_NAME,@V_SERVICE_NAME,@IN_INPUT_MAN)
		UPDATE INTRUST..TCUSTOMERINFO SET SERVICE_MAN = @IN_SERVICE_MAN WHERE CUST_ID = @IN_CUST_ID
    END
    --------------------------------------------------------------------
    UPDATE TCustomers SET SERVICE_MAN = @IN_SERVICE_MAN WHERE CUST_ID = @IN_CUST_ID
    
    SELECT @SSUMMARY = N'修改客户经理，客户ID：' + RTRIM(CONVERT(CHAR(8),@IN_CUST_ID)) + N'_' + @V_CUST_NAME + N';客户经理；' + @OLD_SERVICE_NAME + N'->' + @V_SERVICE_NAME
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    
    COMMIT TRANSACTION
    END TRY

    --异常处理
    BEGIN CATCH
        IF @@TRANCOUNT > 0
            ROLLBACK TRANSACTION
        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)
        SELECT @V_ERROR_STR = N'Message:%s<BR><font color = "white">Error:%d,Level:%d,State:%d,Procedure:%s,Line:%d</font>',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()
        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_NUMBER,@V_ERROR_SEVERITY,@V_ERROR_STATE,
                  @V_ERROR_PROCEDURE,@V_ERROR_LINE)
        RETURN -100
    END CATCH
    RETURN 100
GO
