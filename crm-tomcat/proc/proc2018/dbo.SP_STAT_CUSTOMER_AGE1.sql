USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_STAT_CUSTOMER_AGE1 @IN_DATE         INT = NULL,
                                       @IN_CHANNEL_TYPE NVARCHAR(10) = ''
WITH ENCRYPTION
AS
    SET NOCOUNT ON
    IF @IN_DATE IS NULL OR @IN_DATE = 0
        SET @IN_DATE = dbo.GETDATEINT(GETDATE())
    CREATE TABLE #TEMP_TABLE5
    (
        BEGIN_AGE   INT,
        END_AGE     INT,
        TOTAL       INT,
        RG_MONEY    DECIMAL(16,3),
        NUMBER_RATE DECIMAL(16,4),
        RG_RATE     DECIMAL(16,4)
    )
    DECLARE @VT_CUSTS TABLE(CUST_ID INT)
    INSERT INTO @VT_CUSTS(CUST_ID) --只购买了单一产品的客户
        SELECT DISTINCT CUST_ID FROM INTRUST..TBENIFITOR A WHERE EXISTS(SELECT 1 FROM INTRUST..TPRODUCT B WHERE A.PRODUCT_ID = B.PRODUCT_ID AND B.INTRUST_FLAG1 = 1)
            AND NOT EXISTS(SELECT 1 FROM INTRUST..TPRODUCT C WHERE A.PRODUCT_ID = C.PRODUCT_ID AND C.INTRUST_FLAG1 = 2)
    CREATE TABLE #TMP_CUST_FIRSTBUY(CUST_ID INT, FIRST_RG_DATE INT) --客户首次购买时间
    INSERT INTO #TMP_CUST_FIRSTBUY(CUST_ID) SELECT CUST_ID FROM EFCRM..TCustomers
    UPDATE #TMP_CUST_FIRSTBUY SET FIRST_RG_DATE = B.BEN_DATE
        FROM #TMP_CUST_FIRSTBUY A, (SELECT CUST_ID, MIN(BEN_DATE) AS BEN_DATE FROM INTRUST..TBENIFITOR GROUP BY CUST_ID) B
        WHERE A.CUST_ID = B.CUST_ID

    INSERT INTO #TEMP_TABLE5(BEGIN_AGE,END_AGE,TOTAL,RG_MONEY)
        SELECT C_TABLE.BEGIN_AGE,C_TABLE.END_AGE,COUNT(*) ,SUM(TO_AMOUNT) FROM
            (SELECT
                CASE WHEN (ISNULL(A.AGE,0)=0 ) THEN 0
                     WHEN (ISNULL(A.AGE,0)<30) THEN 20
                     WHEN (ISNULL(A.AGE,0)<40) THEN 30
                     WHEN (ISNULL(A.AGE,0)<50) THEN 40
                     WHEN (ISNULL(A.AGE,0)<60) THEN 50
                     WHEN (ISNULL(A.AGE,0)>=60) THEN 60
                     ELSE 0 END AS BEGIN_AGE ,
                CASE WHEN (ISNULL(A.AGE,0)=0 ) THEN 0
                     WHEN (ISNULL(A.AGE,0)<30) THEN 29
                     WHEN (ISNULL(A.AGE,0)<40) THEN 39
                     WHEN (ISNULL(A.AGE,0)<50) THEN 49
                     WHEN (ISNULL(A.AGE,0)<60) THEN 59
                     WHEN (ISNULL(A.AGE,0)>=60) THEN 100
                     ELSE 0 END AS END_AGE,
                B.TO_AMOUNT AS TO_AMOUNT
             FROM TCUSTOMERS A LEFT JOIN
                  (SELECT CUST_ID, SUM(TO_AMOUNT)AS TO_AMOUNT FROM INTRUST..TMONEYDETAIL
                   WHERE IS_JK_DATA = 1 AND DZ_DATE <= @IN_DATE GROUP BY CUST_ID) B ON A.CUST_ID = B.CUST_ID
             WHERE EXISTS(SELECT 1 FROM EFCRM..TCUSTSOURCEINFO O WHERE A.CUST_ID = O.CUST_ID AND O.CHANNEL_TYPE LIKE @IN_CHANNEL_TYPE+'%')
                AND EXISTS(SELECT 1 FROM #TMP_CUST_FIRSTBUY H WHERE A.CUST_ID = H.CUST_ID AND H.FIRST_RG_DATE <= @IN_DATE)
                AND NOT EXISTS(SELECT 1 FROM @VT_CUSTS Z WHERE A.CUST_ID = Z.CUST_ID)
                AND EXISTS(SELECT 1 FROM INTRUST..TBENIFITOR O WHERE A.CUST_ID = O.CUST_ID)
            ) AS C_TABLE
        GROUP BY C_TABLE.BEGIN_AGE,C_TABLE.END_AGE ORDER BY  C_TABLE.BEGIN_AGE

    DECLARE @V_NMBER_ACOUNT DECIMAL(16,4),@V_RG_ACOUNT DECIMAL(16,4)
    SET @V_NMBER_ACOUNT = (SELECT SUM(TOTAL) FROM #TEMP_TABLE5)
    SET @V_RG_ACOUNT = (SELECT SUM(RG_MONEY) FROM #TEMP_TABLE5)

    UPDATE #TEMP_TABLE5 SET NUMBER_RATE = TOTAL/@V_NMBER_ACOUNT,
                           RG_RATE     = RG_MONEY/@V_RG_ACOUNT

    SELECT * FROM #TEMP_TABLE5
GO
