USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE SP_ADD_TPREDOUBLERECORDINFO @IN_PRE_SERIAL_NO INT, --预约表主键(EFCRM..TPRECONTRACT.SERIAL_NO)
											 @IN_SL_TIME       NVARCHAR(50), --双录时间
											 @IN_SL_TYPE       NVARCHAR(20), --双录模式
											 @IN_SL_TYPE_NAME  NVARCHAR(50), --双录模式说明
											 @IN_INPUT_MAN     INT
WITH ENCRYPTION
AS
    SET NOCOUNT ON
    DECLARE @V_ERROR NVARCHAR(200)
    DECLARE @V_STATUS INT -- -1未审核 0审核未通过 1审核已通过
    DECLARE @V_STATUS_NAME NVARCHAR(50)
    
    SELECT @V_STATUS=-1
    SELECT @V_STATUS_NAME=N'未审核'
    DECLARE @IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)

    SELECT @IBUSI_FLAG = 655350
    SELECT @SBUSI_NAME = N'客户预约双录'
    SELECT @SSUMMARY = N'客户预约双录'

    DECLARE @V_SERIAL_NO INT
    DECLARE @V_PRE_CODE NVARCHAR(16), @V_PREPRODUCT_NAME NVARCHAR(100), @V_PREPRODUCT_ID INT
           

    ------------------------------------------------------------------------
    BEGIN TRY
    --1.业务逻辑与操作
    --校验
	IF NOT EXISTS(SELECT 1 FROM TPRECONTRACT WHERE SERIAL_NO = @IN_PRE_SERIAL_NO)
    BEGIN
        SET @V_ERROR = N'预约记录不存在！'
        RAISERROR(@V_ERROR,16,3)
    END
    IF EXISTS(SELECT 1 FROM TPREDOUBLERECORDINFO WHERE PRE_SERIAL_NO = @IN_PRE_SERIAL_NO AND [STATUS] IN (-1,1))
    BEGIN
        SET @V_ERROR = N'记录已存在！'
        RAISERROR(@V_ERROR,16,3)
    END
    SELECT @V_PREPRODUCT_ID = PREPRODUCT_ID, @V_PRE_CODE = PRE_CODE
        FROM TPRECONTRACT WHERE SERIAL_NO = @IN_PRE_SERIAL_NO
    SELECT @V_PREPRODUCT_NAME = PREPRODUCT_NAME FROM TPREPRODUCT WHERE PREPRODUCT_ID = @V_PREPRODUCT_ID
    ------------------------------------------------------------------------
    BEGIN TRANSACTION
    
    INSERT INTO TPREDOUBLERECORDINFO(PRE_SERIAL_NO,RECORD_TIME,RECORD_TYPE,RECORD_TYPE_NAME,[STATUS],STATUS_NAME,INPUT_MAN)
       VALUES(@IN_PRE_SERIAL_NO,@IN_SL_TIME,@IN_SL_TYPE,@IN_SL_TYPE_NAME,@V_STATUS,@V_STATUS_NAME,@IN_INPUT_MAN)
    SET @V_SERIAL_NO = @@IDENTITY
    --2.日志
    SELECT @SSUMMARY = N'客户预约双录，产品名称：' + @V_PREPRODUCT_NAME
                                     + N'预约号：' + @V_PRE_CODE
                                   + N'双录时间：' + @IN_SL_TIME
                                    + N'双录模式：' + @IN_SL_TYPE_NAME
                                    + N'审核状态：' + @V_STATUS_NAME
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY) VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    COMMIT TRANSACTION
    END TRY
    --3.异常处理
    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION
        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)
        SELECT @V_ERROR_STR = N'Message:%s,Procedure:%s,Line:%d',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()
        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_PROCEDURE,@V_ERROR_LINE)
        RETURN -100
    END CATCH
    RETURN @V_SERIAL_NO
GO
