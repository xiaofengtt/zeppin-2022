USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_SEARCH_INFOREPOS @IN_SEARCHKEY          NVARCHAR(200),  --查询关键字
                                     @IN_INPUT_MAN          INT,            --操作员
                                     @IN_INCLUDE_PRODUCT    INT = 1,        --包含产品库 1包含
                                     @IN_INCLUDE_WIKI       INT = 1,        --包含知识库 1包含
                                     @IN_PRODUCT_STATUS     INT = NULL,     --产品状态（0全部 2推介期 3正常期 4结束）
                                     @IN_OPENFLAG           INT = NULL,     --产品发行方式（0全部 1开放式 2封闭式）
                                     @IN_CLASS_TYPE1        NVARCHAR(10) = NULL,    --产品分类(1113)
                                     @IN_PRE_DATE1          INT = NULL,     --推介期下限
                                     @IN_PRE_DATE2          INT = NULL,     --推介期上限
                                     @IN_PRESTART_TIME1     DATETIME=NULL,  --预约开始时间下限
                                     @IN_PRESTART_TIME2     DATETIME=NULL,  --预约开始时间上限
                                     @IN_WIKI_CLASS         NVARCHAR(30) = NULL,   --知识库文章分类
                                     @IN_SORTFIELD          NVARCHAR(200) = NULL  --排序字段
WITH ENCRYPTION
AS
    SET NOCOUNT ON
    IF @IN_SEARCHKEY IS NULL SET @IN_SEARCHKEY = ''
    IF @IN_PRODUCT_STATUS IS NULL SET @IN_PRODUCT_STATUS = 0
    IF @IN_OPENFLAG IS NULL SET @IN_OPENFLAG = 0
    IF @IN_CLASS_TYPE1 IS NULL SET @IN_CLASS_TYPE1 = ''
    IF @IN_PRE_DATE1 IS NULL SET @IN_PRE_DATE1 = 0
    IF @IN_PRE_DATE2 = 0 OR @IN_PRE_DATE2 IS NULL SET @IN_PRE_DATE2 = 99999999
    IF ISNULL(@IN_PRESTART_TIME1,'')='' SET @IN_PRESTART_TIME1 = '1900-01-01'
    IF ISNULL(@IN_PRESTART_TIME2,'')='' SET @IN_PRESTART_TIME2 = '2199-12-31'
    IF @IN_WIKI_CLASS IS NULL SET @IN_WIKI_CLASS = ''
    DECLARE @V_SQL NVARCHAR(MAX)
    DECLARE @V_DT_INTRUST INT
    DECLARE @V_SW20409 INT, @IN_BOOK_CODE INT, @V_KEY_WORD NVARCHAR(200)
    SET @IN_BOOK_CODE = 1
    SELECT @V_DT_INTRUST = VALUE FROM TSYSCONTROL WHERE FLAG_TYPE = N'DT_INTRUST'
    SELECT @V_SW20409 = VALUE FROM INTRUST..TSYSCONTROL WHERE FLAG_TYPE = 'SW20409'
    DECLARE @V_PRODUCTS TABLE(PRODUCT_ID INT)
    IF @V_SW20409 = 1
        INSERT INTO @V_PRODUCTS
            SELECT PRODUCT_ID FROM INTRUST..TPRODUCT WHERE BOOK_CODE = @IN_BOOK_CODE
                AND(INTRUST_FLAG1 IN ( SELECT -(FUNC_ID-2040900)%2+2 FROM INTRUST..TOPRIGHT WHERE MENU_ID = '20409' AND FUNC_ID >= 2040900 AND FUNC_ID <= 2040999 AND OP_CODE = @IN_INPUT_MAN )
                    OR PRODUCT_ID IN ( SELECT FUNC_ID FROM INTRUST..TOPRIGHT WHERE MENU_ID = '20409' AND FUNC_ID < 2040900 AND OP_CODE = @IN_INPUT_MAN)
                    OR ADMIN_MANAGER = @IN_INPUT_MAN OR INPUT_MAN = @IN_INPUT_MAN OR ISNULL(@IN_INPUT_MAN,0)=0
                    OR ADMIN_MANAGER IN(SELECT OP_CODE FROM INTRUST..TOPCUSTRIGHT WHERE ENABLE_OP_CODE = @IN_INPUT_MAN)
                    )
    CREATE TABLE #TMP_INFOREPOS(
        DATA_TYPE           INT,            --信息类别 1产品 2预发行产品 3知识库内容
        PRODUCT_ID          INT,            --产品ID
        PREPRODUCT_ID       INT,            --预发行产品ID
        FAQ_ID              INT,            --知识ID
        TITLE               NVARCHAR(2000),  --标题
        CLASS_NO            NVARCHAR(10),   --仅DATA_TYPE=3时有效
        CLASS_NAME          NVARCHAR(30),   --仅DATA_TYPE=3时有效
        BRIEF_SUMMARY       NVARCHAR(2000), --简短描述，用于列表页面简要列示关键信息
        STATUS_NAME         NVARCHAR(200),  --状态描述
        CLASS_TYPE1         NVARCHAR(10),   --产品分类(1113)
        CLASS_TYPE1_NAME    NVARCHAR(30),   --产品分类：固定收益类产品、阳光私募类产品、PE类产品
        INPUT_TIME          DATETIME
    )
    DECLARE @VT_SEARCHKEY TABLE(KEY_WORD NVARCHAR(200), KEY_WORD_SAVE NVARCHAR(200))
    DECLARE @V_SEARCHKEYCOUNT INT
    INSERT INTO @VT_SEARCHKEY(KEY_WORD,KEY_WORD_SAVE)
        SELECT STUFF((SELECT '%'+TEXTSTR FROM dbo.F_SPLIT(@IN_SEARCHKEY,' ') C FOR XML PATH('')),1,1,''),
               STUFF((SELECT ' '+TEXTSTR FROM dbo.F_SPLIT(@IN_SEARCHKEY,' ') C FOR XML PATH('')),1,1,'')
    DELETE FROM @VT_SEARCHKEY WHERE KEY_WORD IS NULL
    SELECT @V_SEARCHKEYCOUNT = COUNT(*) FROM @VT_SEARCHKEY

    IF @IN_INCLUDE_PRODUCT = 1
    BEGIN --搜索产品信息start
        --先取“预发行产品”表数据
        INSERT INTO #TMP_INFOREPOS(PREPRODUCT_ID,PRODUCT_ID,TITLE,INPUT_TIME,STATUS_NAME,CLASS_TYPE1,CLASS_TYPE1_NAME,BRIEF_SUMMARY)
            SELECT A.PREPRODUCT_ID,A.BIND_PRODUCT_ID,A.PREPRODUCT_NAME,A.INPUT_TIME,A.STATUS_NAME,CLASS_TYPE1,CLASS_TYPE1_NAME,SUBSTRING(
                  N'期限：' + ISNULL(PERIOD,'') + N'；'
                + N'预约开始日：' + ISNULL(CONVERT(VARCHAR(10),EXPRE_START_TIME,120),N'无') + N'；'
                + N'预募集规模：' + CONVERT(NVARCHAR(20),CONVERT(DEC(16,2),ISNULL(PRE_MONEY,0)/10000)) + N'万元；'
                + N'收益分配时间：' + ISNULL(PROFIT_DATE,N'无') + N'；'
                + N'预期收益率：' + ISNULL(dbo.clearhtml(CAST(PRE_RATE AS NVARCHAR)),N'无') + N'；'
                + N'资金运用方式：' + ISNULL(CASH_USETYPE,N'无') + N'；'
                + N'保障措施：' + ISNULL(ENSURE_METHOD,N'无') + N'；'
                + N'推介期：' + dbo.GETDATESTRYMD(PRE_START_DATE) + N'-' + dbo.GETDATESTRYMD(PRE_END_DATE) + N'；'
                ,1,500)
            FROM EFCRM..TPREPRODUCT A
            WHERE (A.CLASS_TYPE1 = @IN_CLASS_TYPE1 OR @IN_CLASS_TYPE1 = '')
                AND(A.PRE_START_DATE BETWEEN @IN_PRE_DATE1 AND @IN_PRE_DATE2 OR A.PRE_END_DATE BETWEEN @IN_PRE_DATE1 AND @IN_PRE_DATE2)
                AND(A.EXPRE_START_TIME BETWEEN @IN_PRESTART_TIME1 AND @IN_PRESTART_TIME2)
                AND(EXISTS(SELECT 1 FROM @VT_SEARCHKEY Z WHERE (A.PREPRODUCT_NAME LIKE '%'+Z.KEY_WORD+'%' OR A.SUMMARY LIKE '%'+Z.KEY_WORD+'%')) OR @V_SEARCHKEYCOUNT = 0)
                AND(@IN_PRODUCT_STATUS=0 OR (@IN_PRODUCT_STATUS=2 AND A.STATUS='110202') OR (@IN_PRODUCT_STATUS=3 AND A.STATUS='110203') OR (@IN_PRODUCT_STATUS=4 AND A.STATUS='110204'))
        --补充INTRUST..TPRODUCT中的记录
        INSERT INTO #TMP_INFOREPOS(PRODUCT_ID,TITLE,INPUT_TIME,STATUS_NAME,BRIEF_SUMMARY)
            SELECT A.PRODUCT_ID,A.PRODUCT_NAME,A.INPUT_TIME,A.PRODUCT_STATUS_NAME,SUBSTRING(
                  N'期限：' + CASE PERIOD_UNIT WHEN 1 THEN CONVERT(NVARCHAR(20),ISNULL(VALID_PERIOD,''))+N'日' WHEN 2 THEN CONVERT(NVARCHAR(20),ISNULL(VALID_PERIOD,''))+N'个月' WHEN 3 THEN CONVERT(NVARCHAR(20),ISNULL(VALID_PERIOD,''))+N'年' ELSE N'无期限' END + N'；'
                + N'预约开始日：无；'
                + N'预募集规模：' + CONVERT(NVARCHAR(20),CONVERT(DEC(16,2),ISNULL(PRE_MONEY,0)/10000)) + N'万元；'
                + N'收益分配时间：' + CASE WHEN BEN_PERIOD IS NOT NULL THEN CONVERT(NVARCHAR(30),BEN_PERIOD)+N'个月' ELSE N'无' END + N'；'
                + N'预期收益率：' + CONVERT(NVARCHAR(20),CONVERT(DEC(16,2),ISNULL(EXP_RATE1,0)))+'-'+CONVERT(NVARCHAR(20),CONVERT(DEC(16,2),ISNULL(EXP_RATE2,0))) + N'；'
                + N'资金运用方式：' + ISNULL(INTRUST_TYPE1_NAME,N'无') + N'；'
                + N'保障措施：无；'
                + N'推介期：' + dbo.GETDATESTRYMD(PRE_START_DATE) + N'-' + dbo.GETDATESTRYMD(PRE_END_DATE) + N'；'
                ,1,500)
            FROM INTRUST..TPRODUCT A
            WHERE NOT EXISTS(SELECT 1 FROM EFCRM..TPREPRODUCT Z WHERE A.PRODUCT_ID = Z.BIND_PRODUCT_ID)
                AND(EXISTS(SELECT 1 FROM @VT_SEARCHKEY Z WHERE (A.PRODUCT_NAME LIKE '%'+Z.KEY_WORD+'%' OR A.SUMMARY LIKE '%'+Z.KEY_WORD+'%')) OR @V_SEARCHKEYCOUNT = 0 )
                AND(A.PRE_START_DATE BETWEEN @IN_PRE_DATE1 AND @IN_PRE_DATE2 OR A.PRE_END_DATE BETWEEN @IN_PRE_DATE1 AND @IN_PRE_DATE2)
                AND(@IN_PRODUCT_STATUS=0 OR (@IN_PRODUCT_STATUS=2 AND A.PRODUCT_STATUS='110202') OR (@IN_PRODUCT_STATUS=3 AND A.PRODUCT_STATUS='110203') OR (@IN_PRODUCT_STATUS=4 AND A.PRODUCT_STATUS='110204'))
                AND(@IN_OPENFLAG=0 OR (@IN_OPENFLAG=1 AND A.OPEN_FLAG=1) OR (@IN_OPENFLAG=2 AND A.OPEN_FLAG=2))
        UPDATE #TMP_INFOREPOS SET DATA_TYPE = 2 WHERE PREPRODUCT_ID IS NOT NULL
        UPDATE #TMP_INFOREPOS SET DATA_TYPE = 1 WHERE PRODUCT_ID IS NOT NULL
    END --搜索产品信息end

    IF @IN_INCLUDE_WIKI = 1
    BEGIN --搜索知识库start
        INSERT INTO #TMP_INFOREPOS(DATA_TYPE,FAQ_ID,TITLE,INPUT_TIME,CLASS_NO,CLASS_NAME,BRIEF_SUMMARY,STATUS_NAME)
            SELECT 3,SERIAL_NO,FAQ_TITLE,INPUT_TIME,CLASS_NO,CLASS_NAME,dbo.clearhtml(CONVERT(NVARCHAR(4000),A.FAQ_CONTENT)),
                   CONVERT(NVARCHAR(30),INPUT_TIME,120)+'  '+ISNULL(CONVERT(NVARCHAR(30),POPULARITY_RATING)+'%','')
            FROM EFCRM..V_FAQS A
            WHERE (EXISTS(SELECT 1 FROM @VT_SEARCHKEY Z WHERE (A.FAQ_TITLE LIKE '%'+Z.KEY_WORD+'%' OR A.FAQ_KEYWORDS LIKE '%'+Z.KEY_WORD+'%' OR A.FAQ_CONTENT LIKE '%'+Z.KEY_WORD+'%')) OR @V_SEARCHKEYCOUNT = 0 )
                AND(CLASS_NO = @IN_WIKI_CLASS OR @IN_WIKI_CLASS = '')
        UPDATE EFCRM..V_FAQS SET MATCH_TIMES = MATCH_TIMES + 1
            FROM EFCRM..V_FAQS A
            WHERE (EXISTS(SELECT 1 FROM @VT_SEARCHKEY Z WHERE (A.FAQ_TITLE LIKE '%'+Z.KEY_WORD+'%' OR A.FAQ_KEYWORDS LIKE '%'+Z.KEY_WORD+'%' OR A.FAQ_CONTENT LIKE '%'+Z.KEY_WORD+'%')) OR @V_SEARCHKEYCOUNT = 0 )
                AND(CLASS_NO = @IN_WIKI_CLASS OR @IN_WIKI_CLASS = '')
    END --搜索知识库end
    --如果有搜索到内容，记录本次关键字
    IF EXISTS(SELECT 1 FROM #TMP_INFOREPOS)
    BEGIN
        INSERT INTO TINFOREPOS_KEYWORDS(KEY_WORD,SEARCH_TIMES)
            SELECT KEY_WORD_SAVE,0 FROM @VT_SEARCHKEY A WHERE NOT EXISTS(SELECT 1 FROM TINFOREPOS_KEYWORDS Z WHERE A.KEY_WORD_SAVE = Z.KEY_WORD)
        UPDATE TINFOREPOS_KEYWORDS SET SEARCH_TIMES = SEARCH_TIMES + 1, SEARCH_TIME = GETDATE()
            FROM TINFOREPOS_KEYWORDS A WHERE EXISTS(SELECT 1 FROM @VT_SEARCHKEY Z WHERE A.KEY_WORD = Z.KEY_WORD_SAVE)
    END
    --去掉描述中的html标签
    UPDATE #TMP_INFOREPOS SET BRIEF_SUMMARY = dbo.clearhtml(BRIEF_SUMMARY)
    --搜索关键词高亮
    IF @V_SEARCHKEYCOUNT > 0
    BEGIN
        DECLARE CUR1 CURSOR FOR SELECT TEXTSTR FROM dbo.F_SPLIT(@IN_SEARCHKEY,' ')
        OPEN CUR1
        FETCH NEXT FROM CUR1 INTO @V_KEY_WORD
        WHILE @@FETCH_STATUS = 0
        BEGIN
            UPDATE #TMP_INFOREPOS SET TITLE = REPLACE(TITLE,@V_KEY_WORD,'<FONT style="BACKGROUND-COLOR: #ffff80">'+@V_KEY_WORD+'</font>'),
                    BRIEF_SUMMARY = REPLACE(BRIEF_SUMMARY,@V_KEY_WORD,'<FONT style="BACKGROUND-COLOR: #ffff80">'+@V_KEY_WORD+'</font>')
            FETCH NEXT FROM CUR1 INTO @V_KEY_WORD
        END
        CLOSE CUR1
        DEALLOCATE CUR1
    END
    SET @V_SQL = 'SELECT * FROM #TMP_INFOREPOS WHERE 1 = 1 '
    IF @IN_CLASS_TYPE1 <> ''
        SET @V_SQL = @V_SQL + ' AND(CLASS_TYPE1 = ''' + @IN_CLASS_TYPE1 + ''')'
    IF @IN_SORTFIELD IS NOT NULL AND @IN_SORTFIELD <> ''
        SET @V_SQL = @V_SQL + ' ORDER BY ' + @IN_SORTFIELD
    --ELSE
    --    SET @V_SQL = @V_SQL + ' ORDER BY INPUT_TIME DESC'
    EXECUTE( @V_SQL )
GO
