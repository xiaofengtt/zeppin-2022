USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_CHECK_TMONEYDATA_INPUT @IN_SERIAL_NO    INT,    --记录号
                                     @IN_CHECK_FLAG     INT,    --审核1未审核;2录入审核通过
                                     @IN_INPUT_MAN      INT     --操作员
WITH ENCRYPTION
AS
    --SET NOCOUNT ON
    DECLARE @V_RET_CODE INT,@IBUSI_FLAG INT,@SBUSI_NAME NVARCHAR(40),@SSUMMARY NVARCHAR(200)
    DECLARE @V_PRODUCT_ID INT,@V_SUB_PRODUCT_ID INT,@V_PRE_NUM INT,@V_PRE_MONEY DEC(18,3)
    DECLARE @V_INPUT_MAN INT,@V_CHECK_FLAG INT
    SELECT @SBUSI_NAME = N'审核资金输入'
    SELECT @SSUMMARY = N'审核资金输入'
    SELECT @IBUSI_FLAG = 39007
    
    BEGIN TRY
    SELECT @V_PRODUCT_ID=PRODUCT_ID, @V_SUB_PRODUCT_ID=SUB_PRODUCT_ID,@V_INPUT_MAN=INPUT_MAN,@V_CHECK_FLAG=CHECK_FLAG
      FROM INTRUST..TMONEYDATA 
      WHERE SERIAL_NO=@IN_SERIAL_NO
    IF @@ROWCOUNT=0
        RAISERROR('资金输入信息不存在',16,1)
    IF @V_INPUT_MAN=@IN_INPUT_MAN
		RAISERROR('不能自己给自己审核',16,1)
	IF @V_CHECK_FLAG=2
		RAISERROR('财务己审核，录入不能修改，或请财务恢复审核',16,1)
    
    BEGIN TRANSACTION
        IF @IN_CHECK_FLAG=2 --录入审核通过
			UPDATE INTRUST..TMONEYDATA SET CHECK_FLAG=1,INPUT_CHECK_FLAG = @IN_CHECK_FLAG, INPUT_CHECK_MAN = @IN_INPUT_MAN WHERE SERIAL_NO = @IN_SERIAL_NO
        ELSE
			UPDATE INTRUST..TMONEYDATA SET INPUT_CHECK_FLAG = @IN_CHECK_FLAG, INPUT_CHECK_MAN = @IN_INPUT_MAN WHERE SERIAL_NO = @IN_SERIAL_NO
	
	SET @SSUMMARY = N'审核资金输入，操作员：' + CAST(@IN_INPUT_MAN AS NVARCHAR(10))+'，记录号：'+CAST(@IN_SERIAL_NO AS VARCHAR)
    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@IBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    
    COMMIT TRANSACTION
    END TRY

    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION

        DECLARE @V_ERROR_STR NVARCHAR(1000),@V_ERROR_NUMBER INT,@V_ERROR_SEVERITY INT,@V_ERROR_STATE INT,
                @V_ERROR_PROCEDURE sysname,@V_ERROR_LINE INT,@V_ERROR_MESSAGE NVARCHAR(4000)

        SELECT @V_ERROR_STR = N'Message:%s,Procedure:%s,Line:%d',
               @V_ERROR_NUMBER = ERROR_NUMBER(),
               @V_ERROR_SEVERITY = ERROR_SEVERITY(),
               @V_ERROR_STATE = ERROR_STATE(),
               @V_ERROR_PROCEDURE = ERROR_PROCEDURE(),
               @V_ERROR_LINE = ERROR_LINE(),
               @V_ERROR_MESSAGE = ERROR_MESSAGE()

        RAISERROR(@V_ERROR_STR,@V_ERROR_SEVERITY,1,@V_ERROR_MESSAGE,@V_ERROR_PROCEDURE,@V_ERROR_LINE)

        RETURN -100
    END CATCH
    RETURN 100
GO
