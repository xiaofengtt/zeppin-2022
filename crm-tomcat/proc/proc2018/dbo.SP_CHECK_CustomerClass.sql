USE EFCRM
GO
SET ANSI_NULLS, QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE SP_CHECK_CustomerClass @IN_CUST_ID INT,              --客户ID
                                        @IN_CLASSDEFINE_ID INT,       --评级ID
                                        @IN_CLASSDETAIL_ID INT,       --评级明细ID
                                        @IN_CHECK_FLAG INT,           --审核标志1未审核，2已审核
                                        @IN_INPUT_MAN INT             --操作员
WITH ENCRYPTION
AS
    DECLARE @V_RET_CODE INT, @SIBUSI_FLAG INT, @SBUSI_NAME NVARCHAR(40), @SSUMMARY NVARCHAR(200)
    DECLARE @V_PreviousDETAIL_ID INT, @V_PreviousDETAIL_NAME NVARCHAR(60)

    SELECT @SIBUSI_FLAG = 23001
    SELECT @SBUSI_NAME = N'客户评级审核'
    SELECT @SSUMMARY = N'客户评级审核'
    SELECT @V_RET_CODE = -50305000

    IF NOT EXISTS(SELECT * FROM TCustomerClass WHERE CUST_ID = @IN_CUST_ID AND CLASSDEFINE_ID = @IN_CLASSDEFINE_ID AND CLASSDETAIL_ID = @IN_CLASSDETAIL_ID)
        RETURN @V_RET_CODE-1   -- 记录不存在

    BEGIN TRANSACTION

    IF(@IN_CHECK_FLAG = 1) --审核不通过，则保存修改前的状态
    BEGIN
        SELECT @V_PreviousDETAIL_ID = PreviousDETAIL_ID, @V_PreviousDETAIL_NAME = PreviousDETAIL_NAME
            FROM TCustomerClass
            WHERE CUST_ID = @IN_CUST_ID AND CLASSDEFINE_ID = @IN_CLASSDEFINE_ID AND CLASSDETAIL_ID = @IN_CLASSDETAIL_ID

        UPDATE  TCustomerClass
            SET CHECK_MAN = @IN_INPUT_MAN, CHECK_TIME = GETDATE(), CHECK_FLAG = 1, --审核不通过
                CLASSDETAIL_ID = @V_PreviousDETAIL_ID, CLASSDETAIL_NAME = @V_PreviousDETAIL_NAME
            WHERE CUST_ID = @IN_CUST_ID AND CLASSDEFINE_ID = @IN_CLASSDEFINE_ID AND CLASSDETAIL_ID = @IN_CLASSDETAIL_ID
        IF @@ERROR <> 0
        BEGIN
             ROLLBACK TRANSACTION
             RETURN -100
        END
        SELECT @SSUMMARY = N'客户评级审核不通过'
    END
    ELSE    --审核通过，则存储修改后的信息
    BEGIN
        UPDATE TCustomerClass
               SET CHECK_MAN = @IN_INPUT_MAN, CHECK_TIME = GETDATE(), CHECK_FLAG = 2 --审核通过
            WHERE CUST_ID = @IN_CUST_ID AND CLASSDEFINE_ID = @IN_CLASSDEFINE_ID AND CLASSDETAIL_ID = @IN_CLASSDETAIL_ID
        IF @@ERROR <> 0
        BEGIN
             ROLLBACK TRANSACTION
             RETURN -100
        END
        --20110426 dongyg 保存评级变动
        INSERT INTO TCUSTCLASSLOG(CUST_ID,PreviousDETAIL_ID,PreviousDETAIL_NAME,CLASSDETAIL_ID,CLASSDETAIL_NAME,INSERTMAN,INSERTTIME,CHECKMAN,CHECKTIME)
            SELECT CUST_ID,PreviousDETAIL_ID,PreviousDETAIL_NAME,CLASSDETAIL_ID,CLASSDETAIL_NAME,INSERTMAN,INSERTTIME,CHECK_MAN,CHECK_TIME
            FROM TCustomerClass
            WHERE CUST_ID = @IN_CUST_ID AND CLASSDEFINE_ID = @IN_CLASSDEFINE_ID AND CLASSDETAIL_ID = @IN_CLASSDETAIL_ID
        IF @@ERROR <> 0
        BEGIN
             ROLLBACK TRANSACTION
             RETURN -100
        END
        SELECT @SSUMMARY = N'客户评级审核通过'
    END

    INSERT INTO TLOGLIST(BUSI_FLAG,BUSI_NAME,OP_CODE,SUMMARY)
        VALUES(@SIBUSI_FLAG,@SBUSI_NAME,@IN_INPUT_MAN,@SSUMMARY)
    IF @@ERROR <> 0
    BEGIN
        ROLLBACK TRANSACTION
        RETURN -100
    END
    COMMIT TRANSACTION
    RETURN 100
GO
